// This file was decompiled using sascm.ini published by Seemann (http://sannybuilder.com/files/SASCM.rar) on 23.02.2007
DEFINE OBJECTS 1
DEFINE OBJECT SANNY BUILDER v. 2.99f  

DEFINE MISSIONS 0

DEFINE EXTERNAL_SCRIPTS 0 // Use -1 in order not to compile AAA script

DEFINE UNKNOWN_EMPTY_SEGMENT 0

DEFINE UNKNOWN_THREADS_MEMORY 0

{$VERSION 3.1.0020}

//-------------MAIN---------------
thread 'MAIN' 
fade 0 0 
01F0: set_max_wanted_level_to 6 
set_wb_check_to 0 
00C0: set_current_time_hours_to 8 minutes_to 0 
04E4: unknown_refresh_game_renderer_at 2488.56 -1666.84 
Camera.SetAtPos(2488.56, -1666.84, 13.38)
$PLAYER_CHAR = Player.Create(#NULL, 2488.56, -1666.84, 13.38)
$PLAYER_ACTOR = Actor.EmulateFromPlayer($PLAYER_CHAR)
07AF: $PLAYER_GROUP = player $PLAYER_CHAR group 
Camera.SetBehindPlayer
set_weather 0 
wait 0 
Player.SetClothes($PLAYER_CHAR, "PLAYER_FACE", "HEAD", Head)
Player.SetClothes($PLAYER_CHAR, "JEANSDENIM", "JEANS", Legs)
Player.SetClothes($PLAYER_CHAR, "SNEAKERBINCBLK", "SNEAKER", Shoes)
Player.SetClothes($PLAYER_CHAR, "VEST", "VEST", Torso)
Player.Build($PLAYER_CHAR)
Player.CanMove($PLAYER_CHAR) = True
//fade 1 0 
select_interior 0 
0629: change_integer_stat 181 to 4 
016C: restart_if_wasted_at 2027.77 -1420.52 15.99 angle 137.0 town_number 0 
016D: restart_if_busted_at 1550.68 -1675.49 14.51 angle 90.0 town_number 0 
0180: set_on_mission_flag_to $ONMISSION // Note: your missions have to use the variable defined here 
03E6: remove_text_box 
create_thread @CreateActor 
end_thread 

:CreateActor
thread "CreateActor"  
//даем название потоку, это нужно для того, чтобы была возможность вызывать этот поток из любого другого потока или миссии 
player.CanMove($PLAYER_CHAR)=False 
//отключаем управление игроком 
02A3: toggle_widescreen 1 
// включаем широкоэкранный режим
Model.Load(#CSHER)   
Model.Load(#WMOPJ)
// запрашиваем модели 
038B: load_requested_models 
// загружаем запрошенные модели

:CreateActor_31
wait 0 
if and 
// проверка выполнится, если ВСЕ условия, входящие в нее, верны
   Model.Available(#CSHER)   
   Model.Available(#WMOPJ) 
//проверяем загружены ли модели 
jf @CreateActor_31 
//если нет, то идем назад на метку @CreateActor_31 
wait 250 
$acror1 = Actor.Create(Cop, #CSHER, 2484.56, -1665.84, 12.28)
 //создаем актера в заданных координатах
Actor.Health($acror1) = 300 
//устанавливаем здоровье актеру $acror1
$acror1_ANGLE = Actor.Angle($acror1) 
//записываем в переменную $acror1_ANGLE угол актера $acror1
07FE: set_actor $acror1 fighting_style_to 7 6 
//устанавливаем стиль борьбы актеру $acror1
04C4: store_coords_to $X $Y $Z from_actor $acror1 with_offset 0.0 2.0 0.0 
//создаем координаты $X $Y $Z относительно актёра $acror1 со смещением 0.0 2.0 0.0
02CE: $Z = ground_z_at $X $Y $Z
//получить Z земли_для 3D точки $X $Y $Z,
// это нужно для того, чтобы актер $acror2 создавался на земле, а не падал на нее 
04C4: store_coords_to $Camera_X1 $Camera_Y2 $Camera_Z3 from_actor $PLAYER_ACTOR with_offset 0.0 0.0 2.0 
//создаем координаты камеры относительно игрока
$acror1_ANGLE -= 180.0 
//необходимо для того, чтобы актер $acror2 был повернут лицом к $acror1  
$acror2 = Actor.Create(Dealer, #WMOPJ, $X, $Y, $Z)
//создаем $acror2 в координатах $X, $Y, $Z с учетои Z земли
Actor.Angle($acror2) = $acror1_ANGLE 
//устанавливаем угол $acror1_ANGLE актеру $acror2
Actor.Health($acror2) = 1000 
//устанавливаем здоровье актеру $acror2
07FE: set_actor $acror2 fighting_style_to 6 6 
//устанавливаем стиль борьбы актеру $acror2
Camera.SetPosition($Camera_X1, $Camera_Y2, $Camera_Z3, 0.0, 0.0, 0.0)
// устанавливаем камеру
Camera.OnPed($acror1, 15, 2) 
//камера будет направлена на актера $acror1
fade 1 0 
wait 2000 
Actor.PutAt($PLAYER_ACTOR, 2494.767, -1685.347, 13.5118) 
//перемещаем игрока, чтобы не мешал
05E2: AS_actor $acror2 kill_actor $acror1   //актер $acror2 атакует $acror1
05E2: AS_actor $acror1 kill_actor $acror2   

:CreateActor_310
wait 0 
if or  
//проверка сработает тогда, когда будет выполнено одно из условий
   Actor.Dead($acror1) 
   // проверяем умер ли актер $acror1
   Actor.Dead($acror2) 
   // проверяем умер ли актер $acror1
jf @CreateActor_310  
wait 1000 
Actor.DestroyWithFade($acror1) 
//удаляем актера (Актёр исчезает как призрак)
Actor.DestroyWithFade($acror2) 
Model.Destroy(#CSHER)   
Model.Destroy(#WMOPJ)
//удаляем модели    
Actor.PutAt($PLAYER_ACTOR, 2488.56, -1666.84, 12.38) 
//возвращаем игрока на прежнее место
player.CanMove($PLAYER_CHAR)= true   
//включаем возможность управления игроком
wait 1000 
Camera.Restore_WithJumpCut  
//восстановить последнюю позицию камеры 
02A3: toggle_widescreen 0 
//отключаем широкоэкранный режим
end_thread  // завершаем поток
