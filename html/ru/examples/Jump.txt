

{
  use macro (Ctrl+J) "headsa"
  to insert a file header      
}
{$VERSION 3.0.0000}
thread 'Main' 
var 
$PLAYER_CHAR: Player 
end // var 
01F0: set_max_wanted_level_to 6 
set_wb_check_to 0 
00C0: set_current_time 8 0 
04E4: unknown_refresh_game_renderer_at 2488.5601 -1666.84 
Camera.SetAtPos(2488.5601, -1666.84, 13.38) 
$PLAYER_CHAR = Player.Create(#NULL, 2488.5601, -1666.84, 13.38) 
$PLAYER_ACTOR = Actor.EmulateFromPlayer($PLAYER_CHAR) 
Camera.SetBehindPlayer 
set_weather 0 
wait 0 ms 
$PLAYER_CHAR.SetClothes("PLAYER_FACE", "HEAD", Head) 
$PLAYER_CHAR.SetClothes("JEANSDENIM", "JEANS", Legs) 
$PLAYER_CHAR.SetClothes("SNEAKERBINCBLK", "SNEAKER", Shoes) 
$PLAYER_CHAR.SetClothes("VEST", "VEST", Torso) 
$PLAYER_CHAR.Build 
$PLAYER_CHAR.CanMove = True 
fade 1 (out) 0 ms 
select_interior 0 
0629: change_stat 181 (islands unlocked) to 4 
016C: restart_if_wasted at 2027.77 -1420.52 15.99 angle 137.0 unknown 0 
016D: restart_if_busted at 1550.68 -1675.49 14.51 angle 90.0 unknown 0 
0180: set_on_mission_flag_to $ONMISSION
// put your create_thread commands here 
create_thread @JumpT  // создаём поток  
end_thread 

// put your mission mods here 


:JumpT
03A4: name_thread 'JumpT'
03DE: set_pedestrians_density_multiplier_to 0.0 
$CHECK = 0

:JumpT_1
wait 0 ms 
04C4: create_coordinate $X $Y $Z from_actor $PLAYER_ACTOR offset 1.0 3.0 0.0 
$Z -=1.1 
00BE: text_clear_all 
054C: use_GXT_table 'FARLIE3' 
00BC: text_highpriority 'DRI2_12' 5000 ms 1  // ~s~ Kill those ~r~goons.
gosub @Jump_table 
wait 1000
actor.Create($Actor,7,$MODEL,$X, $Y, $Z) 
actor.Health($Actor)= 1000
07FE: set_actor $Actor fighting_style_to 22@ 6 
if 
$CHECK ==3 
jf @JumpT_2
01B2: give_actor $Actor weapon 5 ammo 3000 // Load the weapon model before using this 

:JumpT_2
05E2: actor $Actor kill_actor $PLAYER_ACTOR

:JumpT_3 
wait 0 ms 
if 
actor.Dead($Actor) 
jf @JumpT_3 
wait 2000 ms 
actor.DestroyInstantly($Actor) 
$CHECK +=1 

:JumpT_4 
wait 0 ms 
if 
4 > $CHECK 
jf @JumpT_5
wait 1000 ms 
jump @JumpT_1

:JumpT_5
03DE: set_pedestrians_density_multiplier_to 1.0 
model.Destroy(#VHMYELV) 
model.Destroy(#VWMYBOX) 
model.Destroy(#WMYCR) 
model.Destroy(#OMYKARA) 
model.Destroy(#BAT) 
end_thread 

:Jump_table 
0871: init_jump_table $CHECK total_jumps 4 0 @EXIT_JT jumps 0 @JUMP_0 1 @JUMP_1 2 @JUMP_2 3 @JUMP_3 -1 @EXIT_JT -1 @EXIT_JT -1 @EXIT_JT 

:JUMP_0 
model.Load(#VHMYELV)
038B: load_requested_models 

:JUMP_0_1 
wait 0 ms 
if 
model.Available(#VHMYELV) 
jf @JUMP_0_1 
04AE: $MODEL = 82 
22@ = 5 
jump @EXIT_JT

:JUMP_1 
model.Load(#VWMYBOX)
038B: load_requested_models 

:JUMP_1_1 
wait 0 ms 
if 
model.Available(#VWMYBOX) 
jf @JUMP_1_1 
04AE: $MODEL = 81 
22@ = 6 
jump @EXIT_JT

:JUMP_2 
model.Load(#WMYCR)
038B: load_requested_models 

:JUMP_2_1 
wait 0 ms 
if model.Available(#WMYCR) 
jf @JUMP_2_1 
04AE: $MODEL = 100 
22@ = 7 
jump @EXIT_JT

:JUMP_3 
model.Load(#OMYKARA) 
model.Load(#BAT)
038B: load_requested_models 

:JUMP_3_1 
wait 0 ms 
if and 
model.Available(#OMYKARA) 
model.Available(#BAT) 
jf @JUMP_3_1 
04AE: $MODEL = 203 
22@ = 8 
jump @EXIT_JT

:EXIT_JT 
return 
