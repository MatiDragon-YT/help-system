{$CLEO}
var
  $Player_Actor: Actor
  $Player_Char: Player
  0@: Integer
end
const
  I = 0@
end
0000:
while true
  if 0AB0:  key_pressed 0x4A // VK_J
  then
    select_interior 0  
    $Player_Actor.PutAt(1945.2737, -2300.6677, 13.5469)
    Camera.SetAtPos(1945.6265, -2356.5925, 13.5469) // Для убыстрения загрузки
    wait 500
    015D: set_gamespeed 0.3333333 // Замедление времени  
    Camera.SetPosition(1945.1906, -2335.4775, 18.3014, 0.0, 0.0, 0.0)
    Camera.PointAt(1945.6265, -2356.5925, 13.5469, 2)
    for I = 0 to 6
      020C: create_explosion_with_radius 0 at 1945.6265 -2356.5925 13.5469
    end // для оставления чёрного следа
    wait 3000
    0826: enable_hud 0     // убираем HUD
    0581: enable_radar 0   //
    for I = 0 to 13
      031a: remove_all_fires // Для того, что-бы огонь не мешал посмотреть на взрывы
      0980: extinguish_all_fires_at 1945.6265 -2356.5925 13.5469 radius 10.0 
      020C: create_explosion_with_radius I at 1945.6265 -2356.5925 13.5469
      0513: show_text_box_1number 'NUMBER' number I // Вывод текст-бокса с номером взрыва
      wait 2600
    end
    Camera.Restore_WithJumpCut
    Camera.SetBehindPlayer
    015D: set_gamespeed 1.0
    0826: enable_hud 1
    0581: enable_radar 1      
  end
  wait 25 // что-бы не зависла
end







