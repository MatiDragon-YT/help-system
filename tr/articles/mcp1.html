<!DOCTYPE html>
<html lang="tr">
<head>
	<title>CLEO Kodlama İncili. Bolüm I</title>
	<meta charset="utf-8">
	<meta http-equiv="X-UA-Compatible" content="IE=edge">
	<meta name="viewport" content="width=device-width, initial-scale=1">
	<link rel="stylesheet" type="text/css" href="../../style/style.css">
</head>
<body>

:::center h3
Grand Theft Auto
[orange][San Andreas]
\n
[red][SannyBuilder ile Görev Kodlama Eğitimi]
[gray][Bölüm I]
[gray][Kodlamaya Başlangıç]
\
\
:::

## Öndeyiş 

Merhabalar, uzun zaman boyunca yeni kodlayanlara bir öğretici yapmak istemişimdir, ne yapmanızı gerektiğini değil de, ne yaptığınızda yardımcı olacak şeyi anlatacak. Ayrıca SannyBuilder içinde güzel bir öğretici olacak. Bunun gibi bir öğretici yazısı daha yazdım, kodlamaya hiç girişmeyenler varsa diye bu sayfa var. Eğer siz biliyorsanız [Bölüm II](mcp2.html) sayfasına atlayabilirsiniz, Eğer kodlamaya yeni değilseniz...

Görev kodlama ile (yada Görev entegre etmek) kendi görevlerinizi oluşturabilirsiniz, kendi modifikasyonlarınız için yada var olan modifikasyonlara (onları [Görev Modları](http://www.gtaforums.com/index.php?showforum=65) sayfasında bulabilir ve bu [Görev kodlama](http://www.gtaforums.com/index.php?showforum=49) sayfalarında (bu sayfada da görev kodlama hakkında sorularınızı sorabilirsiniz.).

Görev kodlamaları main.scm yardımı ile ve script.img dosyası ile yapılıyor ki bunlar San Andreas'ın data\script klasöründe bulunabilir. Main.scm derlenmiş ve paketlenmiş dosyadır, yani Bilgisayarın anlayabileceği türden yazılmış bir dosya türünde ve dilinde. Eğer main.scm dosyasını bir tür hexadecimal editor ile açarsanız içini şöyle göreceksiniz:
**KOD**

```
D6 00 04 00 19 01 02 45 0E 4D 00 01 FE 3D 87 02 A6 00 02 45 0E
```

İşte bu yüzden bunu bir insanın anlayacağı türden formatta derlenmesi lazım, ki üzerinde programlama yapalım. Unutmayın ki script.img dosyasını ellemenize gerek yok, ama dosya lazım, çünkü her main.scm kendi script.img dosyasına ihtiyaç duyuyor.

Main.scm'yi geri-derlememiz için gerekli alet tabiki [Sanny Builder](https://sannybuilder.com/). Tabiki birçok farklı geri-derleyen aletler var ama siz yeni bir kullanıcı iseniz Sanny kodlamak için en iyi seçenektir, çünkü çok yenileniyor, çok destek destek sağlıyor (bu çeviri gibi ☻), hızlı, ve baya rengarenk 😏!!!!! Ve bu öğretici yazısı [Sanny Builder](https://sannybuilder.com/) için yapılmıştır. 
Sanny Builder birçok yönü ile Barton Waterduck's San Andreas Mission builder aletine benziyor ama kodlama dizimi türü farklıdır. Kesinlikle ama kesinlike [Sanny Builder](https://sannybuilder.com/) kullanılmasını öneririm ama karşıt çıkıyorsanız, buyrun : [Barton's Waterduck Mission Builder](http://www.kostelecky.com/craig/SAMB033.rar) -Craig'in özel versiyonu, teşekkürler Craig.

Önemli olan bir diğer şey ise, bilgi edinmektir, Sanny Builder'ın Help aleti, birçok bilgiye sahip bu alet/program hakkında ve çeşitli özellikleri var. (İnternetsiz kullanabilmek gibi).

İndirici aleti EXE kısaltımlı dosyaya tıklayarak indirin. Ve ilk olarak Tools\options kısmına girin (ya da direk F10 basın). General ya da Genel penceresinde Show progress, Show report, Show warning, Quick game loading, Condition check, Ranges check, Writes opcodes, maybe Replace mission numbers, Insert Original mission names, Add extra info to SCM ayarlarını tikleyin. Onlara neden ihtiyaç duyduğumuzu sonradan anlatacağım.

Bunun gibi yapın:

:::l
![](mcp1/rdfgbh3.png)
:::

Sonra programı bir kapat-aç yapın.

Size yardım edebilecek birçok öğretici var: [YeTi's SA Coding Tutorial Part One](http://www.gtaforums.com/index.php?showtopic=214055&view=findpost&%20%20p=3163789), [YeTi's SA Coding Tutorial Part Two](http://www.gtaforums.com/index.php?showtopic=214321&view=findpost&%20%20p=3167813), [Y_less quick tuto](http://www.gtaforums.com/index.php?showtopic=232687&view=findpost&%20%20p=3498916), bunlar San Andreas için. Vice City öğreticileri de size yardımcı olabilir: [Alex "Y_Less" Cole's n00b guide to coding](http://www.gtaforums.com/index.php?showtopic=169526&view=findpost&%20%20p=2352371), [littleguna's VC Coding Tutorial](http://www.gtaforums.com/index.php?showtopic=156033&view=findpost&%20%20p=2137684), [YeTi's VC Coding Tutorial Part One](http://www.gtaforums.com/index.php?showtopic=189876&view=findpost&%20%20p=2794115), [YeTi's VC Coding Tutorial Part Two](http://www.gtaforums.com/index.php?showtopic=191001&view=findpost&%20%20p=2819222). The [GTAforums tutorial archive](http://www.gtaforums.com/index.php?showtopic=141222&view=findpost&%20%20p=1834859). Daha fazla öğretici bu forumlarda bulabilirsiniz. 
You can visit [GTAmodding.com](http://www.gtamodding.com/) and particulary [Mission scripting](http://www.gtamodding.com/index.php?title=Category:Mission_Scripti%20%20ng) category.

### 1. Main.scm'nin mimarisi

Şimdi orijinal main.scm ve script.img'yi yeni bir kodlama çalışma klasörüne kopyalayın (veya istediğiniz herhangi bir yer: masaüstü, Belgelerim, geri dönüşüm kutusu, annenizin belgeleri 😁 ....) oluşturdunuz ve şimdi açınız (yedekleme yapın !!!!! orijinal main.scm ve script.img için hemen yeni bir klasör oluşturun) aç'a tıklayarak ve SCM Dosyasını seçin (*.scm). İlerleme çubuğunun dolduğunu görebilirsiniz (Bu nedenle "İlerlemeyi göster" seçeneğini kapatalım). 

#### Tanım listesi bölümü:

Açtığınızda ise şunu göreceksiniz:\
**KOD**

```sb3
// This file was decompiled using sascm.ini published by Seemann (http://sannybuilder.com/files/SASCM.rar) on 13.10.2007
DEFINE OBJECTS 389
DEFINE OBJECT SANNY BUILDER 3.03     
DEFINE OBJECT INFO                     // Object number -1
DEFINE OBJECT KEYCARD                  // Object number -2
DEFINE OBJECT AD_FLATDOOR              // Object number -3
DEFINE OBJECT KB_BANDIT_U              // Object number -4
etc...
```

Gördüğünüz gibi main.scm içinde kullanılacak nesneleri tanımlıyor, üstte söylendiği gibi 389 tane var (DEFINE OBJECTS 389). Bir nesne eklemek için sonuncuya gidin ve kendinizinkini ekleyin.

Daha aşağıda (çok daha aşağıda) bunu bulabilirsiniz:\
**KOD**

```sb3
DEFINE MISSIONS 135
DEFINE MISSION 0 AT @INITIAL
DEFINE MISSION 1 AT @INITIL2
DEFINE MISSION 2 AT @INTRO
DEFINE MISSION 3 AT @NONE
etc...
```

Bu, görevleri tanımlar, üstte söylendiği gibi 135 tane var. (`DEFINE MISSIONS 135`). Xbox, Playstation 2 veya [jarjar 2-player](http://www.gtaforums.com/index.php?showtopic=208450&view=findpost&%20%20p=3069233) mod dosyalarında 2 oyunculu konsol görevi olan daha fazla görev olduğunu görebilirsiniz ama bu başka bir hikaye...\
\
**KOD**
```sb3
DEFINE EXTERNAL_SCRIPTS 78 // Use -1 in order not to compile AAA script
DEFINE SCRIPT PLAYER_PARACHUTE AT @PLCHUTE // 0
DEFINE SCRIPT PARACHUTE AT @PARACH // 1
DEFINE SCRIPT BCESAR2 AT @BCESAR2 // 2
DEFINE SCRIPT BCESAR3 AT @COKEC // 3
DEFINE SCRIPT SLOT_MACHINE AT @BANDIT // 4
etc...
```

Bu, gerektiğinde tetiklenen komut dosyaları (programlar) olan harici komut dosyalarını tanımlar, yukarıda belirtildiği gibi 78 tane vardır. Paraşütü idare eden şey bu (player -`PLAYER_PARACHUTE`- and non-players -`PARACHUTE`-), Kumarhanedeki oyunlar (`ROULETTE`, `VIDPOK` -Video Poker-, `BLACKJ` -Black Jack-, `WHEELO` -Şans Çarkı-, etc...), Dükkanlar (`AMMU`, `TATTOO`, `BARBER`, `JUNKFUD`, `CARMOD`), ve birçok değişik şey yada çokta değişik olmayan (`DEBT`, `STRIPW`, `GF_SEX`, `HOME_BRAINS`...) şeylerde var ama sonra anlatacağım bunları.

Sonra bu 2 satıra geliyorsunuz:\
**CODE**
```sb3
DEFINE UNKNOWN_EMPTY_SEGMENT 0

DEFINE UNKNOWN_THREADS_MEMORY 574
```

Bunlar belgelenmemiş ve bizim için yararsız.

#### ANAYAPIT (MAIN) kısmı:

Now you can see something like this:\
**CODE**
```sb3
{$VERSION 3.1.0027}
//-------------MAIN---------------
03A4: name_thread 'MAIN' 
016A: fade 0 time 0 
042C: set_total_missions_to 147 
030D: set_max_progress 187
0997: set_total_respect_points_to 1339
01F0: set_max_wanted_level_to 6
0111: set_wasted_busted_check_to 0
00C0: set_current_time_hours_to 8 current_time_minutes_to 0
04E4: unknown_refresh_game_renderer_at 2488.562 -1666.864
03CB: set_rendering_origin_at 2488.562 -1666.864 13.3757
062A: change_stat 165 to 800.0 // floating-point values
062A: change_stat 23 to 50.0 // floating-point values
062A: change_stat 21 to 200.0 // floating-point values
062A: change_stat 160 to 0.0 // floating-point values
0629: change_stat 181 to 4 // integer values
0629: change_stat 68 to 0 // integer values
0053: $PLAYER_CHAR = create_player #NULL at 2488.562 -1666.864 12.8757
etc...
```

**Oraya** kadar (Sanny arama aracını kullanın ve "görev 0"ı arayın):\
**CODE** 
```sb3
//-------------Mission 0---------------
// Originally: Initial 1
:INITIAL
03A4: name_thread 'INITIAL'
06C8: enable_riot 0
0004: $1515 = 0 
0005: $1500 = 5.0
etc...
```

Ana bölüm veya ana bölüm olarak adlandırılır (ve bazen ana iş parçacığı olarak adlandırılan belirsizlikle). Bunda, örneğin görevleri başlatan kırmızı daireleri oluşturan, telefon görüşmelerini yöneten şey ('CELLFON' ve diğer şeyler), grilfriends'i yöneten, her şeyin tetiklendiği (başlatıldığı) yer olan pek çok küçük komut dosyası vardır. birçok mod eklendi ve kodlamaya buradan başlayacaksınız. Ana bölüm birçok küçük iplikten oluşur, ancak ana konu ile başlar. Ve iş parçacığı (ve tek iş parçacığı) motorun kendisi tarafından oluşturulur, onu başlatmak için diğerleri gibi "004F" (**create_thread**) iş parçacıkları oluşturan işlem kodunu kullanmanız gerekmez. Endişelenmeyin, daha sonra tüm bölüm konulara ayrılacaktır.

Ana bölümde yalnızca yerel değişkenleri kullanabilirsiniz ('0@', '1@', '2@', vb.) '31@' dahil, '32@' ve '33@' dahili değişkenlerdir sizin de kullanabileceğiniz yerel zamanlayıcılar (geçen süreyi sayar, daha sonra göreceğiz). Ancak `33@` üzerindeki herhangi biri ana kısımda çalışmaz. Sanny mutlu olmayacak ve size yerel var olduğunu söyleyecektir. menzil dışında (bu yüzden "Menzil kontrolü" seçeneğini açtık). İstediğiniz küreselleri kullanabilirsiniz ancak sayıları 16383 ile sınırlıdır, yeterince uzak olmalıdır.

#### Şimdide görev bölümü:

Then after:\
**CODE**
```sb3
//-------------Mission 0---------------
// Originally: Initial 1
:INITIAL
03A4: name_thread 'INITIAL'
06C8: enable_riot 0
0004: $1515 = 0 
0005: $1500 = 5.0
etc...
```

Bu ilk görevdir (aslında bir görev değildir çünkü yeni bir oyunun ara sahneden önceki ilk milisaniyelerinde yapılır ve herhangi bir görev puanı vermez). Bu görev aslında park halindeki arabaları yaratıyor, bazı silahlar toplanıyor ve başka şeyler.

Görev 1'e ulaşıldıktan sonra (Görev 1'i bulun)\
**CODE**
```sb3
//-------------Mission 1---------------
// Originally: Initial 2
:INITIL2
03A4: name_thread 'INITIL2'
0004: $3407 = 25 
0004: $3408 = 100
0004: $3409 = 250
etc...
```

Görev 0 ile aynı, ama bu bir görev değil, sadece bazı değişkenleri sayı değerine eşitliyor, bazı arabaları tekrar park ediyor, bazı kapıları ve asansörleri çalıştırıyor.

Şimdi ise Görev 2\
**CODE**

```sb3
//-------------Mission 2---------------
// Originally: Intro
:INTRO
03A4: name_thread 'INTRO'
0050: gosub @INTRO_47 
00D6: if 
0112:   wasted_or_busted // mission only
004D: jump_if_false @INTRO_38 
0050: gosub @INTRO_9715
etc...
```

Bu, giriş ara sahnesinin bir parçasıdır ve görev, CJ'nin evindeki (Ganton'daki Johnson House) kırmızı daireye geldiğinizde sona erer.

Oyun salonlarından sonra oyun salonları gelir (*orijinal olarak: Video oyunu: Uranüs'ten süründüler; orijinal olarak: Video oyunu: Duality; orijinal olarak: Video oyunu: Go Go Space Monkey; orijinal olarak: Video oyunu: BUMBLE'a hazırlanalım; orijinal olarak: Video oyunu : poker*) sonra bilardo (*başlangıçta: Havuz*), dans (başlangıçta: Lowrider (bahis ve dans)) ve Sıfır "gizli/bonus" görevi (*başlangıçta: Beefy Baron*) gibi diğer şeyler ve bunlardan sonra, " Oyunun ilk görevi olan Big Smoke" Görevi (görev 11).

Ve sonra görevin 134. sonuna kadar böyle.
Eğer bir görev eklemek istiyorsanız, bundan sonra ekleyin ve görev tanımla bölümünde tanımlayın. (devamı daha sonra).

#### Ekstradan Scriptler (External Scripts) bölümü (son bölüm)

Son görevden sonra, ilk `PLAYER_PARACHUTE` ile başlayan tanımlanmış tüm harici komut dosyasını görebilirsiniz.:\
**CODE**
```sb3
//-------------External script 0 (PLAYER_PARACHUTE)---------------
:PLCHUTE
03A4: name_thread 'PLCHUTE'
0247: request_model #GUN_PARA
:PLCHUTE_16
etc...
```
ve ondan sonra, **CODE**

```sb3
//-------------External script 1 (PARACHUTE)---------------
:PARACH
03A4: name_thread 'PARACH'
0004: $8275 = 0 
etc...
```

ve böylece 77. harici komut dosyalarına kadar. Harici komut dosyalarındaki birçok şeyin, harici ismine bakarak, genellikle ne için kullanıldığını anlayabilirsiniz.

Şimdilik en önemli kısım ana kısım.

Ama yine de main.scm mimarisi hakkında, main.scm görünümünde bazı şeylerin sınırlı olduğunu bilmeniz gerekir, [here](http://www.gtaforums.com/index.php?showtopic=213017&view=findpost&%20%20p=3145932). Gördüğünüz gibi ana boyut (tüm 'ana kısım' iş parçacıklarının uzunluğu) 200 000 bayt ile sınırlıdır. 'Ana bölümün' ne kadar süredir derlendiğini bilmek ve rapora bakmak için size "ANA boyut: XXXXXX b" diyor ve ardından en büyük görev olan "En Büyük Görev: XXXX b." ve En Büyük harici komut dosyası :

:::l
![](mcp1/retfyhj2.png)
:::

Bu nedenle "Raporu göster" seçeneğini tikledik.

### 2. Fix Paket bir SCM ve Kaynak Kodları

#### A. Fix Paket Bir SCM

İlk kodlama projeleriniz için küçük (orijinal olan çok büyük) ve komut dosyası yazmak için yararsız hiçbir şeye sahip olmayan bir main.scm'ye ihtiyacınız var. Derleme yaparken zaman kazanacaksınız ve ilk ara sahneyi beklemek zorunda kalmayacaksınız. Ayrıca scm sınırları nedeniyle yukarıyı okuyun. 

Yani fix paket bir scm, herhangi bir görev, herhangi bir harici komut dosyası, herhangi bir ana iş parçacığı içermeyen bir main.scm'dir. Neredeyse hiçbir şey içermez, sadece oynatıcıyı oluşturur. 

Sanny Builder'ı kurduğunuz \data\sa klasöründe stripped.txt adıyla bir tane bulabilirsiniz, garip görünüyor çünkü Sanny Builder sınıfları sözdiziminde yazılmış, size bu konuda fazla bir şey anlatmayacağım, daha fazlasını Sanny Builder'ın dosyasında bulabilirsiniz Yardım. İyi olmadığından değil çünkü önce "opcode yöntemi" ile öğrenmenin daha kolay olacağını düşünüyorum.

Böyle bir şeye sahip olmalı:

**CODE**
```sb3
{
	use macro (Ctrl+J) "headsa"
	to insert a file header      
}
{$VERSION 3.0.0000}
var
	$PLAYER_CHAR: Player
end // var
03A4: name_thread 'MAIN'
01F0: set_max_wanted_level_to 6 
0111: toggle_wasted_busted_check 0 
00C0: set_current_time_hours_to 8 minutes_to 0 
04E4: unknown_refresh_game_renderer_at 2488.56 -1666.84 
03CB: set_rendering_origin_at 2488.56 -1666.84 13.38 
0053: $PLAYER_CHAR = create_player #NULL at 2488.56 -1666.84 13.38 
01F5: $PLAYER_ACTOR = create_player_actor $PLAYER_CHAR 
07AF: $PLAYER_GROUP = player $PLAYER_CHAR group
0373: set_camera_directly_behind_player
01B6: set_weather 0 
0001: wait 0 ms 
087B: set_player $PLAYER_CHAR clothes_texture "PLAYER_FACE" model "HEAD" body_part 1 
087B: set_player $PLAYER_CHAR clothes_texture "JEANSDENIM" model "JEANS" body_part 2 
087B: set_player $PLAYER_CHAR clothes_texture "SNEAKERBINCBLK" model "SNEAKER" body_part 3 
087B: set_player $PLAYER_CHAR clothes_texture "VEST" model "VEST" body_part 0 
070D: rebuild_player $PLAYER_CHAR      
01B4: toggle_player $PLAYER_CHAR can_move 1 
016A: fade 1 time 0 
04BB: select_interior 0 
0629: change_integer_stat 181 to 4 
016C: restart_if_wasted_at 2027.77 -1420.52 15.99 angle 137.0 town_number 0 
016D: restart_if_busted_at 1550.68 -1675.49 14.51 angle 90.0 town_number 0 
0180: set_on_mission_flag_to $ONMISSION // Note: your missions have to use the variable defined here 
0004: $DEFAULT_WAIT_TIME = 250
03E6: remove_text_box 

// put your create_thread commands here



:MAIN_LOOP
0001: wait $DEFAULT_WAIT_TIME ms
00BF: $TIME_HOURS = current_time_hours, $TIME_MINS = current_time_minutes
0002: jump @MAIN_LOOP 


// put your mods (threads) here

//-------------Mission 0---------------
// put your missions here



//-------------External script 0---------------
// put your external scripts here
```

You probably noticed that there isn't a Define section (`DEFINE OBJECTS`, `DEFINE MISSIONS`...) there is nothing to worry here because there isn't anything that needs to be defined here. As said in the file if you need an header (define section) press `CTRL`+`J` and select headsa. Like on the pic:

:::l
![](mcp1/jyfcgfhgcfgkk.png)
:::

Then you'll get what you're missing:\
**CODE**

```sb3
DEFINE MISSIONS 0
//DEFINE MISSION {ID} 0 AT {LABEL} @
DEFINE EXTERNAL_SCRIPTS 0 // Use -1 in order not to compile AAA script
//DEFINE SCRIPT {NAME}  AT {LABEL} @
DEFINE UNKNOWN_EMPTY_SEGMENT 0
DEFINE UNKNOWN_THREADS_MEMORY 0
```

Artık görevlerinizi ve harici komut dosyalarınızı tanımlayabilirsiniz.

Modları bu soyulmuş scm'ye koymak için, create threads komutunuzu ('004F: create_thread @MYTHREAD' gibi) şunun yazdığı yere koymanız gerekir: `// create_thread komutlarınızı buraya koyun`. Ve komut dosyalarınızı "MAIN_LOOP"tan sonra "// modlarınızı (thread'lerinizi) buraya koyun" yazan yere koyun.

### B. Kaynak Kodları

Çoğu satırın başında 4 basamaklı bir sayı ve ardından 0001:, 087B:, 016A: veya 004E: gibi iki nokta vardır, bunun gibi 0605: Sanny Builder'da. Bunlara işlem kodları (kaynak kodları) denir !!!!!! İşte bu yüzden "İşlem kodlarını yaz" seçeneğini belirledik.

İşlem kodu, oyun motorunun verilen parametrelerle yapacağı komutun (eylem veya işlem) sayısıdır, tüm işlem kodlarının (birkaç tanesi hariç) sabit sayıda parametresi vardır.

Parametreler (Paramlar veya yalnızca P1, P2 veya %p olarak da adlandırılır) komutun girdileridir (sayılar, aktörler, araçlar, metin dizileri veya herhangi bir değişken türü). Sol alttaki Sanny oluşturucu size bir işlem kodunun ne kadar parametreye ihtiyacı olduğunu söyler:

:::l
![](mcp1/yrrgrg.png)
:::

İşte iki örnek:

**simple**\
**CODE**
```sb3
000A: 3@ += 1 // integer values
```

* `000A` : işlemi yapan işlem kodu tamsayılara ekler.
* `3@` and 1 : parametrelerdir, bu işlem kodunun 2 parametresi vardır.

Böylece bu yerel değişkeni (daha sonra) `3@` alır ve ona bir ekler, sanki `3@` 1'e eşitse, şimdi 2'ye eşit olacaktır !!!

```sb3
0605: actor $PLAYER_ACTOR perform_animation_sequence "ATM" from_file "PED" 4.0 loop 0 0 0 0 6000 ms
```

* `0605` : the opcode of the command 
* `$PLAYER_ACTOR`, `"ATM"`, `"PED"`, `4.0`, `0`, `0`, `0`, `0`, `6000` : are the parameters, this opcode has 9 parameters. 

Bu komut, mevcut bir aktörün (karakter) `$PLAYER_ACTOR` animasyonunu `"PED"` animasyon dosyasında `"ATM"` gerçekleştirmesini sağlar, `4.0` animasyonun oynatma hızıdır, döngü 0, yapmayacağı anlamına gelir anim tekrar bittiğinde, eğer tamsayı 1'e ayarlanmışsa, oyuncu başka bir şey yapması (veya yok etmesi veya "özgür" olması söylenene kadar) bunu tekrar tekrar yapacaktır (döngü) - ekranda gördüğünüz diğer pedlere değiştirerek oyun, opcode 01C2), daha sonra onlar hakkında konuşacağımız başka parametreler var. Sonuncusu, animasyonun motor tarafından ne kadar süreyle yürütüleceği, bir bekleme gibi davranacağı ve motorun o zamana kadar kodu yürütmesini durduracağı zamandır.

Şimdi ihtiyacınız olan işlem kodunu bulmak için birçok yere sahipsiniz:

* Sanny Builder'da parlak yeni "İşlem kodu arama aracı". Araçlar menüsünde, IDE araçları, işlem kodu araması. Buraya sadece aradığınızı yazın ve onu bulma şansınız olacak. Sanny Builder Yardım'da özel bir bölüm var, [buraya tıkla](../editor/opcode-search-tool.md) Burada size işlem kodlarını panoya kopyalamak için tuşları söyler (seçili işlem kodu = Enter), işlem kodlarını sıralamak için vb... Sanny tarafından oluşturulan ve sacm.ini'yi değiştirirseniz manuel olarak yeniden kaydetmeniz gereken opcodes.txt dosyasını kullanır.
* Original main.scm uncompiled (the more __trustable__ one most times -pretty good explanation and if used here it DOES work-) Thanks to R* for making the game !!!!! Thanks to Barton Waterduck, Sanny Builder authors, and many others who help decompiling the scm, they should recognize themselves (if they have time reading this). 
* [GTA:SA Opcodes](http://www.gtaforums.com/index.php?showtopic=194990&view=findpost&%20%20p=2888074) @ [GTAforums](http://www.gtaforums.com/) işlem kodları için yer ve çok iyi çünkü iyi açıklama, açıklamalar ve bir parametrenin değerinin ne işe yaradığını içeriyor.
Konuya mesaj gönderen herkese ve konuyu başlattığı için opius'a teşekkürler.
* [http://sa-db.webtools4you.net/](http://sa-db.webtools4you.net/) Etkileşimli veritabanı, onu kullanmanın en iyi yolu, "hiçbir şey" araması yapmaktır (sadece git'e tıklayın), tüm işlem kodlarına sahip olacaksınız ve ardından internet tarayıcınızla "sayfada bul" şeklinde arama yapın.

Ama en iyi yol hepsine atıfta bulunmaktır!!!\
\
The opcode descriptions come from a file called sascm.ini which is located in data\sa of Sanny builder installation folder. Updating this file will make new descriptions or/and change the opcode parameters. 

* Orijinal Sanny Builder sascm.ini çok iyi, onu başlangıç ​​için saklamanızı tavsiye ederim.
* PLPynton'dan birleşik işlem kodu veritabanı burada veya dosya burada.. Dosyayı kullanmak istemiyorsanız, yine de konuda bazı işlem kodlarını bulabilirsiniz.
- Bu işlem kodu "veritabanı"nın iki büyük avantajı vardır, çünkü çok eksiksizdir ve orijinal parametrelerin sırasını korur ve bir dezavantajı, açıklama için tamamen farklı bir sözdizimi kullanmasıdır.
* Oluşturduğu için PLPynton'a teşekkürler.

Spaceeinstein'ın sascm.ini burada. Her zaman kullanılan sözdizimini kullanın. Onun sayesinde.
Orada verilen sascm.ini dosyalarını kullanmak için, üzerinde çalıştığınız tüm dosyaları derlemeniz gerekir, Sanny builder'ı kapatın (açıksa), dosyayı Sanny Builder kurulum klasöründeki \data\sa klasörüne koyun (yedekleme yapın), Sanny'yi açın ve dosyaları derleyin. Sascm.ini'yi her değiştirdiğinizde bunu yapmanız gerekir.\
\
:::center
!!!!!!!!!!!! ÖNEMLİİİİİ !!!!!!!!!!!!\
\
Sascm.ini hakkında, onlarla fazla uğraşmayın, birini seçin ve bir süre ona bağlı kalın. Her değiştirdiğinizde, tüm dosyalarınızı dönüştürmeniz veya derlemeniz ve derlemeden çıkarmanız gerekir, ancak işlem kodu parametrelerine dikkat etmeniz gerekir, bunlar bazen sascm.ini'yi değiştirirken karışırlar ve çökmeler ve hatalar alırsınız (ve bunlar bulmak çok zor).

Ayrıca, opcodes.txt dosyalarınızı yeniden oluşturmanız gerekecek, aksi halde F1 ile veya Opcode aramayla tamamlayabilirsiniz. Bunu yapmak için Araçlar'a gidin ve "Make opcodes.txt". Benim tavsiyem, bunlardan birini seçin veya Sanny Builder'da verileni saklayın ve bunun için iyi bir nedeniniz olana kadar değiştirmeyin.\
Öğretici, Sanny Builder'da verilen sascm.ini ile yapılmıştır.
:::

### 3. Parametreler ve değişkenler

Farklı parametre türleri vardır:

* __Tamsayı__ (veya int), "1" veya "-2" veya "321" veya başka herhangi bir tam sayı olarak da adlandırılır.
* __Floats__ veya kayan nokta değerleri, "1.2" veya "-2372.3845" gibi herhangi bir ondalık sayı. Kayan nokta sayıları hakkında daha fazlası.
* `$PLAYER_ACTOR` global değişkeninde saklanan CJ (YOU) gibi bir __actor__ veya `$SA_RYDER`da Ryder oyun içi karakteri veya Ryder'ın görevinde ordu üssünün kapısını izleyen asker yerel değişkende saklanır "@102" Değişken, bir oyuncuyu (veya bir arabayı) tutarken (hafızada tutarken) tutamaç olarak adlandırılır, sonraki nesnenin adı, onu yakaladığınız tutamaç formu olacaktır.
* __Bir "araba"__ (kodlamada her araç -uçan, süzülen, doğrayan- bir arabadır), genellikle kullandığınız araba gibi `$PLAYER_CAR`, yerel veya küresel değişkenlerde depolanan aktörler gibi. Bir değişkende bir araba stoklarken aktörlerle aynı, buna tutamaç denir.
* __Model tanımlayıcısı__ `#CHEETAH`, `#ARMY`, `#PARACHUTE` gibi, bir arabanın, aktörün, nesnenin 3B modeli. gta3.img'de depolanan dff dosyalarını ifade eder. Ve arabalar, aktörler veya nesneler yaratmak için bu modelin yüklenmesi (belleğe alınması) gerekir.
* __Kısa metin dizileri__ (8 bayt uzunluğunda) `'STRAP_4'' veya ''MTIME3'' gibi, motorun içinden geçerken "okuduğu" ve dosyayı - veya dosyanın - adlı satırını aradığı harfli kelimelerdir. Her zaman maksimum 7 karakterdir. Gxt, animasyon dosyaları, IPL dosyaları (iç garaj vb...) veya konu adları, metin girişleri (gxt dosyasından), animasyonlar (animasyon dosyasındaki animasyon) ve diğer birçok şey gibi dosya adlarını ifade eder. Endişelenmeyin, bu sadece parametre için bir formattır.
* __Uzun metin dizileri__ (16 bayt veya daha uzun) `"ATM"` veya `"JEANSDENIM"` gibi. Yukarıdakiyle tamamen aynıdır, yalnızca parametre için bir biçimdir, ancak bu daha uzun sözcükleri işleyebilir (Sanny'nin ReadMe'si 255 karaktere kadar diyor) ve sözcük 7 karakterden kısa olsa bile kullanılır. Dizeler hakkında daha fazla açıklama bulabilirsiniz (Seemann'dan gelen bu mesajda hem kısa hem de uzun)
* __Etiketler__ `MAIN_4059` gibi, yalnızca atlamalarda (ve gosub'larda) kullanılır çünkü bu işlem kodları (`0002: jump @`; `004D: jump_if_false @`; `0050: gosub @` ; veya `004F: create_thread @` ) yapar motora yolculuk (atlama) etiketi söylendi. Etiket, önünde : ile gösterilir.

example:\
**CODE**
```sb3
:label
0001: wait 0 ms
0002: jump @label
```

Değişkenler, içinde bir şey sakladığınız küçük bir bellek parçasıdır. Tanımladıktan sonra motor, değiştirilene veya temizlenene kadar onu hatırlayacaktır, değişkeni kaydettikten sonra bile mevcut olmaya devam edecektir (kaydetmek tüm değişkeni kaydeder ve oyun yüklendiğinde yeniden yükler). Tıpkı bir aktör yarattığınızda, ona bir değişken atarsınız, bu onun adı gibi olur. Bunlara tamsayı, değişkenler, aktörler, araçlar, metin dizileri (özel biçim) gibi her şeyi koyabilirsiniz.

O zaman iki büyük değişken ailesi vardır:

* Global değişkenler: `$` ile başlayarak temsil edilirler. Globaller main.scm'de her yerde kullanılabilir. Görevin ana bölümünde bir şeye ayarlarsanız, yine de erişilebilir olacak ve bir şeye eşit olacaktır. Örnekler: `$ONMISSION`, `$PLAYER_ACTOR`.

* Yerel değişkenler : Yereller, sonunda "1@", "2@" gibi bir "@" ile temsil edilir. Her iş parçacığının ('004F' tarafından oluşturulan) kendi yerel kümeleri vardır, örneğin ':thread_A' içindeki '0@', ':thread_B' içindeki '0@' ile aynı değildir. Ana kısımda `33@` üzerinden __yapamazsınız__ (bu yerel bir zamanlayıcıdır, daha fazla ayrıntı için aşağıya bakın). Ancak görevde en fazla `1024@` kullanabilirsiniz.

Yerel zamanlayıcılar : "32@" ve "33@" dahili yerel zamanlayıcılardır, geçen milisaniye sayısını sayarlar. Örneğin, 0 olarak ayarlarsanız, 10 saniye bekleyin ve bakın ne kadar '32@' veya '33@' değerinde 10 000 bulacaksınız. 1 000 olarak ayarlarsanız ve bakmadan önce 5 saniye beklerseniz eşit olacaktır 6 000 ms'ye kadar.

Kısa ve uzun metin dizileri : metin dizileri, özel değişken biçiminde saklanır. Metin dizisi a Sanny'de kırmızıyla vurgulanır. 4 tür metin değişkeni vardır 2 kısa (8 bayt, `'STRING'') dizeler için, 2 uzun dizeler için (16 bayt, `"LONGSTRING"`):

#### Kısa dizeler için

- `s$1169` veya `s$ACTOR_SPEECH_GXT_REFERENCE` gibi, diğer globaller gibi her zaman erişilebilen global bir metin dizesi.
- "170@s" veya "5@s" gibi, yalnızca kendi ileti dizisinde erişilebilen yerel bir metin dizesi.

#### Uzun dizeler için

- genel bir metin dizesi : `v$1225` veya `v$MYLONGSTRING`, kısa dizeyle aynı, ancak uzun olanlar için...
- yerel bir uzun dize : `28@v`, kısa olanlarla aynı...

ANCAK dikkat edin, "example0@s" için varsa "0@" yerine "0@" koyun, bu aynı değişkendir ancak yalnızca farklı bir biçimdedirler.

Ve size bir tamsayı ve bir kayan noktanın oyun motorundan farklı olduğunu hatırlatmak (veya söylemek) istiyorum ( 1, 1.0 ile aynı değildir !!!!!). İşlem kodunun bir değişkene ihtiyacı varsa ve ona bir int verirseniz (ve tersi), oyun ÇÖKECEKTİR.
Bir tamsayıyı "008D" işlem koduyla kayan sayıya dönüştürebilirsiniz. Ve "008C" ile bir kayan noktayı bir tamsayıya dönüştürün.

### 4. Script Başlıkları ve etiketler

Bir iş parçacığı, "tek başına" (kendisinden başka hiçbir şeye ihtiyaç duymayan bir şey) kod parçasıdır. Kendi başına bir program gibidir (işletim sisteminizde açılan bir pencere gibi -Microsoft Windows gibi-). Gelecekteki modlarınızı (programlar, pencereler) kullanmak (oyun içinde oynamak) için yeni bir tane oluşturmanız gerekecek. "004F" ile ne yaparsınız:\
**CODE**
```sb3
004F: create_thread @MYTHREAD
```

İplikler, kibritlerin olduğu bir dünyada ateş gibidir (daha hafif, silex, yani ateş "yakamazsınız", onu başka bir ateşten tutuşturmanız gerekir - tıpkı Olimpiyat Meşalesinin 2 olimpiyat oyunu arasında hiç sönmemesi gibi). Mevcut olandan yeni bir tane oluşturmalısınız. Daha önce de söylediğim gibi, ana iş parçacığı motorun kendisi tarafından oluşturulur. ''ANA'' iş parçacığı ateşinizi yakacak. Bu nedenle, mevcut tek iş parçacığıysa (soyulmuş bir main.scm'deki gibi), iş parçacığınızı kapatmadan ÖNCE (bu satırdan önce) ana iş parçacığında oluşturmanız gerekir:
**CODE**
```sb3
004E: end_thread
```

If using a bigger file (like orignal main.scm) just create it where there is many others create_thread (`004F`) like this:

```sb3
004F: create_thread @ODDVEH
004F: create_thread @R3
004F: create_thread @GYM
004F: create_thread @SHOOT
004F: create_thread @BLOODR
004F: create_thread @HOTR
004F: create_thread @KICKS
004F: create_thread @PSAVE1
004F: create_thread @FLOW
004F: create_thread @HELP
004F: create_thread @COOLS
004F: create_thread @CRANES
004F: create_thread @BUY_PRO
etc...
```

"00D7: create_thread_with_wasted_busted_check" hakkında endişelenmeyin çünkü gerçek kullanımları henüz bilinmiyor. "004F" kullanın, oyuncunun boşa mı yoksa bozuk mu olduğunu kontrol etmenin daha iyi yolları vardır (koşullu işlem kodu "0112" gibi). Seeman'ın daha sonra bu konuda bize söylediği gibi, "004f" 0 ila 32 ekstra parametreli bir iş parçacığı oluşturur ve "00d7" 0 ekstra parametreli bir iş parçacığı oluşturur. Bu yüzden "00d7" hakkında hiç endişelenmeyin çünkü "004f" aynı işlevi görür ve daha fazla işlevsellik sunar. (Seemann'ın mesajı).

Daha önce de söylediğim gibi, oluşturduğunuz her bir iş parçacığında, her iş parçacığı için "0@" ile "33@" ("32@" ve "33@" yerel zamanlayıcılardır) arasında kendi yerel kümeniz vardır. Hafızayı emmedikleri için oldukça kullanışlıdırlar.

Etiket, `:MAIN_4059` gibi küçük bir ileti dizisidir. Şimdi ipliği bir kitap olarak düşünün (artık ateş yok 😧 ) ve etiketler bu kitabın sayfaları olur. O zaman, belirli bir etikete "atlamanın" (veya "jump_if_false" veya "gosub") belirli bir sayfaya rapor vermek gibi olacağını kolayca anlayabilirsiniz.

Oyun motorunun etiketler arasında nasıl gezindiğini görmeniz için animasyonlu bir örnek yaptım:

:::l
![](mcp1/ASDFG2.gif)
:::

Kırmızı çizgi, etiketlerin içinden geçen kodu (motor bazen kod okuyucu gibi basitçe kod olarak adlandırılır) temsil eder.

Gördüğünüz gibi `etiket` iş parçacığı başka bir iş parçacığından oluşturuldu. "Ayrılan" kodun ikiye ayrıldığını görebilirsiniz, biri az önce oluşturulan 'etiket' iş parçacığına gidiyor, biri yapmak için kullandığı iş parçacığını yapmaya devam ediyor ve sona eriyor (ancak bitirmezseniz bir şeyler yapmaya devam ediyor olabilir).
":label"den ":label_100"e gider. ":label_100"de, ​​içinde koşul bulunan bir "if" ve ardından bir jump_if_false olduğunu görebilirsiniz; bu, "$PLAYER_ACTOR" 3 boyutta "2498.0 -1711.0 1015.0" konumuna yakınsa kodun geçebileceği anlamına gelir ve ":label_200" konumuna atlayın.
Bu nedenle, koşul sağlanana kadar kod ":label_100" içinde döngüye devam eder.
Ardından, ":label_150"den geçmeden ":label_200"e atlar ve doğrudan ":label_200"e gider. `:label_200`de yapması gerekeni yapar (yani `$PLAYER_ACTOR` aktörünün 3B koordinatlarını `$X_COORD $Y_COORD $Z_COORD` değişkenlerine koymaktır). Sonra `:label_150`ye atlar, 1 dakika bekler ve :label_100'e geri atlar ve oyuncunun noktaya yakın olup olmadığını tekrar kontrol eder. Ve her şeyi tekrar tekrar yapacak. Gerekmiyorsa bir ileti dizisini bitirmek zorunda değilsiniz, yalnızca başlamak için bir şeyi kontrol etmeye devam edebilir. İpliğinle ilgili bir şeyler yapman, devam ettirmen veya bitirmen gerektiğini unutma ama onu serbest bırakma.\ 
\
:::center
!!!!!!! Beklemeye dikkat edin (`0001: 0 ms` bekleyin), çünkü bu zorunludur !!!!!!!!!!!!\
Döngü yaparken, motorun kendi kendine sıkışmaması için bir bekleme eklemeniz gerekir ve hatta çökmez, sonra sadece donar. Bundan kurtulmanın tek yolu, çözünürlük değişikliğini zorlamak (bazen yapmak imkansızdır ve bazı dizüstü bilgisayarlarda birleşik tuş bulunur) ve GTA:SA'yı görev yöneticisi ile sonlandırmaktır.

Bir bekleme eklemeniz gerekip gerekmediğini gerçekten bilmiyorsanız, sadece "0 ms bekleyin" eklemeyin, çünkü çoğu zaman farkı hiç görmezsiniz ve çoğunlukla 500 ms'nin altındaki herhangi bir şeyi fark etmezsiniz.
:::

### 5. "Eğer"ler ve koşullu işlem kodları

"if" denen şey, bir şeylerin olup olmadığını kontrol eden bir kod yapısıdır. Beğenmek:\
if\
I die\
if not then --> I will finish my mods\
--> you give all my unfinished mods to the GTA community

In Sanny Builder it would look like 
```sb3
00D6: if
    0118: actor $PLAYER_ACTOR dead
004D: jump_if_false @finishmods
0002: jump @givemods
```

Motor, kontrol edilen bir şey olursa doğrudan hareket edecek, eğer olmazsa başka bir etikete atlayacaktır (`004D: jump_if_false @`). İf'in (olabilecek ya da olmayacak) bir şeyine koşul denir, onlar için koşullu opcode kullanırsınız !!!!!!! Ve bir tanesine en fazla yedi koşul koyabilirsiniz, eğer !!!!!!! ("Evet çok mutluyum, bu gerçekten hayatımı değiştirdi !!!!!!...." dediğinizi biliyorum ama...).

O zaman iki tür çoklu koşul vardır yani Eğerler:

When you have multiple conditions you might want to have __all__ conditions true (happens) or __at least one of them__ true. 

<ul>
<li>
if __and__ is when you want all the conditons to be true for passing through like:
```sb3
00D6: if and
    08AB: external_script 38 (BAR_STAFF) loaded
    08AB: external_script 37 (CUSTOMER_PANIC) loaded
    08AB: external_script 28 (BAR_AMBIENCE) loaded
    08AB: external_script 35 (DABCE) loaded
004D: jump_if_false @INTMAN_1080
```
So here you can see that if all those external scripts are loaded it goes through but then if any of them isn't it will jump to label `:INTMAN_1088`. You can think it as if this __and__ this __and__ this is OK then go throught if not jump because it's false.
</li>
<li>
if or is when you only need one of those conditions to be go through like:
```sb3
00D6: if or
    03B0: garage 38 'BODLAWN' door_open
    03B0: garage 37 'MODLAST' door_open
    03B0: garage 28 'MDSSFSE' door_open
    03B0: garage 35 'VECMOD' door_open
004D: jump_if_false @MAIN_4336
```
Here it's if any of these door is open keep going but if not jump to `:MAIN_4336`. You can think it as if this or this __or__ this __or__ happens go througt but if there isn't any that is true jump because it's false. 
</li>
</ul>

__And__ ve __or__ mantıksal işleçler olarak adlandırılır.

Tek bir koşulda bir veya veya bir ve eklemenin ne kadar anlamlı olmayacağına dikkat edin. Tek bir koşul, onları kullanamazsınız, yalnızca bir öğe !!!!! Ve sanny inşaatçı bundan hoşlanmayacak ve size "hey bunu yapma" demeyecek.

Öte yandan, if'lerin katlarına bir veya veya bir ve koymayı unutmayın, çünkü sanny bir şey söylemez, oyun çökmeyebilir, ama eminim düzgün çalışmaz, ya ve veya veya, ancak bir şeyi birden çok ifs koşuluna koymanız gerekir.

Tüm koşullu işlem kodlarını reddedebilirsiniz. Olumsuzlamak, koşullara bir değil eklemek gibidir. Oyuncu ölmediyse veya aktör araba kullanmıyorsa. O zaman bu, içinden geçmek için koşulun yanlış döndürmesi gerektiği anlamına gelir (olmama). Koşullu işlem kodlarını çoklu ifs koşullarında da olumsuzlayabilirsiniz. Koşullu bir işlem kodunu reddetmek için ilk basamağa 0 yerine 8 koymanız gerekir, örneğin:

```sb3
00D6: if
    02E0: is_char $PLAYER_ACTOR aggressive
003D: jump_if_false @labelX
```
not negated


```sb3
00D6: if
    82E0: not is_char $PLAYER_ACTOR aggressive
003D: jump_if_false @labelX
```
negated.

`not` yazmak zorunda değilsiniz derleyici (sanny) ona bakmayacak ve yeniden derlemeyi açarken yazacaktır. Ama yine de isterseniz `not` yazabilirsiniz, bu daha net ve üzerinde çalışmayı kolaylaştırır. Ama gerçekten 8'i işlem koduna yazmak ZORUNDASINIZ.

Tüm koşulları bir 'birden fazla koşul' olarak olumsuzladığınızda, mantık işlecini değiştirmeniz gerekir.

```sb3
00D6: if or
    02E0: is_char $PLAYER_ACTOR aggressive
    0457: is_player $PLAYER_CHAR aiming_at_actor 11@
    0457: is_player $PLAYER_CHAR aiming_at_actor 14@
    0457: is_player $PLAYER_CHAR aiming_at_actor 15@
    0457: is_player $PLAYER_CHAR aiming_at_actor 16@
    0457: is_player $PLAYER_CHAR aiming_at_actor 17@
003D: jump_if_false @labelY
0002: jump @labelZ
```

```sb3
00D6: if and
    82E0: not is_char $PLAYER_ACTOR aggressive
    8457: not is_player $PLAYER_CHAR aiming_at_actor 11@
    8457: not is_player $PLAYER_CHAR aiming_at_actor 14@
    8457: not is_player $PLAYER_CHAR aiming_at_actor 15@
    8457: not is_player $PLAYER_CHAR aiming_at_actor 16@
    8457: not is_player $PLAYER_CHAR aiming_at_actor 17@
003D: jump_if_false @labelZ
0002: jump @labelY
```

Bu ikisi aynı şeyi yapıyor:

* ilki, oyuncunun agresif olup olmadığını kontrol eder __veya__ aktör `14@` __veya__ `15@` aktörünü hedefliyor __veya__ `16@` aktörünü hedefliyor __veya__ `17@` aktörünü hedefliyor, bunlardan herhangi biri doğruysa kod geçer ve labelz'e atlar.
* ikincisi, oyuncunun saldırgan olup olmadığını __ve__ `14@` aktörünü hedeflemediğini __ve__ `15@` aktörünü hedeflemediğini __ve__ `16@` aktörünü hedeflemediğini __ve__ bunlardan herhangi biri varsa, 17@` aktörünü hedeflemediğini kontrol eder. false ise (eğer oyuncu nişan alırsa), labelz için false olduğu için kod atlayacaktır.

Bazen güzel modlar ararken şunları bulabilirsiniz:\
**CODE**
```sb3
00D6: if 0
```
or\
**CODE**
```sb3
00D6: if 5
```
or\
**CODE**
```sb3
00D6: if 21
```
or\
**CODE**
```sb3
00D6: if 25
```

Endişelenmeyin, aynı, derleyin, sonra kaynak koda dönüştürün ve mantık operatörünü yazdırmış olacaksınız. İşte bu yüzden "Koşul kontrolü"nü etkinleştirdik, böylece ve & veya kullanabiliriz ve Sanny bunları kaynak koda dönüştürürken yazar.

0 ise = eğer

eğer x = ise ve

eğer 2x = ise veya

[Sanny Builder's Help'teki Koşulları](../coding/conditions.md) veya/ve [burayı](http://www.gtaforums.com/index.php?showtopic=134970&view=findpost&%20%20%20) okuyun %20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20p=2083394) (Vice City için ama aynı, sadece daha fazla açıklama için "?") öğesini yoksayın.

Üst düzey koşulları oluşturmak isteyebilirsiniz, [Sanny Builder's Help'teki Koşulları](../coding/conditions.md) okuyun, oldukça iyi açıklıyor. Düşük seviye konstrüksiyonlar şimdilik iyi olabilir ve yine de düşük seviye ile yüksek seviye yapabilirsiniz.

:::merkez
ANCAK, "eğer"leri yaparken iyi hatırlayın, kodun geri döneceğini bildiğiniz yere beklemeler ekleyin. Ve 0 bekleme asla sorun olmaz çünkü aslında 0 milisaniyeyi bekler ki bu oldukça kısadır !!!!!!! ancak motorun başka şeyler yapmasına zaman tanıyacaktır. Nerede kesinlikle beklemeniz gerektiğini anlamak için etiketler için animasyonlu örneğe yeniden bakın.
:::

### 6. Modelleri isteyin, yükleyin, oluşturun ve ardından belleği boşaltmak için serbest bırakın

Aktör ('009A' işlem kodu ile), araba (op '00A5' ile), nesne (op '0107' ile) oluştururken, daha önce dff modelini talep etmeniz ve yüklemeniz gereken bir aktöre silah verin ve olup olmadığını kontrol edin. gerçekten yüklendi !!!!!!

Şunun gibi:\
**CODE** 
```sb3
:GSHAUTO_564 
0001: wait 0 ms 
0247: request_model #HFYBE
0247: request_model #POLICE_BARRIER 
0247: request_model #BMYBOUN 
0247: request_model #WMYBOUN 
0247: request_model #DESERT_EAGLE 
0247: request_model #MP5LNG 0247: request_model #CHEETAH
0247: request_model #TURISMO 
038B: load_requested_models 
00D6: if and
    0248: model #HFYBE available
    0248: model #BMYBOUN available
    0248: model #WMYBOUN available
    0248: model #DESERT_EAGLE available
    0248: model #MP5LNG available
004D: jump_if_false @GSHAUTO_564 
00D6: if and
    0248: model #POLICE_BARRIER available
    0248: model #CHEETAH available
    0248: model #TURISMO available
004D: jump_if_false @GSHAUTO_564 
009A: 11@ = create_actor 4 #WMYBOUN at 561.0 -1252.0 17.0 
009A: 14@ = create_actor 4 #BMYBOUN at 552.0 -1257.0 17.0 
009A: 15@ = create_actor 5 #HFYBE at 567.0 -1290.0 17.5 
009A: 16@ = create_actor 4 #WMYBOUN at 561.0 -1255.0 17.0 
009A: 17@ = create_actor 4 #BMYBOUN at 553.5 -1260.0 17.0 
009A: 18@ = create_actor 4 #WMYBOUN at 569.0 -1292.0 17.5 
009A: 19@ = create_actor 4 #BMYBOUN at 569.0 -1288.0 17.5 
0107: 9@ = create_object #POLICE_BARRIER at 560.0 -1254.0 17.0 
00A5: 0@ = create_car #TURISMO at 1793.4956 -1907.4233 12.3992 
00A5: 1@ = create_car #CHEETAH at 1793.4956 -1907.4233 12.3992 
01B2: give_actor 11@ weapon 29 ammo 900 // Load the weapon model before using this 
01B2: give_actor 14@ weapon 29 ammo 900 // Load the weapon model before using this 
01B2: give_actor 15@ weapon 24 ammo 300 // Load the weapon model before using this 
01B2: give_actor 16@  weapon 29 ammo 900 // Load the weapon model before using this 
01B2: give_actor 17@ weapon 29 ammo 900 // Load the weapon model before using this 
01B2: give_actor 18@  weapon 29 ammo 900 // Load the weapon model before using this
01B2: give_actor 19@ weapon 29 ammo 900 // Load the weapon model before using this
```

Bakın önce modelleri nasıl talep ediyorum (`0247: request_model` ile), sonra onları yüklüyorum (`038B: load_requested_models` ile). Ardından, talep edilen tüm modellerin kullanıma uygun olup olmadığını kontrol eder ('0248' ile). Bunlardan herhangi biri yük ise, en üste atlar ve yeniden talep edin ve yeniden yükleyin. Bunların hepsi varsa, o zaman aktörü, silahları ve nesneyi yaratın. Yorumda ('//' sonrasında) '01B2'nin kullanmadan önce silah modelinin yüklenmesi gerektiğini görebilirsiniz (silah numaraları için [yardım](../scm-documentation/sa/weapons.md) sayfasına bakın. burada veya çok iyi [Generic SA SCM Documentaion](http://www.gtaforums.com/index.php?showtopic=205020&view=findpost&%20%20p=3013775).

Bunlarla yapmanız gerekeni yaptıktan sonra (hepsi öldü, oyuncu çok uzakta, onları burada istemeyin, vb.) oyunda gördüğünüz arabalar) veya yok edin (nesne yok edilmelidir):

**CODE**
```sb3
0108: destroy_object 9@ 
01C3: remove_references_to_car 1@ // Like turning a car into any random car 
01C3: remove_references_to_car 0@ // Like turning a car into any random car 
01C2: remove_references_to_actor 11@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 14@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 15@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 16@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 17@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 18@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 19@ // Like turning an actor into a random pedestrian 
```

or **CODE**
```sb3
0108: destroy_object 9@ 
00A6: destroy_car 0@ 
00A6: destroy_car 1@ 
009B: destroy_actor_instantly 11@ 
009B: destroy_actor_instantly 14@ 
009B: destroy_actor_instantly 15@ 
009B: destroy_actor_instantly 16@
009B: destroy_actor_instantly 17@
009B: destroy_actor_instantly 18@ 
009B: destroy_actor_instantly 19@
```

Bunları (01C2, 01C3, 0108, 00A6, 009B, vb...) bir oyuncuya veya var olmayan bir arabaya (veya onu tutması gereken kol - 0@, $car, vb...- tutmuyor), çünkü oyun kötü bir şekilde çökecek!!!!!!

Aktörlerden/arabalardan/nesnelerden kurtulduğunuzda (Yani onları kullanmayı bitirdiğinizde ve onları sildikten sonra ve ancak ondan sonra), kullandığınız modelleri artık onlara ihtiyacınız olmayacağı için serbest bırakabilirsiniz.

**CODE**
```sb3
0249: release_model #WMYBOUN 
0249: release_model #BMYBOUN 
0249: release_model #HFYBE   
0249: release_model #POLICE_BARRIER
0249: release_model #DESERT_EAGLE
0249: release_model #MP5LNG 
0249: release_model #CHEETAH 
0249: release_model #TURISMO
```

Aslında bu modelleri daha önce yayınlayabilirdiniz, oyun nesnesini (nesne, oyuncu veya araç) oluşturduktan **sonra** yüklenmelerine gerek yoktur. Ancak başlangıç ​​olarak, modeli yalnızca senaryonuzun/görevinizin sonunda, artık ihtiyacınız olmayacağından emin olduğunuzda yayınlamanızı tavsiye ederim. Masaüstüne birkaç dönüş yapmanızı engelleyecektir.

#### Sonuç olarak

Eh, bu kısım daha çok bilgilendirmeydi, yapacak pek bir şey yok. Ama artık bir işlem kodunun, verdiğiniz parametrelere bağlı olarak oyunda bir şeyler yapan 4 haneli bir sayı olduğunu biliyorsunuz. Farklı parametre türlerini, yerel değişkenin ne olduğunu ve küresel olanın ne olduğunu biliyorsunuz. Devam etmek için bilmeniz gereken ileti dizileri oluşturma, adlandırma ve sonlandırma ve diğer birçok şey. Artık gerçek kodlamaya geçebileceğimiz aynı "dili" konuşuyoruz.
hadi geçelim [Bölüm II'ye](mcp2.md)...

Keyfini Çıkarın!

<script src="../../js/main.min.js"></script>