<!DOCTYPE html>
<html lang="es">
<head>
  <title>Bucles</title>
  <meta charset="utf-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="stylesheet" type="text/css" href="../../style/style.css">
</head>
<body>

Un **bucle** es una declaración que permite que el código se ejecute repetidamente. Sanny Builder es compatible con tres tipos de controles de bucles: [for](#for..end), [while](#while..end), [repeat](#repeat..until).

## FOR..END

El bucle `FOR` tiene un número estrictamente determinado de iteraciones (repeticiones).

Sintaxis:\
`FOR <contador> = <inicial> TO/DOWNTO <final> [paso = 1]`\
`    <cuerpo>`\
`END`\
\
`<contador>` - una [variable](variables.md) utilizada para el conteo de iteraciones\
`<inicial>` - un valor de la variable de bucle antes de la primera iteración (cualquier valor inclusive el [ID de un modelo](data-types.md#model-names))\
`TO` ó `DOWNTO` - incrementar o disminuir la variable de bucle entre iteraciones\
`<final>` - un valor de la variable de bucle después de la última iteración (cualquier valor inclusive el ID de un modelo)\
`<paso>` - un valor opcional con el que se aumentará o disminuirá la variable de bucle entre iteraciones. Por defecto es igual a `1`.

```sb3
var
    $value: int = 0
    $final: int = 100
end

FOR $MyCounter = 1 to $final step 2
    $value += $mycounter
end
```

Si el `contador` no se [declara](variables.md#var-end-construct) con ningún tipo antes del bucle, obtiene el tipo `Integer`. Si el `valor inicial`, el `valor final` o el `paso` son variables, obtienen el mismo tipo que la variable de bucle. Para usar números de punto flotante para los valores inicial y final, declare la variable de bucle con el tipo `Float`.

```sb3
var
    $MyCounter: float
end

FOR $MyCounter = 1.0 to $final step 2.0
end
```

Las variable `$MyCounter` y `$final` tienen el tipo `Float` después del bucle.

## WHILE..END

Sintaxis:
```
WHILE <condición>
  <cuerpo>
END
```

`condición` - un único opcode condicional\
`cuerpo` - comandos a ejecutar en cada iteración; puede ser omitido

```sb3
while not #AK47.Available
    wait 0
end
```

El bucle `WHILE` funciona mientras la condición del bucle sea verdadera. La condición se evalúa antes de una iteración de bucle. Por lo tanto, si la condición es falsa, el cuerpo del bucle nunca se ejecuta.

```sb3
$var = 10

while $var > 11
    inc($var)
end

// como la condición del bucle es falsa, inc($var) nunca se ejecuta
```

Las [constantes](constants.md) `True` y `False` se pueden utilizar como condición de bucle.

```sb3
while true
    <cuerpo>
end
```

Este <cuerpo> se ejecuta infinitamente hasta que es detenido con el comando `Break`

```sb3
while false
    <cuerpo>
end
```

El compilador ignora este bucle ya que la condición nunca se cumple.

{% hint style="info" %}
Actualmente, el compilador acepta solo un opcode en la condición del bucle, pero puede verificar más condiciones antes del cuerpo del bucle y usar los comandos `Break` y `Continue`.

```sb3
while true
    if and
       $var >= 0
       $var <= 100
    then
       Break
    end
    // el bucle se ejecuta mientras $var está en el rango [0...100]
      
    <cuerpo>
end
```
{% endhint %}

## REPEAT..UNTIL

Sintaxis:
```
REPEAT
    <cuerpo>
UNTIL <condición>
```

`loop body` - comandos a ejecutar en cada iteración; puede ser omitido\
`loop condition` - un único opcode condicional

El bucle `REPEAT..UNTIL` se ejecuta hasta que la condición del bucle devuelve falso. La condición se evalúa después de la iteración, por lo tanto, se garantiza que el bucle se ejecutará al menos una vez.

Las [constantes](constants.md) `True` y `False` se pueden utilizar como condición de bucle.

```sb3
repeat
  // el bucle tiene la única iteración
until true 
```

```sb3
repeat
  // el bucle se ejecuta infinitamente hasta que se detiene con el comando Break
until false
```

{% hint style="info" %}
Actualmente, el compilador acepta solo un opcode en la condición del bucle, pero puede verificar más condiciones antes del cuerpo del bucle y usar los comandos `Break` y `Continue`.

```sb3
repeat  
   <cuerpo>
   if and
     $var >= 0
     $var <= 100
   then
     Break
   end
  // el bucle se ejecuta mientras $var está en el rango [0...100]
until false
```
{% endhint %}

## Continue y Break

Si desea omitir la iteración actual y pasar a la siguiente, utilice el comando `Continue`.

El comando `Break` hace que el bucle se detenga inmediatamente y continúe con el comando después del cuerpo del bucle.

Pueden sustituir un parámetro de opcode (por ejemplo, `jf Continue`) o servir como una declaración independiente.

```sb3
while true
  if
    not $currentactor.dead
  jf Break // salir del bucle

  if
    $currentactor.dead
  then
    Continue // ir a la siguiente iteración
  end
end
```

<script src="../../js/main.min.js"></script>