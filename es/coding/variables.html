<!DOCTYPE html>
<html lang="es">
<head>
  <title>Variables</title>
  <meta charset="utf-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="stylesheet" type="text/css" href="../../style/style.css">
</head>
<body>

Una **variable (var)** es una ubicación de almacenamiento con nombre que contiene un valor y se puede leer/reescribir muchas veces. Hay dos tipos de variables.

## Variables Globales

Una variable global comienza con `$` seguida de un identificador (nombre).\
El nombre puede conformarse por cualquier combinación de letras, dígitos y `_`:

```sb3
$variable1
$100
$____
```

:::info
Una variable que conste en su nombre de dígitos se denomina variable `DMA` (**D**irect **M**emory **A**ddress). Mire los [Alloc](built-in-commands.md#alloc).
:::

Sus valores están disponibles desde cualquier lugar del código.

### **Variables Guardables**

Una variable guardada es una variable global especial disponible solo en los modos `LCS` y `VCS`. Su nombre tiene el prefijo `$_`, p. `$_var`. El valor de esta variable persiste en los juegos guardados. Las variables globales indicadas solo con `$` (por ejemplo, `$var`) no se guardan y obtienen valores en blanco cuando se carga el juego LCS o VCS.
:::info
Desde salida de [CLEO 3](../cleo-3/opcodes.md#0A95), puede habilitase para `SA`:
```sb3
0A95: enable_thread_saving
```
:::

## Variables Locales

Un nombre de variable local solo puede ser un número seguido de `@`.

```sb3
0@ 
999@ 
56@
```

Sus valores están disponibles solo dentro del script actual o la misión.

:::warning
El número de variables locales por script es estrictamente [limitado](../scm-documentation/gta-limits.md).
:::

### **Variables Temporizadoras**

Cada script o misión tiene 2 variables locales especiales llamadas 'TIMERA' y 'TIMERB'. El valor de una variable de temporizador aumenta automáticamente cuando avanza el reloj del juego, por lo que se usan comúnmente para medir el tiempo transcurrido desde que se reinicia el temporizador:

```sb3
0006: TIMERA = 0 // restablecer el temporizador

:WAIT_2S
0001: wait 0 ms
00D6: if
0019:   TIMERA > 2000 // si el valor del temporizador es > 2000, es decir, han pasado 2 segundos
004D: jump_if_false @WAIT_2S
0662: printstring "han pasado 2 segundos" // mostrara el mensaje
```

:::info
Tenga en cuenta que los nombres `TIMERA` y `TIMERB` solo están disponibles a partir de Sanny Builder v3.3.0. Los scripts más antiguos tienen estos temporizadores como variables numéricas `16@`, `17@` (GTA3, VC) o `32@`, `33@` (SA).
:::

## Constructor VAR..END

Las variables se utilizan habitualmente en las expresiones. En el caso de que el operando derecho sea una constante numérica, se puede omitir el opcode:

```sb3
$var = 0
$myarray($index, 10i) >= 150
```

Si ambas expreciones son variables, el compilador no puede detectar el opcode, debido al tipo desconocido de las variables. 

Por ejemplo, hay dos opcodes para incrementar el valor de una variable: `0058` para los valores enteros y `0059` para los valores flotantes (decimales).

```sb3
0058: $Var1 += $Var2 // (int)
0059: $Var1 += $Var2 // (float)
```

Suponiendo que no haya un opcode, ¿cuál usar?

```sb3
$Var1 += $Var2 // ??
```

Para comunicar al compilador un tipo de variable utiliza el constructor `VAR..END`.

Que permite declarar variables y sus tipos para su uso avanzados.

Sintaxis:\
`var`\
`<variable>: <tipo>`\
`end`\
\
Por ejemplo, si se declaran ambas variables, el compilador puede procesar la expresión sin opcodes:

```sb3
var
    $Var1 : Integer
    $Var2 : Integer
end
$Var1 += $Var2 // opcode 0058
```

Se admiten los siguientes tipos de variables:

* `Integer`, `Int` - valores enteros
* `Float `- valores decimales
* `String`, `ShortString` - una variable que contiene una cadena de texto con la  longitud fija (solo para los arreglos, use `s$`, `@s` para variables))
* `LongString `- una variable que contiene una cadena de texto con la longitud fija (solo para los arreglos, use `v$`, `@v` para variables)
* `<Nombre de la clase>` - el nombre de [cualquier clase](classes.md) disponible any available 

Los tipos de las variables locales tambien se pueden decl.

:::info
Una vez declarado el tipo de la variable, se utiliza para todo el código que sigue a la declaración.\
Puede volver a declarar variables para establecer el nuevo tipo:

```sb3
script_name 'Food'
var
    10@ : Float
    $Var : Float
end
$var = 1
10@ = $Var
end_thread

thread 'Loop'
var
    10@ : Int
    $Var : Int
end
$var = 1
10@ = $Var
end_thread
```

En el script `'Food'` está la variable `10@` como flotante. Y en `'Loop'` esta `10@` como entero.

Puede redeclarar variables tantas veces como lo necesite.
:::

## Forma de Dclaración más Corta

Desde v3.2.0 es posible declarar una variable de un tipo integrado (`Int`, `Float`, `String`, `LongString`) utilizando sólo el nombre de tipo.

Sintaxis:\
`<tipo> <nombre de la variable>`

```sb3
int 0@ // 0@ es declarada como una variable entera
```

A partir de v3.4.0 es posible declarar variables con nombres personalizados:

```sb3
int a
float distance
string name

a = 1
distance = 15.5
name = 'CJ'
```

Un valor inicial puede seguir el nombre de la variable para reducir el número de líneas de código:

```sb3
int a = 1
float distance = 15.5
string name = 'CJ'
```

El compilador enlaza una nueva variable local a cada nombre. En el ejemplo anterior se puede esperar que un código resultante tenga el aspecto de:

```sb3
0006: 0@ = 1 
0007: 1@ = 15.5 
05AA: 2@ = 'CJ'
```

:::info
Debido a [limitaciones de diseño](https://github.com/sannybuilder/dev/issues/32) esta característica solo está disponible en scripts de CLEO.
:::

## Inicialización de Variable

Puede especificar un valor inicial para la variable al declararla. Escribe `=` y luego el valor:

```sb3
var
    $fVar: float = 1.0
end
```

ó

```sb3
float $fVar = 1.0
```

La variable `$fVar` ahora se declara como `Float` y el compilador agrega el código de operación `0005` en el script:

```sb3
0005: $fVar = 1.0
```

La inicialización está permitida para variables, pero no para arreglos.

<script src="../../js/main.min.js"></script>