<!doctype html>
<html lang="es">
<head>
  <title>Clases</title>
  <meta charset="utf-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="stylesheet" type="text/css" href="../../style/style.css">
</head>
<body>

Una **clase** es un grupo de comandos aplicados a las entidades del juego: jugador, actores, objetos, etc. Por ejemplo, la clase `Player` agrupa los comandos realizados sobre el personaje del jugador.

## Sintaxis general

Sintaxis:\
`<nombre>.<miembro>(parametros)`\
\
`nombre` -  el nombre de un grupo de comandos definidos en el archivo `classes.db` para el mismo modo de edición.\
`miembro` - uno de los comandos incluidos en la clase.\
`parametros` - 0 o más [parámetros](data-types.md) delimitados por comas.

```sb3
Player.SetMinWantedLevel($PLAYER_CHAR, 2)
```

`Player` - nombre de la clase\
`SetMinWantedLevel` - miembro de la clase\
`$PLAYER_CHAR, 2` - dos parametros de `SetMinWantedLevel`

Hay tres tipos de miembros de la clase:

* [condiciones](#conditions)
* [métodos](#methods)
* [propiedades](#properties)

### Condiciones

La lista que aparece cuando presiona `Ctrl+Space` marca los comandos condicionales con la palabra `Check`. Se utilizan en [expresiones condicionales](conditions.md): 

```sb3
if
    Player.Defined($PLAYER_CHAR)
jf @anywhere
```

### Métodos

Los métodos son comandos habituales que se utilizan para completar una única acción en el juego, p.ej. mover un objeto, destruir un vehículo, etc:

```sb3
Object.PutAt($crate, 10.0, -25.5, 12.2)
Car.Destroy($car)
```

Están marcados con `proc` en la lista de miembros de la clase.

Un tipo especial de métodos es un constructor. Un constructor crea una nueva instancia de una clase y almacena su identificador en una variable.

En Sanny Builder, el constructor se puede escribir de dos formas equivalentes:

```sb3
Player.Create($PLAYER_CHAR, #NULL, 2488.5601, -1666.84, 13.38)
```

```sb3
$PLAYER_CHAR = Player.Create(#NULL, 2488.5601, -1666.84, 13.38)
```

### Propiedades

La propiedad le permite acceder a los atributos de la clase y/o modificarlos.

Por ejemplo la propiedad `.Money` de la clase `Player` permite operar con la cantidad de dinero del jugador:

```sb3
Player($PLAYER_CHAR).Money += 1000000 // add more money
Player($PLAYER_CHAR).Money > 461@ // check the amount
4@ = Player($PLAYER_CHAR).Money // read the amount and store in variable
```

:::warning
Actualmente, el compilador ignora los espacios en blanco en las [cadena literarias](data-types.md#string-literals) utilizadas en los parámetros de propiedad:

```sb3
0@ = File.Open("file name","wb")
```

se compilará como:

```sb3
0@ = File.Open("filename","wb")
```
:::

## Instancias de clase

Casi todos los miembros de la clase toman una variable como el primer parámetro. Esta variable tiene un identificador de la instancia de clase, que es una entidad concreta del juego a la que se aplica el comando:

```sb3
Player.Build($PLAYER_CHAR)
```

`$PLAYER_CHAR` - la instancia de la clase. 

Para algunas entidades del juego, solo existe una instancia. Un ejemplo de eso sería la cámara que controla lo que el jugador puede ver. Los miembros de clases para tales entidades no requieren una variable con la instancia de clase:

```sb3
Camera.SetBehindPlayer()
```

### Declarando una instancia de clase

Las variables se pueden [declarar](variables.md#var-end-construct) utilizando un nombre de clase como tipo:

```sb3
var
    $PLAYER_CHAR: Player
end
```

Indica al compilador que `$PLAYER_CHAR` contiene una instancia de la clase `Player`. Esta variable puede servir como un alias para el nombre de la clase:

```sb3
if
    $PLAYER_CHAR.Defined
jf @anywhere
```

:::warning
Si una variable sustituye al nombre de una clase, el compilador también lo convierte en el primer parámetro, por lo que no es necesario volver a usarlo en la lista de parámetros:

```sb3
$PLAYER_CHAR.SetClothes("PLAYER_FACE", "HEAD", Head)
```

Es equivalente a:

```sb3
Player.SetClothes($PLAYER_CHAR, "PLAYER_FACE", "HEAD", Head)
```
:::

Las variables declaradas como instancias de una clase se pueden volver a declarar con otro tipo.

### La Clase `Model` 

Los [nombres de los modelos](data-types.md#model-names) son siempre instancias de la clase `Model`:

```sb3
#AK47.Load
  
:loop
wait 0
if
  #AK47.Available
jf @loop
```

Es equivalente a:

```sb3
Model.Load(#AK47)

:loop
wait 0
if
    Model.Available(#AK47)
jf @loop
```

## Constantes de la clase

Los parámetros de clase se pueden asignar a un [tipo enumerado](../edit-modes/enums.txt.md). Hace que el código fuente sea más legible:

```sb3
Player.SetClothes($PLAYER_CHAR, "VEST", "VEST", BodyPart.Torso)
```

El último parámetro (`BodyPart.Torso`)  es un miembro del enumerador `BodyPart` sustituida por `0` durante la compilación. Las enumeraciones con sus valores se definen en el archivo `enums.txt`.

#### Parámetros extendidos (obsoletos)

Sanny Builder antes de v3.6 definió constantes especiales para los miembros de clase en el archivo `classes.db` . Estos parámetros se denominaron extendidos. Esta solución solo permitía un parámetro extendido por miembro de clase.

Desde v3.6 los miembros de la clase usan nombres de enumeración como sus tipos y pueden tener cualquier número de parámetros enumerados.

Para la compatibilidad con versiones anteriores, Sanny Builder sigue siendo compatible con los parámetros extendidos antiguos durante la compilación. classes.db los mantiene bajo la sección `DEPRECATED_ENUMS`.

<script src="../../js/main.min.js"></script>