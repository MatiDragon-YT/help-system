<!DOCTYPE HTML>
<html lang="es">
<head>
  <title>Panel o Lista. Parte II</title>
  <meta charset="utf-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="stylesheet" type="text/css" href="../../style/style.css">
</head>
<body>
[continuando. [lo de aquí](panel1.md)].

En la primera parte vimos una variante simple del panel, ahora añadiremos una dependencia de la cantidad de dinero que tiene el jugador. Y, en consecuencia, siempre es interesante saber cuánto vale el producto, para ello añadiremos una segunda columna.
Vamos a utilizar el opcode:

```sb3
08D6: set_panel $Panel column 1 alignment 2
```

Pero con este opcode sólo hemos añadido una columna, ahora tenemos que añadir filas.

```sb3
08EE: set_panel $Panel column 1 row 0 text_1number GXT 'DOLLAR' number 2@  // $~1~
08EE: set_panel $Panel column 1 row 1 text_1number GXT 'DOLLAR' number 3@  // x ~1~
08EE: set_panel $Panel column 1 row 2 text_1number GXT 'DOLLAR' number 4@  // $~1~
```

Rehagamos nuestro script para que el panel en sí no aparezca desde el principio del juego, sino si el jugador está en un lugar determinado y tiene suficiente dinero. 

```sb3
:PANEL
thread 'PANEL'

:PANEL_1
wait 0
if and
0038:   $ONMISSION == 0 
0256:   player $PLAYER_CHAR defined 
0102: actor $PLAYER_ACTOR near_point 2486.2712 -1650.4073 13.4805 radius 1.0 1.0 1.0 sphere 1
jf @PANEL_1 
if
010A:   player $PLAYER_CHAR money > 100 
// Comprobar si el jugador tiene la cantidad correcta de dinero
jf @PANEL_5
2@ = 100 // valor del producto
3@ = 120 // valor del producto
4@ = 150 // valor del producto
5@ = 200 // valor del producto
6@ = 220 // valor del producto
7@ = 250 // valor del producto
8@ = 270 // valor del producto
01B4: toggle_player $PLAYER_CHAR can_move 0 
0512: show_permanent_text_box 'AMMU_H'  
0581: toggle_radar 0 
08D4: $PANEL = create_panel_with_title 'AMMUN' position 29.0 170.0 width 300.0 columns 2 interactive 1 background 1 alignment 1  
08DB: set_panel $PANEL column 0 header 'DUMMY' data 'HOTKNIF' 'KATANA' 'BASEBAT' 'CHAINSA' 'GA_12' 'GUN8' 'GUN5' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'  
08D6: set_panel $PANEL column 1 alignment 1 
08EE: set_panel $Panel column 1 row 0 text_1number GXT 'DOLLAR' number 2@  // $~1~
08EE: set_panel $Panel column 1 row 1 text_1number GXT 'DOLLAR' number 3@  // x ~1~
08EE: set_panel $Panel column 1 row 2 text_1number GXT 'DOLLAR' number 4@  // $~1~
08EE: set_panel $Panel column 1 row 3 text_1number GXT 'DOLLAR' number 5@  // $~1~
08EE: set_panel $Panel column 1 row 4 text_1number GXT 'DOLLAR' number 6@  // x ~1~
08EE: set_panel $Panel column 1 row 5 text_1number GXT 'DOLLAR' number 7@  // $~1~
08EE: set_panel $Panel column 1 row 6 text_1number GXT 'DOLLAR' number 8@  // $~1~

:PANEL_3
wait 0
if
00E1:   player 0 pressed_key 15
jf @PANEL_2
0581: toggle_radar 1 
03E6: remove_text_box 
08DA: remove_panel $PANEL 
01B4: toggle_player $PLAYER_CHAR can_move 1 
wait 1000 
jump @PANEL_1

:PANEL_5
03D6: remove_styled_text 'NOMONEY'  
00BC: text_highpriority 'NOMONEY'  3000 ms  1 
0002: jump @PANEL_1
```

Ahora tenemos una lista de artículos y sus precios. Todo está bien, pero el panel en sí tiene un aspecto terrible.

Esto sucede porque no especificamos un tamaño de columna y por lo tanto la primera y segunda columnas se crean con el mismo ancho que el panel. Vamos a arreglar esto.
`09DB: set_panel $Panel column 0 width 230` – establece la anchura de la primera columna en 230.
`09DB: set_panel $Panel column 1 width 70` – respectivamente para el segundo - 70.

Ahora tenemos que añadir el resultado de la selección de una línea, es decir, lo que ocurrirá cuando se seleccione una de las líneas de nuestro panel. Utilicemos la variante comentada en el primer artículo. Es muy conveniente usar una tabla de transición para este propósito, pero para la simplicidad del experimento no nos distraeremos con asuntos complicados.
Esto es lo que obtuvimos:

```sb3
:PANEL
thread 'PANEL'

:PANEL_1
wait 0
if and
0038:   $ONMISSION == 0 
0256:   player $PLAYER_CHAR defined 
0102: actor $PLAYER_ACTOR near_point 2486.2712 -1650.4073 13.4805 radius 1.0 1.0 1.0 sphere 1
jf @PANEL_1 
if
010A:   player $PLAYER_CHAR money > 100 
// Check if the player has the right amount of money
jf @PANEL_5
2@ = 100 // valor del producto
3@ = 120 // valor del producto
4@ = 150 // valor del producto
5@ = 200 // valor del producto
6@ = 220 // valor del producto
7@ = 250 // valor del producto
8@ = 270 // valor del producto
01B4: toggle_player $PLAYER_CHAR can_move 0 
0512: show_permanent_text_box 'AMMU_H'  
0581: toggle_radar 0 
08D4: $PANEL = create_panel_with_title 'AMMUN' position 29.0 170.0 width 300.0 columns 2 interactive 1 background 1 alignment 1  
08DB: set_panel $PANEL column 0 header 'DUMMY' data 'HOTKNIF' 'KATANA' 'BASEBAT' 'CHAINSA' 'GA_12' 'GUN8' 'GUN5' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'  
08D6: set_panel $PANEL column 1 alignment 1 
08EE: set_panel $Panel column 1 row 0 text_1number GXT 'DOLLAR' number 2@  // $~1~
08EE: set_panel $Panel column 1 row 1 text_1number GXT 'DOLLAR' number 3@  // x ~1~
08EE: set_panel $Panel column 1 row 2 text_1number GXT 'DOLLAR' number 4@  // $~1~
08EE: set_panel $Panel column 1 row 3 text_1number GXT 'DOLLAR' number 5@  // $~1~
08EE: set_panel $Panel column 1 row 4 text_1number GXT 'DOLLAR' number 6@  // x ~1~
08EE: set_panel $Panel column 1 row 5 text_1number GXT 'DOLLAR' number 7@  // $~1~
08EE: set_panel $Panel column 1 row 6 text_1number GXT 'DOLLAR' number 8@  // $~1~
09DB: set_panel $Panel column 0 width 230 
09DB: set_panel $Panel column 1 width 70

:PANEL_2
wait 0
if 
00E1: player 0 pressed_key 16 // si se pulsa la tecla "ESPACIO".
jf @PANEL_3
08D7: 0@ = panel $Panel active_row // registramos el número de línea
gosub @PANEL_RESULT // ir a la etiqueta PANEL_RESULT

:PANEL_3
wait 0
if
00E1:   player 0 pressed_key 15
jf @PANEL_2
0581: toggle_radar 1 
03E6: remove_text_box 
08DA: remove_panel $PANEL 
01B4: toggle_player $PLAYER_CHAR can_move 1 
wait 1000 
jump @PANEL_1

:PANEL_5
03D6: remove_styled_text 'NOMONEY'  
00BC: text_highpriority 'NOMONEY'  3000 ms  1 
0002: jump @PANEL_1

:PANEL_RESULT 
010B: 14@ = player $PLAYER_CHAR money // escribir en una variable la cantidad de dinero del jugador
if
 0@ == 0 // si se selecciona la primera línea
jf @PANEL_RESULT_2
if
002D: 14@ >= 2@ // la cantidad de dinero que tiene un jugador es mayor o igual que el valor del artículo
jf @PANEL_RESULT_8 
// Si no es así, aparece la inscripción de que el jugador no tiene suficiente dinero
00BE: text_clear_all // eliminar todo el texto
00BA: show_text_styled GXT 'MTG02F' time 3000 style 5
1@ = 0 // introducir una variable
0062: 1@ -= 2@ // restar el valor del producto
0109: player $PLAYER_CHAR money += 1@ 
// establecer la cantidad total de dinero para el jugador, teniendo en cuenta la compra de bienes 
jump @PANEL_RESULT_9 // Ir al marcador de salida

:PANEL_RESULT_2
if
 0@ == 1
jf @PANEL_RESULT_3
if
002D: 14@ >= 3@ 
jf @PANEL_RESULT_8
00BE: text_clear_all 
00BA: show_text_styled GXT 'MTIME1' time 3000 style 5
1@ = 0
0062: 1@ -= 3@
0109: player $PLAYER_CHAR money += 1@ 
jump @PANEL_RESULT_9

:PANEL_RESULT_3
if
 0@ == 2
jf @PANEL_RESULT_4
if
002D: 14@ >= 4@ 
jf @PANEL_RESULT_8
00BE: text_clear_all 
00BA: show_text_styled GXT 'MTIME2' time 3000 style 5 
1@ = 0
0062: 1@ -= 4@
0109: player $PLAYER_CHAR money += 1@ 
jump @PANEL_RESULT_9

:PANEL_RESULT_4
if
 0@ == 3
jf @PANEL_RESULT_5
if
002D: 14@ >= 5@ 
jf @PANEL_RESULT_8
00BE: text_clear_all 
00BA: show_text_styled GXT 'MTIME3' time 3000 style 5 
1@ = 0
0062: 1@ -= 5@
0109: player $PLAYER_CHAR money += 1@ 
jump @PANEL_RESULT_9

:PANEL_RESULT_5
if
 0@ == 4
jf @PANEL_RESULT_6
if
002D: 14@ >= 6@ 
jf @PANEL_RESULT_8
00BE: text_clear_all 
00BA: show_text_styled GXT 'MTIME4' time 3000 style 5 
1@ = 0
0062: 1@ -= 6@
0109: player $PLAYER_CHAR money += 1@ 
jump @PANEL_RESULT_9

:PANEL_RESULT_6
if
 0@ == 5
jf @PANEL_RESULT_7 
if
002D: 14@ >= 7@ 
jf @PANEL_RESULT_8
00BE: text_clear_all 
00BA: show_text_styled GXT 'MTIME5' time 3000 style 5 
1@ = 0
0062: 1@ -= 7@
0109: player $PLAYER_CHAR money += 1@ 
jump @PANEL_RESULT_9

:PANEL_RESULT_7
if
 0@ == 6
jf @PANEL_RESULT_9
if
002D: 14@ >= 8@ 
jf @PANEL_RESULT_8 
00BE: text_clear_all 
00BA: show_text_styled GXT 'MTIME6' time 3000 style 5 
1@ = 0
0062: 1@ -= 8@
0109: player $PLAYER_CHAR money += 1@ 
jump @PANEL_RESULT_9

:PANEL_RESULT_8 // avisar de un problema de efectivo
03D6: remove_styled_text 'NOMONEY'  
00BC: text_highpriority 'NOMONEY'  3000 ms  1 

:PANEL_RESULT_9 // marca de salida
return // a la siguiente línea después de gosub @PANEL_RESULT.
```

En realidad, eso es todo lo que hay que saber sobre los paneles.

<script src="../../js/main.min.js"></script>