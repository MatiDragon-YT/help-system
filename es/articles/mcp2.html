<!doctype html>
<html lang="es">
<head>
  <title>Biblia del Coding - Parte II</title>
  <meta charset="utf-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="stylesheet" type="text/css" href="../../style/style.css">
</head>
<body>

:::center h3
Grand Theft Auto
[orange][San Andreas]
\n
[red][Tutorial de Codificación de Misiones con Sanny Builder]
[gray][Parte II]
[gray][Cómo empezar]
\
\
:::

## Introducción

Hola, en la Parte II vamos a obtener algunos fundamentos de la codificación, crear esferas, actores, coches, marcadores, textos, paneles etc....

Si necesitas mirar la Parte I está [aquí](mcp1.md).

Al igual que en la [Parte I](mcp1.md), te recomiendo que imprimas el tema utilizando la versión imprimible del tema, haciendo clic en imprimir este tema en la parte superior derecha del primer mensaje.

Muchos de los ejemplos aquí utilizados son globales (variables que comienzan con un `$` como : `$markerxx`, `$car00`, `$actor00`, `$myweaps00`) pero esto no significa que no puedas usar locales (variables que terminan con `@` como : `0@`, `1@`, `28@`, `1004@`). Yo utilizo gobales para poder nombrarlos y que se entiendan mejor. Pero cuando vayas a codificar deberías usar locales, por muchas razones son mejores. Solo necesitas usar gloabls cuando necesitas el varaible en dos (o más) hilos diferentes (es bastante raro, y no suele ser mucho más que un varaible).

A menudo daré un número hexadecimal de cuatro dígitos (como `0103`, `02a8`, `00ec`, `032b`, etc...) son opcodes, para tener una descripción escriba el número de cuatro dígitos en una nueva línea en Sanny Builder y pulse `F1`.

La mayoría de los ejemplos se dan en una etiqueta CODE como esta:
```
CODE tag
```

Así que puedes copiar y pegar en Sanny, te recomiendo que lo hagas a menudo, te ayudará a entender. Y no tendrás que escribir el número de opcode y pulsar `F1` cada vez.

### 1. Usando Sanny

Esta parte es la más repetida de la Ayuda de Sanny, pero a veces se explica de manera diferente. Pero aún así puedes informarte en la página de Hotkeys para más detalles.

#### Atajos de teclado

##### F1
El atajo mágico !!!!! ponga un opcode como `004F` y presione `F1` y ahí aparece la magia:

```sb3
004F: create_thread @ODDVE
```

o escriba "thread" y púlselo y vuelva a pulsarlo, Sanny le sugerirá todas esas líneas diferentes:

```sb3
004E: end_thread

004F: create_thread @ODDVEH

00D7: create_thread_with_wasted_busted_check @NONAME_2

03A4: name_thread 'MAIN'

0459: end_thread_named 'INT'

004F: create_thread @ODDVEH
```

`F1` será la tecla que usarás mucho, sólo piensa en lo que quieres (coche, actor, punto de control, marcadores, arma, dinero, pintura, etc...) escríbelo y presiona `F1`. No va a hacer lo que quieres cada vez, pero te pondrá en marcha.
¡¡¡¡¡¡¡`F1` es una especie de google para Sanny!!!!!!!

Pero fíjate que sólo funciona para una y sólo una palabra.

Recuerda (o aprende si no lo sabías) que `F1` obtiene sus sugerencias de opcodes.txt así que asegúrate de haber hecho lo que dice la parte II __introducción__.

##### Ctrl + Shift + <0..9>
hacer marcador, en la línea que está como:

:::center
![](mcp2/tyhtyhety.gif)
:::

Deberían guardarse al guardar el archivo.

##### Ctrl + <0..9>
Ir al marcador número <0..9>, te pone en la línea del marcador (si existe)

##### Ctrl + Num2/Num8
Cuando coloques el cusor (el blinky |) sobre un salto (o `gosub` o `jf`-jump_if_false-)y lo pulses el editor te traerá la etiqueta. Mira la animación para ver que he colocado en `@PSAVE1_1498` y me lleva a la etiqueta en sí `:PSAVE1_1498`

:::center
![](mcp2/azdzefqerf.gif)
:::

##### F3
Buscar siguiente 

##### Ctrl + F3
Buscar anterior

##### Ctrl + Alt 
Mantenida, selección rectangular

:::center
![](mcp2/fgywdfwgwd.gif)
:::

##### Ctrl + Alt + 1
Gestor de Coords (el juego debe estar en marcha)

:::center
![](mcp2/erferfertgyyhze2.png)
:::

##### Ctrl + Alt + C
Insertar las coordenadas del jugador donde está el cursor

##### Ctrl + Alt + E
Insertar el valor del ángulo z donde está el cursor

##### Ctrl + T
Borrar palabra, recuerda que cosas como end_thread serán consideradas como una palabra

##### Ctrl + Y
borrar línea

##### Ctrl + Shift + Y
borrar línea, deja la línea pero borra lo que está escrito en ella

##### Ctrl + Desplazamiento arriba/abajo
subir/bajar página

##### Ctrl + Shift + U/L
hacer que la palabra sea MAYÚSCULA / minúscula

##### Ctrl + Q
comentar la línea en la que se encuentra el cursor (añade // al principio de la línea), para que la línea sea ignorada al compilar

##### Ctrl + G
ir a la línea

##### Ctrl + R
reemplazar

##### Ctrl + C
copiar 

##### Ctrl + V
pegar

##### F7
compilar y copiar en la carpeta de GTA SA main.scm y script.img

#### Completar el editor
Sanny puede completar algunas cosas por ti como variables globales, etiquetas, IDs de modelos, para usarlo solo escribe el primer caracter ($, @, o #) y obtienes lo que necesitas. Mira la animación creo que lo explica bien:

:::center
![](mcp2/ertyydjyj.gif)
:::

Estos son los ejemplos más utilizados, pero puede encontrar muchos usos para esta herramienta.

Fíjate en que con los modelos se muestra el número de nudel, por lo que es una forma bastante fácil de encontrar qué número tiene un modelo.

:::center
![](mcp2/xdfgsdrgs.png)
:::

#### Gestor de Coords

Herramienta muy útil para obtener la posición 3D del jugador. Por supuesto, el juego debe estar funcionando ....

La posición en 3 dimensiones es el valor de cada eje (x, y, z). El mapa de San Andreas va de `-3000.0` a `3000.0` en los ejes x e y. La altitud (z) máxima de los planos es de unas `800,0` unidades. El eje x está orientado al oeste este, el valor de x aumenta al ir al este. El eje y está orientado al sur-norte, el valor de y aumenta al ir al norte. El centro del mapa se encuentra en `0,0 0,0 0,0`.
Pues así parece:

:::center
![](mcp2/SDVDF.gif)
:::

Esta captura de pantalla es del antiguo Sanny Builder, el nuevo gestor de coordenadas es ligeramente diferente.
- **Copy** copiará en el portapapeles la posición 3d como `747.756 -1809.0986 13.0234`.
- **Read** actualizará la posición del jugador en el juego.
- **Set** pondrá al jugador en la posición que has escrito en las tres dimensiones.

En el nuevo Sanny Builder se obtiene el valor del ángulo Z. Este es el valor del ángulo alrededor del eje z que significa en qué dirección está mirando el jugador (CJ). `0.0` es norte, `180.0` es sur, `90.0` es este, `270.0` es oeste.

### 2. Añadir mods de misión

Esto no es realmente codificación, pero es útil para que todo el mundo mire/pruebe/utilice/imite partes de los mods de otros.

Lo primero que hay que saber al añadir mods : ¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡SEGUIR LAS INSTRUCCIONES !!!!!!!!!!!!!!!!!!!! Cada mods es diferente, así que cuando añadas un mod primero sigue las instrucciones del autor para añadirlo, son las mejores instrucciones. Pero a veces no hay muchas instrucciones.

Entonces, la mayoría de los mods son parte principal (mira la parte I, 1. Arquitectura de main.scm) mods. Así que se activan (create_thread) y se ejecutan en la parte principal. Probablemente quieras añadir mods al main.scm original (o al modded `main.scm`), entonces usarás `004F: create_thread` aquí:
```sb3
004F: create_thread @ODDVEH
004F: create_thread @R3
004F: create_thread @GYM
004F: create_thread @SHOOT
004F: create_thread @BLOODR
004F: create_thread @HOTR
004F: create_thread @KICKS
004F: create_thread @PSAVE1
004F: create_thread @FLOW
004F: create_thread @HELP
004F: create_thread @COLLS
004F: create_thread @CRANES
004F: create_thread @BUY_PRO
004F: create_thread @VALET_L
004F: create_thread @ADPLANE
```
Entre cualquiera de esas líneas añadirás:
```sb3
004F: create_thread @modlabel
```
- donde será por supuesto el nombre de la primera etiqueta del mod en lugar de `modlabel`.

Luego tienes el mod en sí, tienes que añadirlo en cualquier lugar de la parte principal. PERO no es realmente en cualquier lugar, es más como en cualquier lugar después de que se ha creado / activado, y no dentro de otro hilo. para simplificar sólo lo puso en el final de la parte principal, por lo que antes:

```sb3
//-------------Mission 0---------------
// Originally: Initial 1
```

Luego compila y copia en San Andreas/data/script (F7), lanza el juego, comienza una nueva partida y...

diviértete...

¡¡¡¡Espera, si realmente ha funcionado, eres como muy muy afortunado (y has hecho un buen trabajo aparentemente) !!!! La mayoría de las veces el primer intento simplemente no funciona. Así que mira lo que has hecho, mira cómo lo ha hecho el autor del mod, mira las variables globales (si sólo son números deberías cambiarlos por un nombre personalizado), el intento de nuevo, y si realmente no puedes conseguir que funcione, pregunta al autor cómo lo ha hecho (hazlo después de varios intentos y comprobaciones, no molestes al autor por un error que hayas cometido).

### 3. Recogidas de armas y coches aparcados
oportunidad de explicar cómo funciona.

Recogidas y coches aparcados son opcode spaecial porque le dice al motor para crear tal coche o tal arma si el jugador está lo suficientemente cerca del punto.

Bueno, voy a poner como ejemplo los coches/vehículos que siempre encuentras en un lugar (el skimmer cerca de la presa, el AT400 en el aeropuerto de Las Vegas, el cropduster en el desierto, estos son los únicos que se me ocurren ahora mismo) cada vez (en realidad no cada vez) que vuelves está aquí. Lo mismo para las armas (los cohetes heatseek en el pueblo abandonado, la pistola detrás de la casa de Sweet...), la vida (el corazón rojo en las guerras de bandas) y las armaduras.

#### Coches aparcados
```sb3
014B: $mycarx00 = init_parked_car_generator #CHEETAH 15 15 1 alarm 0 door_lock 0 0 0 at -1253.1 -348.0 13.9 angle 0.0
014C: set_parked_car_generator $mycarx00 cars_to_generate_to 101
```

* Esto creará un guepardo (`#CHEETAH`).
* en la coordenada 3d `-1253.1 -348.0 13.9`.
* el coche se dirigirá el z_angle `219.0` (z_angle es el ángulo alrededor del eje z, `0.0` y `360.0` es el norte, `180`.0 es el sur, `90.0` es el este, `270.0` es el oeste), por lo que este coche se dirige al sur-oeste.
* Con el número de color uno "15" (se puede notar a veces "1" que significa cualquier).
* Con el color dos número "15" (se puede notar a veces "1" que significa cualquier).
* El coche pertenece al jugador (la policía no piensa que lo has robado) porque el siguiente parámetro es "1". Si se establece en 0 los policías le dará un nivel de búsqueda cuando usted entra en él.
* Sin alarma, porque es "0". Es en realidad un porcentaje de posibilidades de obtener una alarma. Asi que si quieres que la alarma suene cada vez ponlo a "100", si quieres que suene una de cada dos veces entonces "50", etc.
* Las puertas no se bloquearán. Como se puede leer door_lock se establece en `0`. Si 1 puertas serán bloqueadas. Los otros dos parámetros son desconocidos.
* Esta generación de coches (no el coche en sí) se establece como mango `$mycarx00`.

Y te estarás preguntando cómo sé todo eso (además de simplemente leer y pensar) ???????
Pues bien, vaya a comprobar [http://sa-db.webtools4you.net/](http://sa-db.webtools4you.net/), haga clic en ir sin escribir nada, y con su navegador de Internet (si no sabe lo que es entonces es Internet Explorer) vaya a Edition\Find en la página, y busque 014B.

Entonces te saldrá [`014B` descripción](http://sa-db.webtools4you.net/?show=all&mark=014b#014b). Espere hasta que su navegador de Internet le lleve a la descripción 014 4 o 5 segundos), y será en la parte superior de la página th resaltado en amarillo.

La línea después de `014C` sólo activa o desactiva el generador de coches aparcados para este tirador de generación de coches: 101 hace que aparezca y 0 no. Es necesario no olvidar uno para cada generación de coches, pero no tienen que estar juntos puedes separarlos.

Hay otros tipos de coches aparcados que son muy parecidos a este y no son muy útiles.

#### Recogidas de armas

Hay dos tipos de recogidas de armas, una para armas que no necesitan munición (como dildos, katanas, palos de golf, palas, paracaídas....), y es en realidad una recogida normal.

Estos dos opcodes de recogida como con los coches aparcados opcode no necesitan solicitar y cargar el modelo antes de usar.

##### Armas cuerpo a cuerpo (y de regalo)

```sb3
0213: $myweaps00 = create_pickup #CHNSAW       type 15 at -2083.0 298.0 42.0
0213: $myweaps01 = create_pickup #BAT          type 15 at -2306.0  93.0 35.0
0213: $myweaps02 = create_pickup #SHOVEL       type 15 at -2796.4 123.6  6.8
0213: $myweaps03 = create_pickup #POOLCUE      type 15 at -2135.0 197.0 35.0
0213: $myweaps04 = create_pickup #KATANA       type 15 at -2208.0 696.0 50.0
0213: $myweaps05 = create_pickup #BRASSKNUCKLE type 15 at -2206.0 961.0 80.0
```

Puedes usar este opcode para otras recogidas como la tarjeta llave (lo que obtienes con la chica croupier- modelo : `#KEYCARD`), armadura (modelo : `#BODYARMOUR`), vida (modelo : `#HEALTH`), soborno (modelo : `#BRIBE`), discos de guardado (¡sólo la recogida no la rutina de guardado! - modelo : `#PICKUPSAVE`), maletín (modelo : `#BRIEFCASE`) etc...

Todas las armas de cuerpo a cuerpo (incluyendo el paracaídas -`#PARA_PACK` - pero no las gafas), y la salud, la armadura, los sobornos, son hardcoded que significa que usted no tiene que comprobar si el jugador recoge el llamado pickup, sólo tiene que ir a la recogida y automáticamente da el arma/soborno/armadura/salud, etc ....

Para la tarjeta de acceso, el disco de guardado, el maletín, así como todos los modelos de objetos definidos (si no son demasiado grandes, por supuesto), ya que pueden ser utilizados como un pickup, tienes que comprobar si el pickup ha sido recogido o no. No tienes que destruirlo una vez que ha sido recogido, porque en cuanto el jugador esté sobre él desaparecerá automáticamente.
Para esto debes usar algo como esto:

```sb3
00D6: if
0214: pickup $pickupvar picked_up
004D: jump_if_false @
```

##### Armas de fuego (y todas las demás)
Pero para las armas que tienen munición, a excepción de las gafas nocturnas e infrarrojas, (modelos: `#NVGOGGLES`, y `#IRGOGGLES`) tendrás que usar los captadores que dan munición con el arma (¿no parece obvio?):

```sb3
032B: $myweaps06 = create_weapon_pickup #NVGOGGLES 15 ammo 1   at   299.5   -31.7 1001.0
032B: $myweaps07 = create_weapon_pickup #IRGOGGLES 15 ammo 1   at   299.6   -41.3 1001.0
032B: $myweaps08 = create_weapon_pickup #M4        15 ammo 60  at  2021.8  1001.4   10.3
032B: $myweaps09 = create_weapon_pickup #MP5LNG    15 ammo 120 at  2025.2  1001.4   10.3
032B: $myweaps10 = create_weapon_pickup #SHOTGSPA  15 ammo 120 at  2021.0  1013.3   10.3
032B: $myweaps11 = create_weapon_pickup #SATCHEL   15 ammo 20  at  2023.7  1013.5   10.5
032B: $myweaps12 = create_weapon_pickup #SNIPER    15 ammo 60  at -2035.7   139.4   28.3
032B: $myweaps13 = create_weapon_pickup #MICRO_UZI 15 ammo 120 at -2038.4   139.6   28.3
032B: $myweaps14 = create_weapon_pickup #CHROMEGUN 15 ammo 120 at -2038.6   137.4   28.3
032B: $myweaps15 = create_weapon_pickup #GRENADE   15 ammo 20  at -2035.4   137.2   28.3
032B: $myweaps16 = create_weapon_pickup #AK47      15 ammo 120 at  2499.3 -1707.4 1014.2
032B: $myweaps17 = create_weapon_pickup #TEC9      15 ammo 120 at  2499.5 -1709.6 1014.2
032B: $myweaps18 = create_weapon_pickup #SAWNOFF   15 ammo 60  at  2493.4 -1708.2 1014.9
032B: $myweaps19 = create_weapon_pickup #MOLOTOV   15 ammo 20  at  2493.5 -1706.8 1015.1
032B: $myweaps22 = create_weapon_pickup #COLT45    15 ammo 100 at  -365.4 -1422.4   25.5
032B: $myweaps23 = create_weapon_pickup #FLAME     15 ammo 200 at  -366.0 -1418.6   25.5
```
Para esos, el número que aparece después de la munición es el número de municiones que tendrás cuando recojas el arma.

Para todos los recogedores (tanto el `0213` como el `032B`)

- el tipo 1 desactiva la recogida (no puedes recogerla)
- el tipo 3 hace que el recogido se pueda recoger sólo una vez 
- el tipo 15 hace que el pickup vuelva a crecer cada vez que lo recoges (probablemente lo que quieres)

los tres flotadores son las coordenadas del pickup como estos: `-366.0 -1418.6 25.5`.

`$myweapsxx` es el mango del pickup, la variable que lo contiene.

y `#GRENADE` es el modelo.

Una última cosa es que usted puede encontrar una lista de armas aquí.

Hay algunas armas como el jetpack, o el paracaídas donde puedes usar tanto `0213` como `032B` con munición `1` tiene el mismo resultado.

Puedes destruir las camionetas antes de que sean recogidas, con el `0215`:

```sb3
0215: destroy_pickup $myweaps01
```

### 4. Hilos
No hay mucho más que decir que lo que se ha contado en [Parte I: 4. Hilos y etiquetas](mcp1.md#4.-hilos-y-etiquetas).

Sólo que se crean con `004F` así :
```sb3
004F: create_thread @name_of_your_thread
```

Lo nombras con `03A4`:
```sb3
03A4: name_thread 'NAME'
```
No más de 7 letras.

Se puede terminar con `004E` del propio hilo:
```sb3
004E: end_thread
```

O desde cualquier otro lugar con `0459`:
```sb3
0459: end_thread_named 'NAME'
```

A veces puedes encontrar extraños `004F` como:
```sb3
004F: create_thread @AUDIOL $ACTOR_RYDER 0 0 2 1
```

No te preocupes, es sólo la forma de establecer la variable local para el hilo que se crea. Como en el ejemplo `$ACTOR_RYDER` será `0@`, 0 (primero) será `1@`, `0` (segundo) será `2@`, 2 será `3@`, 1 será `4@` en el hilo `:AUDIOL`.

Como te dice Sanny puedes poner hasta 32 locales allí para el hilo que crees desde `0@` hasta `31@` . Puede ser muy útil pero si no lo entiendes realmente no importa, es sólo para que lo sepas.

### 5. Esferas y marcadores
#### Esferas
Una esfera (también llamada cilindro de PDI) es la cosa redonda roja en la que caminas cuando comienzas una misión.

Aunque no veas realmente el círculo rojo, el juego (y tú mientras haces modding) utiliza muchas esferas todo el tiempo. Sólo para comprobar si el actor x está cerca de una coordenada dada dentro de un determinado radio.

¡¡¡¡Para crear esas esferas especiales que comprueban si el actor está cerca de ellas se necesita un tipo especial de opcodes que tienen un comando y son condicionales !!!! Son raros pero muy útiles desde GTA III. Son condicionales porque comprueban si el actor está cerca y mientras tanto muestran la esfera roja dentro del juego.

opcodes 2d desde [00ec](http://sa-db.webtools4you.net/?show=all&mark=00ec#00ec) hasta [00f4](http://sa-db.webtools4you.net/?show=all&mark=00f4#00ec).
```sb3
00EC: actor $PLAYER_ACTOR 0 near_point 1812.369 -1929.9215 radius 80.0 80.0
00ED: actor $PLAYER_ACTOR 0 near_point_on_foot 2043.6801 -1635.73 radius 4.0 4.0
00EE: actor $PLAYER_ACTOR 0 near_point_in_car 2067.3999 -1831.2 radius 15.0 15.0
00EF: actor $PLAYER_ACTOR stopped 0 near_point 2196.239 -1191.459 radius 2.0 2.0
00F0: actor $PLAYER_ACTOR stopped 0 near_point_on_foot 2175.29 -2259.22 radius 3.0 3.0
00F1: actor $PLAYER_ACTOR stopped 0 near_point_in_car -175.29 -59.22 radius 3.0 3.0
00F2: actor 67@ near_actor $PLAYER_ACTOR radius 20.0 20.0 0
00F3: actor $PLAYER_ACTOR near_actor_on_foot 15@ radius 2.0 2.0 0
00F4: actor 34@ near_actor_in_car $PLAYER_ACTOR radius 6.0 6.0 0
```
Esos opcodes no deberían ser usados porque pueden llevar a muchos bugs particularmente en interiores.

opcodes 3d desde [00fe](http://sa-db.webtools4you.net/?show=all&mark=00fe#00fe) hasta [016](http://sa-db.webtools4you.net/?show=all&mark=0106#00fe).
```sb3
00FE: actor $PLAYER_ACTOR 0 near_point_3d 658.0068 -1866.3127 4.4537 radius 15.0 15.0 15.0
00FF: actor $PLAYER_ACTOR 0 near_point_3d_on_foot $X_JOHNSON_HOUSE $Y_JOHNSON_HOUSE $Z_JOHNSON_HOUSE radius 1.0 1.0 2.0
0100: actor $PLAYER_ACTOR near_point_3d_in_car 1793.0676 -1904.2538 12.3989 radius 4.0 4.0 4.0 sphere 1
0101: actor $PLAYER_ACTOR stopped_near_point_3d 10@ 11@ 12@ radius 5.0 5.0 3.0 sphere 0
0103: actor $PLAYER_ACTOR stopped_near_point_3d_in_car -1577.9417 52.6333 16.3281 radius 4.0 4.0 6.0 sphere 0
0104: actor $PLAYER_ACTOR near_actor_3d 1@ radius 15.0 15.0 15.0 sphere 0
0105: actor $PLAYER_ACTOR near_actor_3d_on_foot 102@(42@,9i) radius 5.0 5.0 2.0 sphere 0
0106: actor $PLAYER_ACTOR near_actor_3d_in_car 71@ radius 15.0 15.0 15.0 0
```
¡¡¡Estos opcodes son buenos utilízalos!!!
Para todos estos opcodes:

Donde sea que se coloque el entero `0` o `1` le dice al motor que muestre el círculo rojo (1=mostrar; 0=no mostrar).

Los primeros 2 o 3 (2 para 2D, 3 para 3D) flotantes como `-366.066 -1418.683 25.5` son la posición.

Los segundos 2 o 3 (2 para 2D, 3 para 3D) flotadores como `4.0 4.0 6.0` son el radio de la esfera.

Las variables `$PLAYER_ACTOR` o `71@` , son las variables del actor.

Para que la esfera funcione (y aparezca) hay que colocarla en un bucle:
```sb3
:SPHERE_LOOP
0001: wait 0 ms
00D6: if
00EC: actor $PLAYER_ACTOR 0 near_point 1812.369 -1929.9215 radius 80.0 80.0
004D: jump_if_false @SPHERE_LOOP
```
El hilo debe estar corriendo en el bucle para que la condición se compruebe, y para que el círculo rojo aparezca. La espera es necesaria, no la hagas demasiado larga, de lo contrario parpadeará, mantenla a 0 ms para que sea agradable.

#### Marcadores
##### Iconos de marcadores
Los iconos de los marcadores son los activos del radar a los que vas para hacer una misión. Como ![](%sa-r/icon6.gif), ![](%sa-r/icon11.gif), o ![](%sa-r/icon16.gif), etc.

Gracias a LazioFreak por encontrarlos. Puedes encontrar una lista de los iconos y los números para usarlos en esta página [SA: iconos de radar](http://www.lzfdownloads.de/index2.php?page=46&action=text) o en los contenidos de ayuda de Sanny Builder en [Documentación SCM: GTA SA: iconos de radar](../scm-documentation/sa/radar-icons.md).

Hay cuatro opcodes para crearlos:

Tres sin esfera (como se dice en la descripción del opcode) 04ce y `0570`:
```sb3
04CE: $marker01 = create_icon_marker_without_sphere 12 at 2447.3643 -1974.4963 12.5469
0570: $marker02 = create_asset_radar_marker_with_icon 36 at 1837.3643 -1974.4963 12.5469
02A8: $marker03 = create_marker 23 at 658.0068 -1866.3127 4.4537
```
El `02a8` será visible SIEMPRE en el radar.

Otro con una esfera 02a7:
```sb3
02A7: $marker00 = create_icon_marker_and_sphere 33 at 2514.5601 -1666.84 13.38
```
Al igual que el `02a8`, este es bastante chulo porque estará SIEMPRE visible en el radar, vayas donde vayas lo verás.
Pero ojo, es sólo una esfera (sólo el círculo rojo) no comprueba si el jugador está cerca, sólo el marcador del radar y la esfera. Para comprobar si el jugador está cerca usa los opcodes que se dicen arriba en las esferas.

Todos ellos son opcodes de 5 parámetros:
* `$markerxx` es el handle del marcador.
* Los enteros como `12`, `36`, o `23` son el número de iconos del marcador que puedes encontrar en [Documentación SCM: GTA SA: iconos de radar](../scm-documentation/sa/radar-icons.md).
* Los tres flotantes son las coordenadas x y z.

##### Marcadores de color
Los marcadores de color son eso:
:::center
![](mcp2/rtgerterh.jpg)
:::

Y en el radar están representados por un cuadrado de color como este:

:::center
![](mcp2/rthrtyjty.jpg)
:::

Para crearlos en el vehículo/actor/objeto/recogida:
```sb3
0186: $marker04 = create_marker_above_car $car00
0187: $marker05 = create_marker_above_actor $actor00
0188: $marker06 = create_marker_above_object $object00
03DC: $marker07 = create_marker_above_pickup $myweaps00
```
Cuidado, el vehículo/actor/objeto/recogida debe existir y guardarse en el hanlde, de lo contrario se colgará el juego.
Con el `03dc`, el marcador sobre el pickup siempre será verde hagas lo que hagas con él.
- `$marcadorxx` es el mango del marcador 
- `$car00`, `$actor00`, `$object00`, `$myweaps00` son las asas del vehículo/actor/objeto/recogida 
Puedes cambiar el color del marcador con:
```sb3
0165: set_marker $marker05 color_to 1
```
- 0 : rojo
- 1 : verde
- 2 : azul claro
- 3 : blanco
- 4 : amarillo claro
Con 0168 puedes cambiar el tamaño del marcador (a diferencia de lo que dice la descripción):
```sb3
0168: show_on_radar $marker04 1
```
- < 3 : menor el tamaño por defecto
- = 3 : tamaño por defecto
- > 3 : más grande
Se puede establecer el color del enemigo o del amigo (sólo cambia el color):
```sb3
07E0: set_marker $marker02 type_to 1
```
- 0 : rojo enemigo
- 1 : azul amistoso

Para ocultar y mostrar (tanto los iconos como los marcadores de color) :
```sb3
018B: show_on_radar $marker04 1
```
- 0 : ocultar
- 1 : ocultar
- 2 : mostrar
- 3 : mostrar
- 4 : ocultar
- 5 : ocultar

Para destruirlos (tanto los iconos como los marcadores de color):
```
0164: disable_marker $markerxx
```
Donde, por supuesto, `$marcadorxx` es el mango del marcador.

### 6. Actores, armas y vehículos
¡¡¡¡Ahora vamos a crear algunas cosas para destruirlas!!!!

Primero cuando creas actores, vehículos, objetos, o das armas a un actor, tienes que cargar los modelos y comprobar que están cargados antes de crearlos.

Así que tienes que solicitar los modelos con 0247:
```sb3
0247: request_model #CHEETAH
0247: request_model #DESERT_EAGLE
```

y luego cargar los modelos solicitados con 038b:
```sb3
038B: load_requested_models
```

Después se necesita una comprobación para confirmar que los modelos están realmente cargados (disponibles para que el motor los utilice):
```sb3
00D6: if and
0248: model #HEATSEEK available
0248: model #DESERT_EAGLE available
0248: model #MP5LNG available
0248: model #CR_AMMOBOX available
0248: model #CHEETAH available
0248: model #WFYSEX available
004D: jump_if_false @LOOP_RQ_10
```

El salto si es falso salta a `:LOOP_RQ_10` donde el modelo será solicitado de nuevo cargado.

Todo en uno se ve así :
```sb3
:LOOP_RQ_10
0001: wait 0
0247: request_model #WFYSEX
0247: request_model #CR_AMMOBOX
0247: request_model #CHEETAH
0247: request_model #DESERT_EAGLE
0247: request_model #MP5LNG
0247: request_model #HEATSEEK
038B: load_requested_models
00D6: if and
0248: model #HEATSEEK available
0248: model #DESERT_EAGLE available
0248: model #MP5LNG available
0248: model #CR_AMMOBOX available
0248: model #CHEETAH available
0248: model #WFYSEX available
004D: jump_if_false @LOOP_RQ_10
```

Fíjate en la espera porque es muy importante aquí haces un bucle, haces que el motor vuelva a donde estaba antes. Necesitas esta espera, esperar 0 ms es suficiente, no necesitas más, pero SÍ la necesitas.


Ahora que los modelos están disponibles puedes crear tus actores/vehículos/objetos, y dar armas a los actores :

```sb3
00A5: $car00 = create_car #CHEETAH at 2368.5601 -1666.84 13.38
009A: $actor00 = create_actor 22 #WFYSEX at 2490.56 -1666.84 14.38
009A: $actor01 = create_actor 22 #WFYSEX at 2495.56 -1666.84 14.38
009A: $actor02 = create_actor 22 #WFYSEX at 2490.56 -1660.84 14.38
009A: $actor03 = create_actor 22 #WFYSEX at 2495.56 -1660.84 14.38
0107: $object00 = create_object #CR_AMMOBOX at 2502.5601 -1566.84 24.38
01B2: give_actor $actor00 weapon 36 ammo 900 // Load the weapon model before using this
01B2: give_actor $actor01 weapon 29 ammo 900 // Load the weapon model before using this
01B2: give_actor $actor02 weapon 24 ammo 300 // Load the weapon model before using this
01B2: give_actor $actor03 weapon 29 ammo 900 // Load the weapon model before using this
```
* Para crear un coche con `00a5`:
  * `$car00` es el mango del coche.
  * `#CHEETAH` es el modelo. Utiliza el editor de finalización para tener una lista de modelos (cuidado que hay TODOS los modelos, coches, actores, objetos).
  * `2490.56 -1666.84 14.38` son las coordenadas.
* Crear actor con `009a`:
  * `$actor00` es el handle del actor.
  * `#WFYSEX` es el modelo. Usa el editor para tener una lista de modelos.
  * El entero 22 es el tipo de ped. El tipo ped es el temperamento, la forma en que el actor actuará por defecto (sin ningún orden dado). Puedes encontrar una lista de tipos de ped en los contenidos de ayuda de Sanny Builder en la documentación de SCM: GTA SA: PedTypes. Aquí no les gustan los policías y se defenderán si son atacados, entre otras cosas.
  * `2368.5601 -1666.84 13.38` son las coordenadas.
* 0107 creará un objeto : 
  * `$object00` es el handle del objeto 
  * `#CR_AMMOBOX` es el modelo. Aquí puedes usar cualquier objeto definido en los archivos de mapa (archivos .ide en la carpeta GTA San Andreas\data\maps) siempre que hayas definido el objeto en la sección definir objeto en el scm. Ver [Parte I: 1. Arquitectura main.scm](mcp1.md#1.-main.scm-architecture).
    * `2502.5601 -1566.84 24.38` son las coordenadas.
    * Finalmente 01b2 da a un actor un arma:
  * `$actor00` es el mango del actor 
  * El primer número entero 36, 29 o 24 después de arma es el número de arma. Puedes encontrar una lista de armas, sus números y el modelo correspondiente en los contenidos de ayuda de Sanny Builder en [SCM Documentation: GTA SA: Weapon numbers](../scm-documentation/sa/weapons.md).
    * El segundo entero 300 o 900 después de la munición es el número de municiones que se le da al actor junto con su arma. Aquí depende mucho del tiempo de vida del actor, pero he encontrado que 150 es normalmente suficiente.
- Aunque se pueda pensar que este opcode no necesita que se cargue el modelo del arma, pero SI NECESITA que se cargue el modelo. Para asegurarte puedes mirar el comentario (después de //) que dice: `"// Cargar el modelo del arma antes de usar esto"`.

¡¡¡¡Ahora están listos para disparar pero tendrán que esperar un poco!!!!

Puede que necesites cambiar la dirección del coche (ángulo z), o sus colores (hay hasta cuatro colores para los coches):

```sb3
0175: set_car $car00 z_angle_to 90.0
0229: set_car $car00 color_to 11 0
0A11: set_car $car00 tertiary_color_to 6 quaternary_color_to 12
```

Es bastante fácil así que voy a ser rápido:
¡¡¡¡* para todos ellos `$car00` es el mango del coche como siempre !!!! 
* En 0175 el float `90.0` es el ángulo z de `-360.0` a `360.0` .
* En 0229 los enteros `11` es el color primario y `0` es el color secundario.
* En 0a11 los enteros `6` es el color terciario y `12` es el color cuaternario. No te preocupes por la descripción del opcode, en el momento en que se escribió el sascm.ini utilizado aquí (el que se dio con Sanny) aún no se conocía el opcode, pero aún así funcionará muy bien, y ahora que sabes lo que hace el opcode, ¡¡¡no hay nada que te detenga!!!

Para información, esos eran solo opcodes "set", pero hay opcodes opuestos para todos esos opcodes "get". Los opcodes "get" obtienen el color/ángulo del coche y lo almacenan en variables:

```sb3
0174: $carangle = car $car00 z_angle
03F3: get_car $car00 color $primcolor $scndcolor
0A12: get_car $car00 tertiary_color_to $tertcolor quaternary_color_to $quatcolor
```

Bueno, qué puedo decir, es lo mismo que antes pero al revés, en lugar de decirle al motor que ponga un color en el coche o que ponga el coche en tal dirección, encuentra de qué color es el coche o en qué dirección está el coche y pone el entero resultante en la variable.
Los nombres de las variables deberían hablar por sí mismos.

¡¡¡¡¡¡Por muy obvio que sea, no se puede cambiar el color de un actor !!!!!! Pero puedes cambiar la dirección a la que mira, y saber hacia dónde mira:
```sb3
0172: $actorangle = actor $actor00 z_angle
0173: set_actor $actor00 z_angle_to 262.0
```

Lo mismo para los objetos, aquí es sólo el ángulo, pero hay más en los objetos justo a continuación :
```sb3
0176: $objangle = object $object00 z_angle
0177: set_object $object00 z_angle_to 180.0
```

Cuando termines de usar esos actores/vehículos/objetos tienes dos formas diferentes de deshacerte de ellos: 
En primer lugar, puedes simplemente destruirlos (desaparecerán de inmediato):
```sb3
00A6: destroy_car $car00
0108: destroy_object $object00
009B: destroy_actor_instantly $actor01
034F: destroy_actor_with_fade $actor00 // The actor fades away like a ghost
```
034f como dice el comentario `"// El actor se desvanece como un fantasma"`. Es lo que suele ocurrir en el juego original cuando un actor está muerto, desaparece lentamente.

En segundo lugar puedes eliminar las referencias de ellos, esto los convertirá en peatones normales (como cualquier peatón/vehículo que veas en el juego). Así que el actor/vehículo/objeto seguirá estando ahí pero el handle `$car00`/`$actor00`/`$object00` ya no contendrá al actor/vehículo/objeto:
```sb3
01C2: remove_references_to_actor $actor00 // Like turning an actor into a random pedestrian
01C3: remove_references_to_car $car00 // Like turning a car into any random car
01C4: remove_references_to_object $object00 //This object will now disappear when the player looks away
```
Los comentarios (después de //) explican bastante bien lo que hacen los opcodes.

Ahora que los actores/vehículos/objetos son destruidos el motor ya no necesita los modelos. Así que para liberar algo de memoria del juego ~~puedes~~ liberar los modelos con 0249 :
```sb3
0249: release_model #WFYSEX
0249: release_model #CR_AMMOBOX
0249: release_model #CHEETAH
0249: release_model #DESERT_EAGLE
0249: release_model #MP5LNG
0249: release_model #HEATSEEK
```

##### Sobre los actores especiales
Actores como Ryder, BigSmoke, Sweet, Tenpeny, Pulaski, Toreno, Ceasar, Jethro etc... son llamados actores especiales, no son como los ped habituales que encuentras en la calle. ¡¡¡¡Entonces, en la codificación scm, se manejan de una manera especial, con opcodes especiales esos actores tan especiales !!!! 

Es así:
```sb3
023C: request_special_actor 'TENPEN' as 1
023C: request_special_actor 'PULASKI' as 2
023C: request_special_actor 'HERN' as 3
023C: request_special_actor 'CESAR' as 4
023C: request_special_actor 'JETHRO' as 5
023C: request_special_actor 'MADDOGG' as 6
```

Aquí ha solicitado los modelos `'TENPEN'`, `'PULASKI'` y `'HERN'` y los ha definido como modelos de actores especiales enteros `1`, `2` y `3`.
Puedes encontrar una lista de actores especiales en los contenidos de ayuda de Sanny Builder en la documentación de SCM: GTA SA: Actores especiales. Sólo puedes definir 9 modelos de actores especiales al mismo tiempo (enteros del 1 al 10 incluidos).

Entonces tienes que comprobar si los actores especiales están cargados y crearlos a pie o en coche con los opcodes habituales de create_actor :
```sb3
wait 0
00D6: if and
023D:  special_actor 1 loaded
023D:  special_actor 2 loaded
023D:  special_actor 3 loaded
023D:  special_actor 4 loaded
023D:  special_actor 5 loaded
023D:  special_actor 6 loaded
0248:  model #COPCARLA available                  
004D: jump_if_false @check_1
00A5: 0@ = create_car #COPCARLA at 2431.609 -1254.061 22.8303
01C8: 1@ = create_actor 23 #SPECIAL01 in_car 0@ passenger_seat 0
0129: 2@ = create_actor 23 #SPECIAL02 in_car 0@ driverseat
01C8: 3@ = create_actor 23 #SPECIAL03 in_car 0@ passenger_seat 1
009A: 4@ = create_actor 23 #SPECIAL04 at 2436.609 -1254.061 22.8303
009A: 5@ = create_actor 23 #SPECIAL05 at 2437.609 -1254.061 22.8303
009A: 6@ = create_actor 23 #SPECIAL06 at 2438.609 -1254.061 22.8303
```

Cuando se crean los actores, se ve cómo el modelo se llama `#SPECIAL01` y que hace referencia al entero utilizado en `023c: request_special_actor`. Luego los modelos de actores especiales se nombran desde `#SPECIAL01` hasta `#SPECIAL10`.

Si necesitas otros actores especiales más tarde (como en otra misión) sigue el mismo procedimiento con diferentes nombres de modelos pero manteniendo `1`, `2` y `3` como modelo de actor especial entero, como :
```sb3
023C: request_special_actor 'SMOKE' as 1
023C: request_special_actor 'TORINO' as 2
023C: request_special_actor 'SWEET' as 3
023C: request_special_actor 'TRUTH' as 4
023C: request_special_actor 'CESAR' as 5
023C: request_special_actor 'MADDOGG' as 6
023C: request_special_actor 'ZERO' as 4
```

Para liberar el modelo basta con utilizar 0296 con el modelo especial de actor entero, y destruir el actor con los habituales 009b, 034f o 01c2 :
```sb3
0296: unload_special_actor 1
0296: unload_special_actor 2
0296: unload_special_actor 3
0296: unload_special_actor 4
0296: unload_special_actor 5
0296: unload_special_actor 6
009B: destroy_actor_instantly 0@
009B: destroy_actor_instantly 1@
009B: destroy_actor_instantly 2@
009B: destroy_actor_instantly 4@
009B: destroy_actor_instantly 5@
009B: destroy_actor_instantly 6@
00A6: destroy_car 3@
```

### 7. Objetos y moverlos : 

En Grand Theft Auto San Andreas hay muchas cosas que puedes hacer con los objetos, pero no te lo voy a contar todo (ni puedo). Te daré lo suficiente para hacer cosas bonitas y divertidas con ellos.

No voy a repetir (aunque repita muchas cosas) cómo solicitar y cargar modelos para crear objetos, está dicho arriba. Tampoco repetiré cómo destruir/quitar referencias y liberar objetos por la misma razón.

Primero algunas cosas generales (generales porque hay opcodes similares para vehículos y actores) sobre coordenadas y offsets. A veces se necesita saber dónde está colocado el objeto, y a veces se necesita saber cuáles son las coordenadas a un poco de distancia del objeto (esto se llama un offset) :
```sb3
01BB: store_object $object00 position_to $objpos_x $objpos_y $objpos_z
0400: create_coordinate $objpos_x $objpos_y $objpos_z from_object $object00 offset 1.0 2.0 3.0
```
01bb simplemente obtendrá las coordenadas x y z del objeto `$objeto00` y las almacenará en `$objpos_x $objpos_y $objpos_z`.
En 0400 almacenará las coordenadas en los desplazamientos `1.0 2.0 3.0` del objeto. Significa que las coordenadas que se almacenarán en `$objpos_x $objpos_y $objpos_z` estarán a `1.0` unidad del objeto en el eje z del objeto, `2.0` unidad del objeto en el eje y del objeto, y `3.0` unidad del objeto en el eje z del objeto.

Como he dicho hay opcodes similares para coches y actores, la explicación es la misma que antes solo hay que sustituir objeto por coche o actor : 
Para los vehículos :
```sb3
00AA: store_car $car00 position_to $carpos_x $carpos_y $carpos_z
0407: create_coordinate $carpos_x $carpos_y $carpos_z from_car $car00 offset 1.0 2.0 3.0
```
Para los actores :
```sb3
00A0: store_actor $actor00 position_to $actorpos_x $actorpos_y $actorpos_z
04C4: create_coordinate $actorpos_x $actorpos_y $actorpos_z from_actor $actor00 offset 1.0 2.0 3.0
```

Una vez creado el objeto, puede moverlo a nuevas coordenadas:
```sb3
01BC: put_object $object00 at 2368.5601 -1666.84 13.38
```
Para información los opcodes similares para actores y coches son : 00a1 para los actores y 00ab para los coches.

Puedes usar esto para hacer que el objeto se mueva, no es la mejor manera de hacerlo pero es simple y funciona bien. Aquí hay un ejemplo :
```sb3
:MOVE_10
0001: wait 3 ms
00D6: if
0031:  0@ >= 200.0 // floating-point values
004D: jump_if_false @MOVEND
01BC: put_object $object00 at 0@ 1@ 2@
000B: 0@ += 0.1 // floating-point values
0002: jump @MOVE_10
```
Puedes ver aquí que el objeto se moverá a lo largo del eje x, ya que cada 3ms el objeto irá `0.1` unidad de distancia más allá en el eje x hasta que `0@` (que es aquí la unidad del eje x) alcance `200.0`. ¡¡¡¡¡¡NO OLVIDES EL WAIT!!!!!!

De la misma manera puedes cambiar los ángulos x y y z del objeto creado
```sb3
0453: object $object00 set_rotation 0.0 0.0 180.0
```
Igual que 01bc con un pequeño bucle puedes hacer que el objeto gire. Y como para 01bc hay mejores maneras de hacerlo, pero esta es simple y funciona bien. Además tiene la ventaja de que te da la posibilidad de cambiar cualquier ángulo x y o z.

Pero también puedes usar 034d para que se mueva alrededor del eje x:
```sb3
034D: object $object00 at_angle 0.0 then_rotate_by_angle 360.0 flag 0
```
Así es como Rockstar abre muchas puertas de garaje (no todas).

Pero antes de usar los opcodes de movimiento (como 034d o 0381) necesitas darle al objeto la capacidad de moverse (los objetos no son capaces de moverse cuando los creas):
```sb3
0392: object $object00 toggle_in_moving_list 1
```
* 0 : incapaz de moverse
* 1 : capaz de moverse
También puede lanzar el objeto (como si lanzara una pelota) con 0381 :
```sb3
0381: throw_object $object00 distance 20.0 10.0 5.0
```
* 20 es la distancia en el eje X
* 10 es la distancia en el eje Y
* 5 es la distancia en el eje Z
Puedes unir el objeto a los coches, actores u otros objetos:
```sb3
0681: attach_object $object00 to_car $car00 at_offset 0.5 0.3 0.3 rotation 0.0 0.0 0.0
069B: attach_object $object00 to_actor $actor00 at_offset 0.0 0.5 -1.0 rotation 0.0 0.0 0.0
069A: attach_object $object00 to_object $object01 at_offset 0.0 0.0 0.0 rotation 0.0 0.0 0.0
```

Estos son bastante obvios, y como ya expliqué lo de los offsets, sólo tengo que decir que los tres flotadores después de la rotación son los ángulos x y z que se establecerán, y que el objeto no es sólido cuando se une a otra cosa.

Si no quieres que el objeto sea sólido puedes desactivar la detección de colisiones 
```sb3
0382: set_object $object00 collision_detection 0
```
* `0` no sólido 
* `1` sólido 
Puedes hacer que el objeto sea elevable de esta manera:
:::n
![](mcp2/TRXCT.jpg)
:::

con 08e9:
```sb3
08E9: set_object $object00 liftable 1
```
Como siempre
* `1` : encendido
* `0` : apagado

Esto es una cosa muy muy chula (gracias a DexX y PLPynton):

:::n
![](mcp2/azerdfy.jpg)
:::

¡¡¡¡¡¡¡Puedes adjuntar objetos a partes del cuerpo del actor, de hecho puedes adjuntar 2 objetos al actor y hacer que el actor haga una animación !!!!!!!
Habrá más sobre la animación más tarde, pero voy a explicar estos ahora:

```sb3
070A: AS_actor $PLAYER_ACTOR attach_to_object $object00 offset 0.0 0.0 0.0 on_bone 6 16 perform_animation "NULL" IFP_file "NULL" time 0
```
adjuntar el primer objeto (objeto A):

```sb3
09A0: actor $PLAYER_ACTOR attach_object $object01 with_offset 0.0 0.0 0.0 on_bone 5 16 perform_animation "NULL" IFP_file "NULL" time -1
```
adjuntar el segundo objeto (objeto B):

Para ambos:
* `$PLAYER_ACTOR` es el handle del actor (aquí eres tú CJ) 
* los tres flotantes `0.0 0.0 0.0` son los desplazamientos x y z de las coordenadas normales del hueso 
* El primer entero `5` o `6` es el número de hueso (no es el mismo que el número de las partes del cuerpo del jugador para la ropa).
  * 0" - se bloquea el juego. 
  * `1` - ¿pecho? 
  * `2` - ¿cuello? 
  * `3` - hombro izquierdo 
  * 4 - hombro derecho 
  * 5. Mano izquierda 
  * 6. Mano derecha 
  * 7. ¿Raíz? 
  * `8` - ?? 
  * `9` - pie izquierdo 
  * 10 - pie derecho 
  * 11 - rodilla derecha 
  * `12` - ?? 
  * 13 - codo izquierdo 
  * 14 - codo derecho 
  * 15 - clavícula izquierda 
  * 16 - clavícula derecha 
  * 17 - parte posterior del cuello 
  * 18 - boca o barbilla 
  * `19` - se bloquea el juego 
* El segundo entero "16" es desconocido y siempre es "16". 
* La primera cadena larga `"NULL"` es el archivo de animación (más adelante, y tiene que ser cargado como los modelos) donde el motor obtendrá la animación. Aquí `"NULL"` significa que no se hará ninguna animación.
* La segunda cadena larga "NULL" es la animación en sí misma dentro del archivo de animación. Aquí `"NULL"` significa de nuevo que no se hará ninguna animación.
* El último entero `0`, `1` o `-1` es desconocido 
Para quitar estos objetos tienes dos opcodes:

```sb3
070B: set_actor $PLAYER_ACTOR onbone_attached_object_operation 1 // drop or detaches object B automatically with object A 
09A1: set_actor $PLAYER_ACTOR onbone_attached_objectB_operation 0 // drop or detaches object B only
```

070b separa/suelta el objeto A y B si el objeto B está unido.
09a1 separa/suelta el objeto B.
* `$PLAYER_ACTOR` sigue siendo el handle del actor (aquí eres tú CJ).
* El número entero "1" o "0" indica si el objeto será eliminado o si sólo se ha eliminado.
  * 1` suelta el objeto 
  * `0` desprende el objeto 

:::center
!!!!!!!!!!!!!!!!!!!!!!!!!!! cuidado !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Agarra el objeto A primero porque si no el objeto B no aparecerá. Cuando sujetes el objeto A no podrás subirte a un coche y tu arma no será visible. Si sostienes dos objetos, el objeto B caerá cuando te caigas o dispares con un arma. Si mueres, ambos objetos caerán.
:::
\
Como he dicho hay muchas cosas que hacer con los objetos en San Andreas pero ahora ya sabes las más importantes que puedes hacer.

### 8. Animaciones y partículas

#### A. Animaciones

¡¡¡¡¡¡¡¡¡¡¡¡¡¡Vamos a llegar a algo muy importante en GTA: Las animaciones de los actores !!!!!!!!!!!!!!
El tema de las animaciones en GTA es interminable pero al igual que con los objetos veremos las cosas más importantes.

Primero digamos que son las secuencias de animación, son animaciones de un actor. Como cuando CJ mueve los brazos mientras habla, cuando Ryder está fumando, cuando CJ está usando la llave-tarjeta, o cuando CJ está usando el paracaídas...

:::n
![](mcp2/dfghvjgn.gif)
:::

Más generalmente, cuando un actor está haciendo una acción, ¡cuando ha sido animado!

Hay dos tipos de secuencias de animación (AS), una que hace una animación precisa y otra que hace la animación que pones como parámetros en el opcode desde los archivos anims (*.ifp en el archivo anim.img en la carpeta anim del directorio del juego). El segundo tipo necesita que la animación se cargue con 04ed, y como siempre hay que comprobar si la animación está cargada.

##### Primer tipo de animaciones: un opcode hace una animación

algunos ejemplos:
```sb3
05BA: AS_actor $actor00 chew_gum 10000 ms
05BB: AS_actor $actor00 fall_down 0 time_on_ground 500
05BC: AS_actor $actor00 jump 1
05C5: AS_actor $actor00 cower 3000 ms
05C7: AS_actor $actor00 use_atm
05C8: AS_actor $actor00 look_around
05C9: AS_actor $actor00 on_guard 2000 ms
05CD: AS_actor $actor00 exit_car $car00
05D3: AS_actor $actor00 go_to 81.42 1474.63 51.56 speed 4 3000 ms
05D4: AS_actor $actor00 rotate_angle 77.0
0639: AS_actor $actor00 rotate_to_actor $actor01
0672: AS_actor $actor00 attack_car $car00
0673: AS_actor $actor00 dive_to_offset 1.0 -3.0 time_on_ground 1000 ms
0676: AS_actor $actor00 in_car $car00 move_from_passengerseat_to_driverseat
0677: AS_actor $actor00 chat_with_actor $actor02 1 1
06A9: AS_actor $actor00 look_at_point 681.81 -474.15 15.53 5000 ms
06E3: AS_actor $actor00 roll_sideways 1
```

Son los más fáciles de usar, sólo hay que poner el opcode, los parámetros adecuados (el primero es el actor que hará la animación (`$actor00`), luego hay como se dice en la descripción o un ángulo (`77.0`), una posición 3D (`681.81 -474.15 15.53`), un coche (`$car00`), otro actor (`$actor01`), el tiempo que durará la animación (`5000`) etc....
¡¡¡¡¡¡Son fáciles, sencillos y funcionan muy bien, no dudes en usar muchos de ellos !!!!!!

##### Animaciones de segundo tipo: un opcode hace la animación que has puesto como parámetros.

algunos ejemplos:
```sb3
0829: actor $actor00 perform_animation "CRCKDETH3" IFP_file "CRACK" 4.0 time 0 and_dies 
0605: actor $actor00 perform_animation_sequence "PASS_RIFLE_PED" IFP_file "MISC" 4.0 loop 0 0 0 0 time -1 // versionA
0812: AS_actor $actor00 perform_animation "LRGIRL_IDLELOOP" IFP_file "LOWRIDER" 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 // versionB
0A1A: actor $actor00 perform_animation "HIKER_POSE_L" IFP_file "MISC" 4.0 loopA 0 lockX 0 lockY 0 lockF 1 -1 ms // versionC
088A: actor $actor00 perform_animation "RAIL_FALL" IFP_file "SWAT" 8.0 loopA 0 lockX 0 lockY 0 lockF 1 limT -1 unknown 0 unknown 1 
070A: AS_actor $actor00 attach_to_object $object00 offset 0.0 0.0 0.0 on_bone 6 16 perform_animation "NULL" IFP_file "NULL" time 1 
09A0: actor $actor00 attach_object $object01 with_offset 0.0 0.0 0.0 on_bone 5 16 perform_animation "NULL" IFP_file "NULL" time -1
```

O con las descripciones de sascm.ini de PLPynton muy buenas se ven así:
```sb3
0829: char $actor00 perform_animation "CRCKDETH3" group "CRACK" 4.0 limT 0 and_dies
0605: char $actor00 perform_animation "PASS_RIFLE_PED" group "MISC" 4.0 loopA 0 lockX 0 lockY 0 lockF 0 limT -1 //versionA
0812: char $actor00 perform_animation "EAT_VOMIT_P" group "FOOD" 4.0 loopA 0 lockX 0 lockY 0 lockF 0 limT -1 //versionB
0A1A: char $actor00 perform_animation "VEND_EAT1_P" group "VENDING" 4.0 loopA 0 lockX 0 lockY 0 lockF 0 limT -1 //versionC
088A: char $actor00 perform_animation "RAIL_FALL" group "SWAT" 8.0 loopA 0 lockX 0 lockY 0 lockF 1 limT -1 unknown 0 unknown 1
070A: set_char $actor00 attach_objectA $object00 with_offset 0.0 0.0 0.0 on_bone 6 16 perform_animation "NULL" group "NULL" time 1 //object no 1
09A0: set_char $actor00 attach_objectB $object00 with_offset 0.0 0.0 0.0 on_bone 5 16 perform_animation "NULL" group "NULL" time -1 //object no 2
```

En primer lugar para aquellos que tienen que **__cargar el archivo de animación__** antes de usar el opcode de animación. Es aesy, es igual que lo que pedimos antes los modelos de coches y actores:
```sb3
:ANIMLOAD
0001: wait 0 ms
04ED: load_animation "FOOD"
00D6: if
04EE: animation "FOOD" loaded
004D: jump_if_false @ANIMLOAD
```

Como habrás notado es un poco diferente que con los modelos, porque aquí cargas directamente el anim, y con los modelos, solicitas (con 0247) y luego cargas los modelos (con 038b).

No olvides **__después de usarlo__** liberar el anim con 
```sb3
04EF: release_animation "LOWRIDER"
```

Aquí para explicar los parámetros de los opcodes usaré las descripciones de sacsm.ini de PLPynton en lugar de usar las descripciones originales de Sanny Builder sascm.ini como suelo hacer, porque será mucho más fácil de entender. Pero no os preocupéis que es el mismo orden de los parámetros.

Parámetros (gracias ceedj):
1. La primera palabra entre comillas es el nombre de la animación (`"CRCKDETH3"`, `"PASS_RIFLE_PED"`, `"HIKER_POSE_L"`, etc...) 
2. El segundo es la librería (archivo de animación) donde está la animación (`"CRACK"`, `"SWAT"`, `"VENDING"`, `"FOOD"`, etc...).\NEn algunos casos, como para 09a0 y 070a `"NULL"` significa que no se hará ninguna animación, ambos se explican en el capítulo 7.Objetos y su desplazamiento.
3. `4.0` es la velocidad de reproducción. Por defecto es `4.0`, menos de esto lo ralentiza, más lo acelera y puede saltarse cuadros de animación con números más altos.
4. loopA o bucle Es la bandera de bucle, para hacer que la animación comience de nuevo una vez terminada. 1 on/0 off, PERO si está on, Timelimit (parámetro 8.) DEBE ser puesto a -1 .
5. lockX Desbloquea o bloquea al actor en su posición en el eje X. Si es 1 y si el actor se está moviendo en el eje X desde el origen del actor (de izquierda a derecha), se quedará en esas nuevas coordenadas. Si es 0, el actor volverá a las coordenadas de la posición inicial.
6. lockY desbloquea o bloquea al actor en su posición en el eje Y. Si es 1 y el actor se mueve en el eje Y desde el origen del actor (de adelante hacia atrás), se mantendrá en esas nuevas coordenadas. Si es 0, el actor volverá a las coordenadas de la posición inicial.
7. lockF bloquea el actor en la posición final. Si es 1, se reproducirá la animación una vez, y luego se bloqueará al actor en el último cuadro de la animación. Esto NO puede ser detenido con el opcode 0687 (clear_actor_task), pero si se puede proceder a otra animación.
8. limT el Timelimit en milisegundos, es el tiempo que el actor hará la animación, ajustado a 1000 ms para 1 segundo, 6000 ms para 6 segundos etc... Si se establece en -1 se reproducirá toda la animación, una vez. Con la bandera de bucle activada (1 ), la animación se repetirá hasta que sea interrumpida por el opcode 0687 (clear_actor_task) o por otra animación.

Para encontrar el nombre de las animaciones y en que archivo de animación se encuentran puedes buscar en el muy buen archivo escrito por ceedj : [SAAnims.txt](http://www.pawfectfilms.com/mods/SAAnims.txt), pero también puedes pensar en donde se ha usado esta animación en el juego original, ir a la misión o al script externo en el main.scm original que se usa y buscar con Sanny Builder el anim que quieres en la misión/externo que lo has visto.

#### B. Partículas

crear partícula\
lanzar ángulos\
activar/desactivar partículas

### 9. Animación de vehículos y mods de coches

comportamiento del conductor\
seguir camino/carretera\
metodo de "poner en"\
como el comportamiento del conductor\
para avión/coches/barcos\
carmods, request set, numbers\
carparts, abrir/cerrar romper/reparar\
remolques enganchar\
neumáticos, inflar/desinflar

### 10. Fuego, explosiones e interiores

fuego extinguir, destruir todos los fuegos\
tipo de explosión\
interiores:\
jugador\
actor/coche/objeto

### 11. Cámaras y dirección de las escenas de la película

¡¡¡¡¡tú eres el director !!!!!\
Moviendo la cámara\
cámara sobre el vehículo/sobre el actor mirando al vehículo\
entrada y salida de la toma de la cámara\
usar animaciones\
omitir escenas de corte

### 12. Textos y paneles

paneles\
cuadros de texto\
cajones de texto\
gxt

### 13. Matemáticas, estadísticas y temporizadores

variables e int y floats donde se puede utilizar un float o un int se puede utilizar una variable que contenga el float o el int\
+/-\
*/\
==\
estadísticas\
temporizador internos 32@ y 33@

### 14. Aleatoriedad y tabla de saltos

Random\
tabla de saltos

### 15. Arreglos

### 16. Misiones y scripts externos

misión de inicio/externa\
Una misión a la vez, muchas externas al mismo tiempo\
rango local\
Limpieza de la misión

### Conclusión

opcode search ****\
hacer experimentos\
prueba fácil para principiantes\
Implantar el código existente\
usa tu cerebro !!!\
multiplicar las fuentes de información\
utilizar la búsqueda de google en el foro gta dominio, o gtaforum herramienta de búsqueda.
preguntar hay un montón de buenos chicos (hola chicos 😉 ), yo estaba como tú hace un tiempo, y alguien me ayudó (gracias de nuevo Dem), ahora estoy aquí escribiendo un tutorial.
CRÉDITOS: cns (no estaría aquí sin él), Demarest (ídem), Seeman (creó Sanny Builder !!!!), PLPynton, ceedj, Y_less, PatrickW, Gangsta Killa, Bigun, SteaVor pero también cualquiera que haya publicado la descripción de los opcodes (como Opius, spaceeinstein, Craig Kostelecky, DexX. ..), y cualquier persona que me ayudó o me ayudará para esto, así como las personas que señalan los errores que he hecho en la biblia, y tal vez usted pronto ...

Que se diviertan

<script src="../../js/main.min.js"></script>