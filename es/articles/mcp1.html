<!DOCTYPE HTML>
<html lang="es">
<head>
	<title>Biblia del Coding - Parte I</title>
	<meta charset="utf-8">
	<meta http-equiv="X-UA-Compatible" content="IE=edge">
	<meta name="viewport" content="width=device-width, initial-scale=1">
	<link rel="stylesheet" type="text/css" href="../../style/style.css">
</head>
<body>

:::center h3
Grand Theft Auto
[orange][San Andreas]
\n
[red][Tutorial de Codificaci√≥n de Misiones con Sanny Builder]
[gray][Parte I]
[gray][C√≥mo empezar]
\
\
:::

## Introducci√≥n 

Hola, desde hace tiempo quer√≠a hacer un tutorial para novatos, no te dir√° lo que tienes que hacer, te ayudar√° a hacer lo que quieres hacer. Tambi√©n puede ser un buen tutorial para Sanny builder. Trat√© de hacer un tutorial para cualquier persona, incluso si usted no sabe nada acerca de la programaci√≥n. Puede que quieras saltar directamente a la [parte II](mcp2.html), si crees que sabes lo suficiente sobre codificaci√≥n para empezar.

Con la codificaci√≥n de misiones (o scripting de misiones) puedes hacer tus propias misiones, tus propios mods o modificar los existentes (puedes encontrar muchos en este [Mission Mods](http://www.gtaforums.com/index.php?showforum=65) y este [Mission Coding](http://www.gtaforums.com/index.php?showforum=49) (y es donde puedes hacer preguntas sobre codificaci√≥n).

La codificaci√≥n de las misiones se hace con los archivos main.scm y script.img en la carpeta data\script de San Andreas. Main.scm es un archivo compilado, por lo que est√° hecho en un lenguaje que el ordenador puede entender f√°cilmente. Si abres main.scm con un editor hexadecimal se ve as√≠ por dentro:

```
D6 00 04 00 19 01 02 45 0E 4D 00 01 FE 3D 87 02 A6 00 02 45 0E
```

As√≠ que hay que descompilarlo para tenerlo en un formato legible para los humanos, entonces podremos programar en √©l. No necesitas hacer nada en el archivo script.img, el archivo es necesario, as√≠ que cada main.scm necesitar√° su propio script.img.

As√≠ que para descompilar main.scm necesitas [Sanny Builder](https://sannybuilder.com/). ¬°¬°¬°¬°¬°Hay muchos otros descompiladores pero si eres un principiante Sanny ser√° la mejor soluci√≥n de codificaci√≥n, porque se actualiza continuamente, tiene soporte adecuado, es r√°pido, es colorido üòè!!!!! Y el tutorial est√° hecho con y para [Sanny Builder](https://sannybuilder.com/). 
Sanny Builder es cercano al San Andreas Mission Builder de Barton Waterduck pero debido a que la sintaxis es diferente aconsejo encarecidamente usar [Sanny Builder](https://sannybuilder.com/) pero si quieres desobedecer, aqu√≠ est√° : [Barton's Waterduck Mission Builder](http://www.kostelecky.com/craig/SAMB033.rar) -La versi√≥n de Craig, gracias a √©l.

Algo muy importante, para obtener informaci√≥n, la Ayuda de Sanny Builder, est√° llena de informaci√≥n muy √∫til y siempre disponible (como offline).

Inicie el ejecutable y siga el proceso de instalaci√≥n habitual. Luego vaya primero a Herramientas-opciones (o presione F10). En la pesta√±a general marque Mostrar progreso, Mostrar informe, Mostrar advertencia, Carga r√°pida del juego, Comprobaci√≥n de condiciones, Comprobaci√≥n de rangos, Escribir opcodes, tal vez Reemplazar n√∫meros de misi√≥n, Insertar nombres de misi√≥n originales, A√±adir informaci√≥n extra a SCM. Explicar√© m√°s tarde por qu√© los necesitamos.

As√≠:

:::l
![](mcp1/rdfgbh3.png)
:::

Cierre Sanny Builder y vuelva a abrirlo.

Hay muchos otros buenos tutoriales que deber√≠an ayudarte: [YeTi's SA Coding Tutorial Part One](http://www.gtaforums.com/index.php?showtopic=214055&view=findpost&%20%20p=3163789), [YeTi's SA Coding Tutorial Part Two](http://www.gtaforums.com/index.php?showtopic=214321&view=findpost&%20%20p=3167813), [Y_less quick tuto](http://www.gtaforums.com/index.php?showtopic=232687&view=findpost&%20%20p=3498916), esos son para San Andreas. Los tutoriales de Vice City tambi√©n pueden ayudarte: [Alex "Y_Less" Cole's n00b guide to coding](http://www.gtaforums.com/index.php?showtopic=169526&view=findpost&%20%20p=2352371), [littleguna's VC Coding Tutorial](http://www.gtaforums.com/index.php?showtopic=156033&view=findpost&%20%20p=2137684), [YeTi's VC Coding Tutorial Part One](http://www.gtaforums.com/index.php?showtopic=189876&view=findpost&%20%20p=2794115), [YeTi's VC Coding Tutorial Part Two](http://www.gtaforums.com/index.php?showtopic=191001&view=findpost&%20%20p=2819222). El [archivo de tutoriales de GTAforums](http://www.gtaforums.com/index.php?showtopic=141222&view=findpost&%20%20p=1834859). Puedes encontrar m√°s en este foro aqu√≠. 
Puedes visitar la categor√≠a [GTAmodding.com](http://www.gtamodding.com/) y particularmente [Mission scripting](http://www.gtamodding.com/index.php?title=Category:Mission_Scripti%20%20ng).

### 1. Arquitectura de main.scm

As√≠ que ahora copia el main.scm original y el script.img a una nueva carpeta de trabajo de codificaci√≥n (o donde quieras: escritorio, Mis documentos, papelera de reciclaje, los documentos de tu madre üòÅ ....) que hayas creado y √°brela (haz copias de seguridad !!!!! como hacer de inmediato una nueva carpeta para el main.scm original y el script.img) haciendo clic en abrir y seleccionando Archivo SCM (*.scm). Podr√°s ver c√≥mo se va llenando la barra de progreso (por eso hemos truncado la opci√≥n "Mostrar progreso").

#### Secci√≥n de definiciones:

Y obtendr√° algo as√≠:

```sb3
// This file was decompiled using sascm.ini published by Seemann (http://sannybuilder.com/files/SASCM.rar) on 13.10.2007
DEFINE OBJECTS 389
DEFINE OBJECT SANNY BUILDER 3.03     
DEFINE OBJECT INFO                     // Object number -1
DEFINE OBJECT KEYCARD                  // Object number -2
DEFINE OBJECT AD_FLATDOOR              // Object number -3
DEFINE OBJECT KB_BANDIT_U              // Object number -4
etc...
```

Como puedes ver define los objetos que se usar√°n en el main.scm, hay 389 de ellos como se dice arriba (DEFINE OBJECTS 389). Para a√±adir un objeto s√≥lo tienes que ir al √∫ltimo y a√±adir el tuyo.

M√°s abajo (mucho m√°s abajo) puedes encontrar esto:

```sb3
DEFINE MISSIONS 135
DEFINE MISSION 0 AT @INITIAL
DEFINE MISSION 1 AT @INITIL2
DEFINE MISSION 2 AT @INTRO
DEFINE MISSION 3 AT @NONE
etc...
```

Esto define las misiones, hay 135 de ellas como se dice arriba (`DEFINIR MISIONES 135`). Si usas los archivos del mod de Xbox, Playstation 2, o [jarjar 2-player](http://www.gtaforums.com/index.php?showtopic=208450&view=findpost&%20%20p=3069233) puedes ver que hay m√°s misiones que son de consola para 2 jugadores pero eso es otra historia...\

```sb3
DEFINE EXTERNAL_SCRIPTS 78 // Use -1 in order not to compile AAA script
DEFINE SCRIPT PLAYER_PARACHUTE AT @PLCHUTE // 0
DEFINE SCRIPT PARACHUTE AT @PARACH // 1
DEFINE SCRIPT BCESAR2 AT @BCESAR2 // 2
DEFINE SCRIPT BCESAR3 AT @COKEC // 3
DEFINE SCRIPT SLOT_MACHINE AT @BANDIT // 4
etc...
```

Esto define los scripts externos que son scripts (programas) que se activan cuando se necesitan, hay 78 de ellos como se dijo arriba. Esto es lo que maneja el Paraca√≠das (jugador -`PLAYER_PARACHUTE`- y no jugadores -`PARACHUTE`-), Juegos en el casino (`ROULETTE`, `VIDPOK` -Video Poker-, `BLACKJ` -Black Jack-, `WHEELO` -Wheel Of Fortune-, etc. ..), tiendas (`AMMU`, `TATTOO`, `BARBER`, `JUNKFUD`, `CARMOD`), y muchas otras cosas m√°s o menos raras (`DEBT`, `STRIPW`, `GF_SEX`, `HOME_BRAINS`...) M√°s adelante se habla de ellas.

Luego llegas a esas 2 l√≠neas:
```sb3
DEFINE UNKNOWN_EMPTY_SEGMENT 0

DEFINE UNKNOWN_THREADS_MEMORY 574
```

Que son indocumentados e in√∫tiles para m√≠ y para ti.

#### Secci√≥n del principio:

Ahora puedes ver algo como esto:
```sb3
{$VERSION 3.1.0027}
//-------------MAIN---------------
03A4: name_thread 'MAIN' 
016A: fade 0 time 0 
042C: set_total_missions_to 147 
030D: set_max_progress 187
0997: set_total_respect_points_to 1339
01F0: set_max_wanted_level_to 6
0111: set_wasted_busted_check_to 0
00C0: set_current_time_hours_to 8 current_time_minutes_to 0
04E4: unknown_refresh_game_renderer_at 2488.562 -1666.864
03CB: set_rendering_origin_at 2488.562 -1666.864 13.3757
062A: change_stat 165 to 800.0 // floating-point values
062A: change_stat 23 to 50.0 // floating-point values
062A: change_stat 21 to 200.0 // floating-point values
062A: change_stat 160 to 0.0 // floating-point values
0629: change_stat 181 to 4 // integer values
0629: change_stat 68 to 0 // integer values
0053: $PLAYER_CHAR = create_player #NULL at 2488.562 -1666.864 12.8757
etc...
```

Hasta **antes** de ah√≠ (utiliza la herramienta de b√∫squeda de Sanny y busca "mission 0"): 
```sb3
//-------------Mission 0---------------
// Originally: Initial 1
:INITIAL
03A4: name_thread 'INITIAL'
06C8: enable_riot 0
0004: $1515 = 0 
0005: $1500 = 5.0
etc...
```

Se llama la parte principal o la secci√≥n principal (y a veces con imprecisi√≥n llamada el hilo principal). En ella hay muchos peque√±os scripts que por ejemplo crean los c√≠rculos rojos que inician las misiones, lo que maneja las llamadas telef√≥nicas (`CELLFON` y otras cosas), lo que maneja los grilfriends, desde donde se dispara todo (se inicia), donde se agregan muchos muchos mods, y ah√≠ es donde vas a empezar a codificar. La secci√≥n principal se compone de muchos hilos peque√±os, pero comienza con el hilo principal. Y el hilo es (y es el √∫nico) creado por el propio motor, no tienes que usar el opcode que crea hilos `004F` (**create_thread**) como todos los dem√°s para ingnitarlo. No te preocupes, m√°s adelante se dedicar√° una parte entera a los hilos.

En la parte principal s√≥lo puedes usar variables locales (`0@`, `1@`, `2@`, etc...) hasta `31@` incluido, `32@` y `33@` son temporizadores locales internos (cuenta el tiempo transcurrido, lo veremos m√°s adelante) que tambi√©n puedes usar. Pero cualquiera por encima de `33@` no funcionar√° en la parte principal. Sanny no estar√° contento y te dir√° que la var. local est√° fuera de rango (por eso hemos activado la opci√≥n "Range check"). Puedes usar los globals que quieras pero su n√∫mero est√° limitado a 16383, deber√≠a ser suficiente.

#### Ahora la secci√≥n Misi√≥n:

Luego, despu√©s de:
```sb3
//-------------Mission 0---------------
// Originally: Initial 1
:INITIAL
03A4: name_thread 'INITIAL'
06C8: enable_riot 0
0004: $1515 = 0 
0005: $1500 = 5.0
etc...
```

Es la primera misi√≥n (que en realidad no es una misi√≥n porque se hace durante los primeros milisegundos de una nueva partida antes de la escena de corte y no da ning√∫n punto de misi√≥n). Esta misi√≥n en realidad crear coches aparcados, algunas armas recoger y otra cosa.

Despu√©s de llegar a la misi√≥n 1 (buscar la misi√≥n 1)
```sb3
//-------------Mission 1---------------
// Originally: Initial 2
:INITIL2
03A4: name_thread 'INITIL2'
0004: $3407 = 25 
0004: $3408 = 100
0004: $3409 = 250
etc...
```

Al igual que la misi√≥n 0 no es una misi√≥n, s√≥lo establece algunas variables igual al valor del n√∫mero, estacionado algunos coches de nuevo, activar algunas puertas y ascensores.

Luego la misi√≥n 2

```sb3
//-------------Mission 2---------------
// Originally: Intro
:INTRO
03A4: name_thread 'INTRO'
0050: gosub @INTRO_47 
00D6: if 
0112:   wasted_or_busted // mission only
004D: jump_if_false @INTRO_38 
0050: gosub @INTRO_9715
etc...
```

Esta es parte de la escena de introducci√≥n y la misi√≥n termina cuando llegas al c√≠rculo rojo en la casa de CJ (Jonhson House en Ganton).

Despu√©s est√°n los juegos de arcadas (*originalmente: Videojuego: Se arrastraron desde uranus; originalmente: Videojuego: Dualidad; originalmente: Videojuego: Go Go Space Monkey; originalmente: Videojuego: Let's get ready to BUMBLE; originalmente: Videojuego: p√≥ker*) y luego otras cosas como el billar (*originalmente: Pool*), el baile (originalmente: Lowrider (apuesta y baile)) y la misi√≥n Zero "hidden/bonus" (*originalmente: Beefy Baron*) y despu√©s de esas, la misi√≥n "Big smoke" (misi√≥n 11) que es la primera misi√≥n del juego. 

Y as√≠ hasta el final de la misi√≥n 134.
Si quieres a√±adir una misi√≥n, a√±√°dela despu√©s y def√≠nela en la secci√≥n de definir misi√≥n. (m√°s adelante).

#### Secci√≥n de script externo (√∫ltima secci√≥n)

Despu√©s de la √∫ltima misi√≥n puedes ver todos los script externos definidos empezando por el primero `PLAYER_PARACHUTE`:
```sb3
//-------------External script 0 (PLAYER_PARACHUTE)---------------
:PLCHUTE
03A4: name_thread 'PLCHUTE'
0247: request_model #GUN_PARA
:PLCHUTE_16
etc...
```
y despu√©s

```sb3
//-------------External script 1 (PARACHUTE)---------------
:PARACH
03A4: name_thread 'PARACH'
0004: $8275 = 0 
etc...
```

y as√≠ sucesivamente hasta el 77¬∫ script externo. Muchas cosas en los scripts externos, mirando el nombre del externo, a menudo se puede averiguar para qu√© se utiliza.

Lo m√°s importante por ahora es la parte principal.

Pero a√∫n sobre la arquitectura de main.scm, necesitas saber que algunas cosas est√°n limitadas en main.scm mira, [aqu√≠](http://www.gtaforums.com/index.php?showtopic=213017&view=findpost&%20%20p=3145932). Como puedes ver el tama√±o principal (la longitud de todos los hilos de la 'parte principal') est√° limitado a 200 000 bytes. Para saber que longitud tiene la 'parte principal' compila y mira el informe que te dice "MAIN size : XXXXXX b." y luego la misi√≥n m√°s grande "Largest Mission : XXXX b." y el Largest external script:

:::l
![](mcp1/retfyhj2.png)
:::

Por eso hemos puesto en la opci√≥n "Mostrar informe".

### 2. SCM despojado y Opcodes

#### A. SCM despojado

Para tus primeros proyectos de codificaci√≥n necesitas un main.scm que sea peque√±o (el original es s√≥lo hudge), y que no tenga cosas in√∫tiles para el scripting. Ganar√°s tiempo compilando y no tendr√°s que esperar a la primera escena. Y tambi√©n por los l√≠mites del scm, leer arriba. 

As√≠ que un scm despojado es un main.scm que no contiene ninguna misi√≥n, ning√∫n script externo, ning√∫n hilo principal. No contiene casi nada, s√≥lo crea el jugador. 

Puede encontrar uno en la carpeta /data/sa donde instal√≥ Sanny Builder bajo el nombre stripped.txt, parece extra√±o porque est√° escrito en la sintaxis de las clases de Sanny Builder, no le dir√© mucho sobre esto, encuentre m√°s en la ayuda de Sanny Builder. No porque no sea bueno porque creo que ser√° m√°s f√°cil aprender primero con el "m√©todo opcode".

Deber√≠as tener algo as√≠:

```sb3
{
	use macro (Ctrl+J) "headsa"
	to insert a file header      
}
{$VERSION 3.0.0000}
var
	$PLAYER_CHAR: Player
end // var
03A4: name_thread 'MAIN'
01F0: set_max_wanted_level_to 6 
0111: toggle_wasted_busted_check 0 
00C0: set_current_time_hours_to 8 minutes_to 0 
04E4: unknown_refresh_game_renderer_at 2488.56 -1666.84 
03CB: set_rendering_origin_at 2488.56 -1666.84 13.38 
0053: $PLAYER_CHAR = create_player #NULL at 2488.56 -1666.84 13.38 
01F5: $PLAYER_ACTOR = create_player_actor $PLAYER_CHAR 
07AF: $PLAYER_GROUP = player $PLAYER_CHAR group
0373: set_camera_directly_behind_player
01B6: set_weather 0 
0001: wait 0 ms 
087B: set_player $PLAYER_CHAR clothes_texture "PLAYER_FACE" model "HEAD" body_part 1 
087B: set_player $PLAYER_CHAR clothes_texture "JEANSDENIM" model "JEANS" body_part 2 
087B: set_player $PLAYER_CHAR clothes_texture "SNEAKERBINCBLK" model "SNEAKER" body_part 3 
087B: set_player $PLAYER_CHAR clothes_texture "VEST" model "VEST" body_part 0 
070D: rebuild_player $PLAYER_CHAR      
01B4: toggle_player $PLAYER_CHAR can_move 1 
016A: fade 1 time 0 
04BB: select_interior 0 
0629: change_integer_stat 181 to 4 
016C: restart_if_wasted_at 2027.77 -1420.52 15.99 angle 137.0 town_number 0 
016D: restart_if_busted_at 1550.68 -1675.49 14.51 angle 90.0 town_number 0 
0180: set_on_mission_flag_to $ONMISSION // Note: your missions have to use the variable defined here 
0004: $DEFAULT_WAIT_TIME = 250
03E6: remove_text_box 

// put your create_thread commands here



:MAIN_LOOP
0001: wait $DEFAULT_WAIT_TIME ms
00BF: $TIME_HOURS = current_time_hours, $TIME_MINS = current_time_minutes
0002: jump @MAIN_LOOP 


// put your mods (threads) here

//-------------Mission 0---------------
// put your missions here



//-------------External script 0---------------
// put your external scripts here
```

Probablemente has notado que no hay una secci√≥n de definici√≥n (`DEFINE OBJECTS`, `DEFINE MISSIONS`...) no hay nada de que preocuparse aqu√≠ porque no hay nada que necesite ser definido aqu√≠. Como se dice en el archivo si necesitas una cabecera (definir secci√≥n) presiona `CTRL`+`J` y selecciona headsa. Como en la imagen:

:::l
![](mcp1/jyfcgfhgcfgkk.png)
:::

Entonces entender√°s lo que te est√°s perdiendo:

```sb3
DEFINE MISSIONS 0
//DEFINE MISSION {ID} 0 AT {LABEL} @
DEFINE EXTERNAL_SCRIPTS 0 // Use -1 in order not to compile AAA script
//DEFINE SCRIPT {NAME}  AT {LABEL} @
DEFINE UNKNOWN_EMPTY_SEGMENT 0
DEFINE UNKNOWN_THREADS_MEMORY 0
```

Ahora puedes definir tus misiones y scripts externos.

Para poner los mods en este scm despojado necesitas poner tu comando de crear hilos (como `004F: create_thread @MYTHREAD`) donde dice : `// put your create_thread commands here`. Y ponga sus scripts despu√©s del `MAIN_LOOP` donde dice `// put your mods (threads) here`.

### B. Opcodes

Al principio de la mayor√≠a de las l√≠neas hay un n√∫mero de 4 d√≠gitos seguido de dos puntos como 0001:, 087B:, 016A: o 004E: como este 0605: en Sanny Builder. ¬°¬°¬°¬°¬°¬°¬°Estos se llaman opcodes !!!!!!! Y por eso hemos activado la opci√≥n "Write opcodes".

El opcode es el n√∫mero del comando (acci√≥n u operaci√≥n) que el motor del juego har√° con los par√°metros dados, todos los opcodes (excepto algunos) tienen un n√∫mero fijo de par√°metros.

Los par√°metros (tambi√©n llamados Params, o simplemente P1, P2, o %p ) son la entrada (n√∫meros, actores, veh√≠culos, cadenas de texto o cualquier tipo de variables) del comando. El constructor Sanny en la parte inferior izquierda le indica cu√°ntos par√°metros necesita un opcode:

:::l
![](mcp1/yrrgrg.png)
:::

Aqu√≠ hay dos ejemplos:

**simple**
```sb3
000A: 3@ += 1 // integer values
```

* `000A` : el opcode que realiza la operaci√≥n de sumar a los enteros.
* `3@` y 1 : son los par√°metros, este opcode tiene 2 par√°metros.

¬°¬°¬°As√≠ que esto obtiene la variable local (m√°s adelante) `3@` y le a√±ade uno, como si `3@` fuera igual a 1 ahora ser√° igual a 2 !!!

```sb3
0605: actor $PLAYER_ACTOR perform_animation_sequence "ATM" from_file "PED" 4.0 loop 0 0 0 0 6000 ms
```

* `0605` : el opcode del comando 
* `$PLAYER_ACTOR`, `"ATM"`, `"PED"`, `4.0`, `0`, `0`, `0`, `0`, `6000` : son los par√°metros, este opcode tiene 9 par√°metros. 

Este comando hace que un actor (personaje) existente `$PLAYER_ACTOR` realice la animaci√≥n `"ATM"` en el archivo de animaci√≥n `"PED"`, `4. 0` es la velocidad de reproducci√≥n de la animaci√≥n, el bucle 0 significa que no har√° la animaci√≥n de nuevo una vez, si el n√∫mero entero se establece en 1 el actor har√° el una y otra vez (bucle) hasta que se le dice que haga otra cosa (o destruir, o "libre" - cambiando a cualquier otro ped que se ve en el juego, opcode 01C2), entonces hay otros par√°metros que vamos a hablar m√°s adelante sobre ellos. El √∫ltimo es el tiempo durante el cual la animaci√≥n ser√° ejecutada por el motor, act√∫a como una espera y har√° que el motor deje de ejecutar el c√≥digo hasta ese momento.

Ahora para encontrar el opcode que necesitas tienes muchos lugares:

* Brillante nueva "Herramienta de b√∫squeda de opcode" en Sanny Builder. En el men√∫ Herramientas, herramientas del IDE, b√∫squeda de opcode. Aqu√≠ s√≥lo tienes que escribir lo que buscas y tendr√°s la oportunidad de encontrarlo. Hay una secci√≥n especial en la ayuda de Sanny Builder [aqu√≠](../editor/opcode-search-tool.md) en Te indica las teclas para copiar opcodes al portapapeles (opcode seleccionado = Enter), para ordenar opcodes, etc... Utiliza el opcodes.txt que es creado por Sanny y que tienes que reconstruir manualmente si cambias el sacm.ini.
* Original main.scm uncompiled (el m√°s __confiable__ la mayor√≠a de las veces -buena explicaci√≥n y si se usa aqu√≠ S√ç funciona-) ¬°¬°¬°¬°¬°Gracias a R* por hacer el juego !!!!! Gracias a Barton Waterduck, a los autores de Sanny Builder, y a muchos otros que ayudan a descompilar el scm, deber√≠an reconocerse (si tienen tiempo de leer esto). 
* [GTA:SA Opcodes](http://www.gtaforums.com/index.php?showtopic=194990&view=findpost&%20%20p=2888074) @ [GTAforums](http://www.gtaforums.com/) el lugar para opcodes y es muy bueno porque contiene buena descripci√≥n, explicaciones, y lo que el valor de un par√°metro hacer.
Gracias a todos los que postearon en el tema y a opius por iniciarlo.
* [http://sa-db.webtools4you.net/](http://sa-db.webtools4you.net/) LA base de datos interactiva, la mejor manera de usarla es buscar "nada" (solo clic en ir) tendr√°s todos los opcodes, y luego buscar con tu navegador de internet "encontrar en la p√°gina".

¬°¬°¬°Pero la mejor manera es consultarlos todos!!!
\
Las descripciones de los opcodes vienen de un archivo llamado sascm.ini que se encuentra en data\sa de la carpeta de instalaci√≥n de Sanny builder. La actualizaci√≥n de este archivo har√° nuevas descripciones o/y cambiar√° los par√°metros del opcode. 

* El sascm.ini original de Sanny Builder es muy bueno y le aconsejo que lo mantenga para el comienzo.
* Base de datos de opcode unificada de PLPynton aqu√≠, o el archivo aqu√≠.. Si no quieres usar el archivo puedes encontrar algunos opcodes en el tema.
- Esta "base de datos" de opcodes tiene dos grandes ventajas, porque es muy completa y mantiene el orden original de los par√°metros, y una desventaja es que utiliza una sintaxis completamente diferente para la descripci√≥n. 
* Gracias a PLPynton por crearla.

El sascm.ini de Spaceeinstein aqu√≠. Utiliza la sintaxis que siempre se ha utilizado. Gracias a √©l. 
Para usar los archivos sascm.ini que se dan all√≠ tienes que compilar todos los archivos en los que est√°s trabajando cerrar Sanny builder (si est√° abierto) poner el archivo en la carpeta \data\sa en la carpeta de instalaci√≥n de Sanny Builder (hacer una copia de seguridad), abrir Sanny y descompilar los archivos. Cada vez que cambie el sascm.ini tiene que hacer esto.\
\
:::center
!!!!!!!!!!!! Importante !!!!!!!!!!!!\
\
Sobre los sascm.ini, no te l√≠es mucho con ellos, elige uno y qu√©date con √©l durante un tiempo. Cada vez que lo cambies tendr√°s que convertir todos tus archivos, o compilar y descompilar, pero tienes que tener cuidado con los par√°metros opcode, a veces se estropean al cambiar el sascm.ini y tendr√°s cuelgues y bugs (y ser√°n muy dif√≠ciles de encontrar).

Tambi√©n tendr√°s que volver a crear tus archivos opcodes.txt de lo contrario completar con F1 o en la b√∫squeda de Opcode. Para hacer esto vaya a Herramientas y "Hacer opcodes.txt". Mi consejo es que elija uno de ellos, o mantener el que se da en Sanny Builder, y no lo cambie hasta que tenga una buena raz√≥n para hacerlo.\
El tutorial est√° hecho con el sascm.ini dado en Sanny Builder.
:::

### 3. Par√°metros y variables

Hay diferentes tipos de par√°metros:

* __Integer__ (o int), tambi√©n llamados n√∫meros enteros como `1` o `-2` o `321` o cualquier otro.
* __Floats__ o valores de punto flotante, cualquier n√∫mero decimal como `1.2` o `-2372.3845`. M√°s sobre los n√∫meros de punto flotante.
* Un __actor__ como CJ (T√ö) que se almacena en la variable global `$PLAYER_ACTOR`, o el personaje de Ryder en el juego en `$SA_RYDER`, o el soldado que vigila la puerta de la base militar en la misi√≥n de Ryder se almacena en la variable local `@102`. La variable se llama "handle" cuando se sostiene (mantiene en memoria) un actor (o un coche), ser√° el nombre del objeto siguiente, la forma de handle donde se agarra.
* _Un "coche"__ (en la codificaci√≥n todo veh√≠culo -volador, flotante, helic√≥ptero- es un coche), como a menudo el coche que est√°s conduciendo `$PLAYER_CAR`, pues como los actores se almacenan en variables locales o globales. Al igual que los actores, cuando se almacena un coche en una variable se llama entonces un mango.
* __Model identifier__ como `#CHEETAH`, `#ARMY`, `#PARACHUTE`, el modelo 3D de un coche, un actor, un objeto. Se refiere a los archivos dff que se almacenan en gta3.img. Y este modelo es necesario cargarlo (ponerlo en memoria) para crear coches, actores u objetos.
* __Cadenas de texto cortas__ (8 bytes de longitud) como `'STRAP_4'` o `'MTIME3'` que son palabras letradas que el motor "lee" al pasar y buscar el archivo -o la l√≠nea del archivo- nombrado. Son siempre de 7 caracteres como m√°ximo. Se refiere a nombres de archivos como gxt, archivos de animaci√≥n, archivos IPL (garaje interior, etc...) o nombres de hilos, entradas de texto (del archivo gxt), animaciones (la animaci√≥n en el archivo de animaci√≥n), y muchas otras cosas. No te preocupes mucho es s√≥lo un formato para el par√°metro.
* __Cadenas de texto largas__ (16 bytes o m√°s de longitud,) como `"ATM"` o `"JEANSDENIM"` . Es exactamente lo mismo que lo anterior, es s√≥lo un formato para el par√°metro, pero √©ste puede manejar palabras m√°s largas (el ReadMe de Sanny dice hasta 255 caracteres) y se utiliza incluso si la palabra es m√°s corta que 7 caracteres. Puedes encontrar m√°s explicaciones sobre las cadenas (tanto cortas como largas en este mensaje de Seemann)
* __Etiquetas__ como `MAIN_4059` que se usan s√≥lo en los saltos (y gosubs) porque esos opcodes (`0002: jump @`; `004D: jump_if_false @`; `0050: gosub @` ; o `004F: create_thread @` ) hacen que el motor se desplace hasta (salte en) la etiqueta indicada. La etiqueta se representa con : delante.

ejemplo:
```sb3
:label
0001: wait 0 ms
0002: jump @label
```

Las variables son un peque√±o trozo de memoria en el que se almacena algo. Una vez que la has definido, el motor la recordar√° hasta que se cambie o se borre, incluso despu√©s de guardar la variable seguir√° presente (guardar guarda toda la variable y la recarga cuando se carga el juego). Al igual que cuando creas un actor le asignas una variable, que ser√≠a como su nombre. En ellas puedes poner cualquier cosa, enteros, flotantes, actores, veh√≠culos, cadenas de texto (formato especial).

Luego hay dos grandes familias de variables:

* Variables globales : que se representan empezando por `$` . Las globales pueden ser usadas en cualquier parte del main.scm. Si se establece algo en la parte principal en la misi√≥n seguir√° siendo accesible e igual a algo. Ejemplos : `$ONMISSION`, `$PLAYER_ACTOR`.

* Variables locales : Las locales se representan con una `@` al final como `1@`, `2@`. Cada hilo (creado por `004F`) tiene su propio conjunto de locales, como `0@` en `:thread_A` no es lo mismo que `0@` en `:thread_B`. En la parte principal no se puede utilizar m√°s de `33@` (que es un temporizador local, ver downer para m√°s detalles). Pero en la misi√≥n se puede utilizar hasta `1024@`.

Temporizadores locales: `32@` y `33@`, son temporizadores locales internos, cuentan el n√∫mero de milisegundos que han transcurrido. Por ejemplo, si lo pones a 0, esperas 10 segundos y miras cu√°nto vale `32@` o `33@`, encontrar√°s 10 000. Si lo pones a 1 000 y esperas 5 segundos antes de mirar, entonces ser√° igual a 6 000 ms.

Cadenas de texto cortas y largas : las cadenas de texto se almacenan en un formato especial de variables. Las cadenas de texto est√°n resaltadas en rojo en Sanny. Hay 4 tipos de variables de texto: 2 para cadenas cortas (8 bytes, "STRING") y 2 para cadenas largas (16 bytes, "LONGSTRING"):

#### Para las cadenas cortas

- una cadena de texto global como : `s$1169` o `s$ACTOR_SPEECH_GXT_REFERENCE` que siempre puede ser accesible como otros globales.
- una cadena de texto local como : `170@s` o `5@s` que s√≥lo son accesibles en su propio hilo.

#### Para cadenas largas

- una cadena de texto global: `v$1225` o `v$MYLONGSTRING`, igual que la cadena corta pero para las largas...
- una cadena larga local: `28@v`, igual que las cortas...

PERO ojo, para `ejemplo0@s` sustituye a `0@` si existe y viceversa, es la misma variable pero solo est√°n en un formato diferente.

Y quiero recordarte (o decirte) que un entero y un float son diferentes para el motor del juego ( 1 no es lo mismo que 1.0 !!!!!). Si el opcode necesita un float y tu le das un int (y viceversa) el juego se estrellar√°. 
Puedes convertir un entero en un float con el opcode `008D`. Y convertir un float en un entero con `008C`.

### 4. Hilos y etiquetas

Un hilo es una pieza de c√≥digo "independiente" (algo que no necesita nada m√°s que a s√≠ mismo). Es como un programa por s√≠ mismo (como una ventana abierta en tu sistema operativo -como Microsoft Windows-). As√≠ que para usar (jugar en el juego) tus futuros mods (programas, ventanas) tendr√°s que crear uno nuevo. Lo que se hace con `004F` como:
```sb3
004F: create_thread @MYTHREAD
```

Threads are like fire in a world with matches (ligther, silex, well you cannot "make" fire you need to ingnite it from another fire -like the Olympic Flame never switched off between 2 olympic games). You must create a new one from an existing one. As I said before the main thread is created by the engine itself. The `'MAIN'` thread will lite your fire. So if it's the only existing thread (like in a stripped main.scm) you'll need to create your thread in the main thread BEFORE it gets switched off by (before this line):
```sb3
004E: end_thread
```

Si se utiliza un archivo m√°s grande (como orignal main.scm) s√≥lo tiene que crear donde hay muchos otros create_thread (`004F`) as√≠:

```sb3
004F: create_thread @ODDVEH
004F: create_thread @R3
004F: create_thread @GYM
004F: create_thread @SHOOT
004F: create_thread @BLOODR
004F: create_thread @HOTR
004F: create_thread @KICKS
004F: create_thread @PSAVE1
004F: create_thread @FLOW
004F: create_thread @HELP
004F: create_thread @COOLS
004F: create_thread @CRANES
004F: create_thread @BUY_PRO
etc...
```

No te preocupes por `00D7: create_thread_with_wasted_busted_check` porque su uso real es a√∫n desconocido. Utiliza `004F`, hay mejores formas de comprobar si el jugador est√° gastado o reventado (como el opcode condicional `0112`). Como Seeman nos dice m√°s adelante en el tema `004f` crea un hilo con 0 a 32 par√°metros adicionales, y `00d7` crea un hilo con 0 par√°metros adicionales. As√≠ que no te preocupes en absoluto por `00d7` porque `004f` hace lo mismo y como m√°s funcionalidades. (Mensaje de Seemann).

Como dije antes en cada hilo que has creado tienes para cada hilo su propio conjunto de locales desde `0@` hasta `33@` (`32@` y `33@` son temporizadores locales). Son bastante √∫tiles porque no chupan memoria.

Una etiqueta es un peque√±o trozo de hilo como `:MAIN_4059`. Piensa ahora en el hilo como un libro (no m√°s fuego üòß ) y las etiquetas ser√≠an p√°ginas de este libro. Entonces puedes entender f√°cilmente que un `jump` (o `jump_if_false`, o `gosub`) a una determinada etiqueta ser√≠a como un informe a una determinada p√°gina.

He hecho un ejemplo animado para que veas c√≥mo el motor del juego recorre las etiquetas:

:::l
![](mcp1/ASDFG2.gif)
:::

La l√≠nea roja representa el c√≥digo (el motor se llama a veces simplemente el c√≥digo, como el lector de c√≥digo) pasando por las etiquetas.

Como se puede ver el hilo `etiqueta` se crea a partir de otro hilo. Puedes ver el c√≥digo "separ√°ndose" en dos, uno va al hilo reci√©n creado `label`, otro sigue haciendo el hilo que sol√≠a hacer, y se termina (pero podr√≠a seguir haciendo cosas si no lo terminas). 
Pasa por `:label` hasta `:label_100`. En `:label_100` puedes ver que hay un "if" con una condici√≥n en √©l, seguido de un jump_if_false, esto significa que si `$PLAYER_ACTOR` est√° cerca de la posici√≥n en 3 dimensiones `2498.0 -1711.0 1015.0` el c√≥digo puede pasar y saltar a `:label_200`.
As√≠ que el c√≥digo sigue haciendo un bucle en `:label_100` hasta que se cumpla la condici√≥n.
Entonces salta a `:label_200` sin pasar por `:label_150`, yendo directamente a `:label_200`. Hace lo que tiene que hacer en `:label_200` (que es poner las coordenadas 3D del actor `$PLAYER_ACTOR` en las variables `$X_COORD $Y_COORD $Z_COORD`). Luego salta a `:label_150`, espera 1minuto y vuelve a saltar a :label_100 y comprueba de nuevo si el jugador est√° cerca del punto. Y lo har√° todo de nuevo y de nuevo y de nuevo. No tienes que terminar un hilo si no lo necesitas, puede seguir comprobando algo para empezar. No olvides que tienes que hacer algo con tu hilo, mantenerlo en marcha o terminarlo pero no dejarlo libre.\ 
\
:::center
!!!!!!! Observe la espera (`0001: wait 0 ms`), porque es obligatorio !!!!!!!!!!!!!\
Cuando se hace un bucle hay que a√±adir una espera para que el motor no se atasque en √©l, y ni siquiera se bloquea, sino que simplemente se congela. La √∫nica forma de recuperarse de ello es forzar el cambio de resoluci√≥n (a veces es imposible hacerlo, y algunos port√°tiles tienen una tecla combinada) y terminar GTA:SA con el administrador de tareas.

Si no sabes realmente si debes a√±adir una espera o no simplemente a√±ade "espera 0 ms", porque la mayor√≠a de las veces no ver√°s la diferencia en absoluto, y sobre todo cualquier cosa por debajo de 500 ms no la notas.
:::

### 5. "If "s y opcodes condicionales

Lo que se llama un `if` es una estructura de c√≥digo que comprueba si algo est√° sucediendo. Como por ejemplo\
si\
Me muero\
si no, entonces --> terminar√© mis mods\
--> le das todos mis mods inacabados a la comunidad GTA

En Sanny Builder se ver√≠a as√≠
```sb3
00D6: if
    0118: actor $PLAYER_ACTOR dead
004D: jump_if_false @finishmods
0002: jump @givemods
```

El motor viajar√° directamente si el algo comprobado sucede pero saltar√° a otra etiqueta si no sucede (`004D: jump_if_false @`). El algo (que podr√≠a o no suceder) del if se llama condici√≥n, para ellos se usa el opcode de las condicionales !!!!!!! Y puedes poner hasta siete condiciones en una si !!!!!!! (S√© que est√°s como "S√≠, estoy tan feliz, que realmente ha cambiado mi vida !!!!!!...." pero...).

Entonces hay dos tipos de condiciones m√∫ltiples si:

Cuando tienes m√∫ltiples condiciones puedes querer que __todas__ las condiciones sean verdaderas (sucede) o que __al menos una de ellas__ sea verdadera.

<ul>
<li>
`if and` es cuando quieres que todas las condiciones sean verdaderas para pasar como:
```sb3
00D6: if and
    08AB: external_script 38 (BAR_STAFF) loaded
    08AB: external_script 37 (CUSTOMER_PANIC) loaded
    08AB: external_script 28 (BAR_AMBIENCE) loaded
    08AB: external_script 35 (DABCE) loaded
004D: jump_if_false @INTMAN_1080
```
As√≠ que aqu√≠ puedes ver que si todos esos scripts externos est√°n cargados pasa pero entonces si alguno de ellos no lo est√° saltar√° a la etiqueta `:INTMAN_1088`. Puedes pensar que si esto __y__ esto __y__ esto est√° bien entonces pasa si no salta porque es falso.
</li>
<li>
`if or` es cuando s√≥lo se necesita una de esas condiciones para que pase como:
```sb3
00D6: if or
    03B0: garage 38 'BODLAWN' door_open
    03B0: garage 37 'MODLAST' door_open
    03B0: garage 28 'MDSSFSE' door_open
    03B0: garage 35 'VECMOD' door_open
004D: jump_if_false @MAIN_4336
```
Aqu√≠ es si alguna de estas puertas est√° abierta sigue adelante pero si no salta a `:MAIN_4336`. Puedes pensarlo como si pasa esto o esto __o__ esto __o__ pasa pero si no hay ninguna que sea verdadera salta porque es falsa. 
</li>
</ul>

`and` y `or` son llamados operadores l√≥gicos.

F√≠jate que no tendr√≠a sentido a√±adir un or o un and en una sola condici√≥n if. ¬°¬°¬°¬°¬°Solo una condici√≥n, simplemente no puedes usarlos, solo un elemento !!!!! Y a sanny builder no le gustar√° ni te dir√° "hey no hagas eso".

Por otro lado no te olvides de poner un o un y a tus multiples condiciones ifs, porque sanny no te dir√° nada, puede que el juego no se cuelgue, pero seguro que no funcionar√° bien, ya sea y o o, pero tienes que poner algo en las multiples condiciones ifs.

Puedes negar todos los opcodes condicionales. Negar es como a√±adir un not en las condiciones. Si el actor no est√° muerto, o si el actor no est√° conduciendo. Entonces significa que para pasar a trav√©s de √©l, la condici√≥n tiene que devolver false (no sucede). Puedes negar opcodes condicionales en condiciones mutlpiple si tambi√©n. Para negar un opcode condicional tienes que poner un 8 en el primer d√≠gito en lugar de 0, como:

```sb3
00D6: if
    02E0: is_char $PLAYER_ACTOR aggressive
003D: jump_if_false @labelX
```
no negado


```sb3
00D6: if
    82E0: not is_char $PLAYER_ACTOR aggressive
003D: jump_if_false @labelX
```
negado.

No tienes que escribir `not` el compilador (sanny) no lo mirar√°, y lo escribir√° al volver a compilar. Pero puedes escribir `not` si quieres, lo hace m√°s claro y f√°cil de trabajar. Pero realmente TIENES que escribir el 8 en el opcode.

Cuando niegas todas las condiciones en un "if de condiciones m√∫ltiples", tienes que cambiar el operador l√≥gico.

```sb3
00D6: if or
    02E0: is_char $PLAYER_ACTOR aggressive
    0457: is_player $PLAYER_CHAR aiming_at_actor 11@
    0457: is_player $PLAYER_CHAR aiming_at_actor 14@
    0457: is_player $PLAYER_CHAR aiming_at_actor 15@
    0457: is_player $PLAYER_CHAR aiming_at_actor 16@
    0457: is_player $PLAYER_CHAR aiming_at_actor 17@
003D: jump_if_false @labelY
0002: jump @labelZ
```

```sb3
00D6: if and
    82E0: not is_char $PLAYER_ACTOR aggressive
    8457: not is_player $PLAYER_CHAR aiming_at_actor 11@
    8457: not is_player $PLAYER_CHAR aiming_at_actor 14@
    8457: not is_player $PLAYER_CHAR aiming_at_actor 15@
    8457: not is_player $PLAYER_CHAR aiming_at_actor 16@
    8457: not is_player $PLAYER_CHAR aiming_at_actor 17@
003D: jump_if_false @labelZ
0002: jump @labelY
```

Esos dos hacen lo mismo:

* el primero comprueba si el jugador es agresivo `or` apunta al actor `14@` `or` apunta al actor `15@` `or` apunta al actor `16@` `or` apunta al actor `17@` , si alguno de ellos es verdadero el c√≥digo pasa y salta a labelz. 
* El segundo comprueba si el jugador no es agresivo `and` no apunta al actor `14@` `and` no apunta al actor `15@` `and` no apunta al actor `16@` `and` no apunta al actor 17@`, si alguno de ellos es falso (si el jugador apunta) el c√≥digo saltar√° porque es falso a labelz. 

A veces cuando se busca alg√∫n mods bonito se puede encontrar:
```sb3
00D6: if 0
```
o
```sb3
00D6: if 5
```
o
```sb3
00D6: if 21
```
o
```sb3
00D6: if 25
```

No te preocupes es lo mismo, compila, luego descompila y tendr√°s el operador l√≥gico escrito. Y por eso hemos habilitado la "Comprobaci√≥n de condiciones", para poder usar and & or y que Sanny los escriba al descompilar. 
```txt
if 0 = if

if x = if and 

if 2x = if or 
```

Lee [Condiciones en la Ayuda de Sanny Builder](../coding/conditions.md) o/y [aqu√≠](http://www.gtaforums.com/index.php?showtopic=134970&view=findpost&%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20p=2083394) (Es para Vice City pero es lo mismo, s√≥lo ignora "?") para m√°s explicaciones.

Puede que quieras hacer construcciones de condiciones de alto nivel, lee [Conditions in Sanny Builder's Help](../coding/conditions.md), lo explica bastante bien. Las construcciones de bajo nivel pueden ser buenas por ahora, y de todas formas se puede hacer alto nivel con bajo nivel.

:::center
PERO recuerda bien cuando hagas "if", a√±ade esperas donde sepas que el c√≥digo va a hacer un bucle de vuelta. Y una espera 0 nunca es un problema porque en realidad espera por 0 milisegundo que es bastante corto !!!!!!! pero dejar√° tiempo al motor haciendo otras cosas. Vuelve a mirar el ejemplo animado de las etiquetas para entender d√≥nde necesitar√°s absolutamente esperar.
:::

### 6. Solicitar modelos, cargarlos, crearlos y luego liberarlos para liberar memoria

¬°¬°¬°¬°¬°¬°Cuando se crea un actor (con el opcode `009A`), un coche (con el op `00A5`), un objeto (con el op `0107`), o se le da un arma a un actor hay que solicitar el modelo dff antes y cargarlo, y comprobar que est√° cargado de verdad !!!!!!

Como: 
```sb3
:GSHAUTO_564 
0001: wait 0 ms 
0247: request_model #HFYBE
0247: request_model #POLICE_BARRIER 
0247: request_model #BMYBOUN 
0247: request_model #WMYBOUN 
0247: request_model #DESERT_EAGLE 
0247: request_model #MP5LNG 0247: request_model #CHEETAH
0247: request_model #TURISMO 
038B: load_requested_models 
00D6: if and
    0248: model #HFYBE available
    0248: model #BMYBOUN available
    0248: model #WMYBOUN available
    0248: model #DESERT_EAGLE available
    0248: model #MP5LNG available
004D: jump_if_false @GSHAUTO_564 
00D6: if and
    0248: model #POLICE_BARRIER available
    0248: model #CHEETAH available
    0248: model #TURISMO available
004D: jump_if_false @GSHAUTO_564 
009A: 11@ = create_actor 4 #WMYBOUN at 561.0 -1252.0 17.0 
009A: 14@ = create_actor 4 #BMYBOUN at 552.0 -1257.0 17.0 
009A: 15@ = create_actor 5 #HFYBE at 567.0 -1290.0 17.5 
009A: 16@ = create_actor 4 #WMYBOUN at 561.0 -1255.0 17.0 
009A: 17@ = create_actor 4 #BMYBOUN at 553.5 -1260.0 17.0 
009A: 18@ = create_actor 4 #WMYBOUN at 569.0 -1292.0 17.5 
009A: 19@ = create_actor 4 #BMYBOUN at 569.0 -1288.0 17.5 
0107: 9@ = create_object #POLICE_BARRIER at 560.0 -1254.0 17.0 
00A5: 0@ = create_car #TURISMO at 1793.4956 -1907.4233 12.3992 
00A5: 1@ = create_car #CHEETAH at 1793.4956 -1907.4233 12.3992 
01B2: give_actor 11@ weapon 29 ammo 900 // Load the weapon model before using this 
01B2: give_actor 14@ weapon 29 ammo 900 // Load the weapon model before using this 
01B2: give_actor 15@ weapon 24 ammo 300 // Load the weapon model before using this 
01B2: give_actor 16@  weapon 29 ammo 900 // Load the weapon model before using this 
01B2: give_actor 17@ weapon 29 ammo 900 // Load the weapon model before using this 
01B2: give_actor 18@  weapon 29 ammo 900 // Load the weapon model before using this
01B2: give_actor 19@ weapon 29 ammo 900 // Load the weapon model before using this
```

Mira c√≥mo primero solicito los modelos (con `0247: request_model`) y luego los cargo (con `038B: load_requested_models`). Luego comprueba si todos los modelos solicitados est√°n disponibles para su uso (con `0248`). Si alguno de ellos est√° cargado salta de nuevo a la parte superior y vuelve a solicitarlos y a cargarlos. Si todos lo est√°n entonces crea el actor, las armas y el objeto. Puedes ver en el comentario (despu√©s de `//`) que `01B2` necesita que el modelo del arma est√© cargado antes de usarlo (para los n√∫meros de las armas mira en [help](../scm-documentation/sa/weapons.md) aqu√≠ o en la muy buena [Generic SA SCM Documentaion](http://www.gtaforums.com/index.php?showtopic=205020&view=findpost&%20%20p=3013775).

Una vez que hayas terminado de hacer lo que necesites con ellos (est√°n todos muertos, el jugador est√° demasiado lejos, no los quieres aqu√≠, etc...) transfi√©relos en los habituales ciclomotores y coches (como todos los ciclomotores y coches que ves en el juego) o destr√∫yelos (el objeto debe ser destruido):

```sb3
0108: destroy_object 9@ 
01C3: remove_references_to_car 1@ // Like turning a car into any random car 
01C3: remove_references_to_car 0@ // Like turning a car into any random car 
01C2: remove_references_to_actor 11@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 14@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 15@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 16@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 17@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 18@ // Like turning an actor into a random pedestrian 
01C2: remove_references_to_actor 19@ // Like turning an actor into a random pedestrian 
```

o
```sb3
0108: destroy_object 9@ 
00A6: destroy_car 0@ 
00A6: destroy_car 1@ 
009B: destroy_actor_instantly 11@ 
009B: destroy_actor_instantly 14@ 
009B: destroy_actor_instantly 15@ 
009B: destroy_actor_instantly 16@
009B: destroy_actor_instantly 17@
009B: destroy_actor_instantly 18@ 
009B: destroy_actor_instantly 19@
```

¬°¬°¬°¬°¬°¬°No apliques esos (01C2, 01C3, 0108, 00A6, 009B, etc...) a un actor, o a un coche que no existe (o cuando el manejador que deber√≠a tenerlo -como 0@, $car, etc...- no lo tiene), porque el juego se colgar√° mal!!!!!! 

Una vez que te hayas deshecho de los actores/coches/objetos (As√≠ que cuando hayas terminado de usarlos, y despu√©s de haberlos borrado, y s√≥lo despu√©s de eso), puedes liberar los modelos que has usado porque ya no los necesitar√°s.

```sb3
0249: release_model #WMYBOUN 
0249: release_model #BMYBOUN 
0249: release_model #HFYBE   
0249: release_model #POLICE_BARRIER
0249: release_model #DESERT_EAGLE
0249: release_model #MP5LNG 
0249: release_model #CHEETAH 
0249: release_model #TURISMO
```

En realidad podr√≠as haber liberado esos modelos antes, no necesitas que se carguen **despu√©s** de haber creado el objeto del juego (objeto, actor o veh√≠culo). Pero te aconsejo para empezar que liberes el modelo s√≥lo al final de tu script/misi√≥n cuando est√©s seguro de que ya no lo vas a necesitar. Esto te evitar√° que vuelvas al escritorio unas cuantas veces.

#### Conclusi√≥n

Bueno, esta parte era principalmente informaci√≥n, no hay mucho que hacer. Pero ahora sabes que un opcode es un n√∫mero de 4 d√≠gitos que hace cosas en el juego dependiendo de los par√°metros que le hayas dado. Sabes los diferentes tipos de par√°metros, lo que es una variable local y lo que es una global. Crear, nombrar y terminar hilos, y muchas otras cosas que necesitas saber para seguir adelante. Ahora que hablamos el mismo "idioma" podemos llegar a la codificaci√≥n real.
Vamos a la [parte II](mcp2.md)...

Divi√©rtete

<script src="../../js/main.min.js"></script>